Перем ИмяФайлаЛога;

Процедура ПередНачаломРаботыСистемы(Отказ)
	Отказ = Истина;
	
	ПараметрыЗапуска = ПолучитьСтруктуруПараметров(ПараметрЗапуска);
	Если НЕ ПараметрыЗапуска.Свойство("AddinPath") Тогда
		ВызватьИсключение "Не передан параметр с архивом компоненты AddinPath.";
	КонецЕсли;	 
	Если НЕ ПараметрыЗапуска.Свойство("LogPath") Тогда
		ВызватьИсключение "Не передан параметр с файлов лога LogPath.";
	КонецЕсли;	 
	
	ПутьККомпоненте = ПараметрыЗапуска.AddinPath;
	ИмяФайлаЛога = ПараметрыЗапуска.LogPath;
	
	Файл = Новый Файл(ПутьККомпоненте); 
	Если НЕ Файл.Существует() Тогда
		ТекстОшибки = СтрШаблон("Файл <%1> не существует.", ПутьККомпоненте);
		ЗаписатьВЛог(ТекстОшибки);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;	 
	
	ДД = Новый ДвоичныеДанные(ПутьККомпоненте);
	МестоположениеВнешнейКомпонентыДляСкриншотов = ПоместитьВоВременноеХранилище(ДД, Новый УникальныйИдентификатор);
	ИдентификаторВнешнейКомпонентыДляСкриншотов = "_" + СтрЗаменить(Новый УникальныйИдентификатор, "-", "");
	
	Попытка
		Подключение = ПодключитьВнешнююКомпоненту(МестоположениеВнешнейКомпонентыДляСкриншотов,
			ИдентификаторВнешнейКомпонентыДляСкриншотов, ТипВнешнейКомпоненты.Native); 
	Исключение
		ЗаписатьВЛог("Ошибка при подключении компоненты.");
		ЗаписатьВЛог(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
		
	Если Подключение Тогда
		ЗаписатьВЛог("Компонента уже установлена.");
		Возврат;
	Иначе	
		ЗаписатьВЛог("Будет выполнено подключение компоненты.");
	КонецЕсли;	 
	
	Если НЕ Подключение Тогда
		Попытка
			УстановитьВнешнююКомпоненту(МестоположениеВнешнейКомпонентыДляСкриншотов);
		Исключение
			ЗаписатьВЛог("Ошибка при подключении компоненты.");
			ЗаписатьВЛог(ОписаниеОшибки());
			Возврат;
		КонецПопытки;
	КонецЕсли;	 	
	
	ЗаписатьВЛог("Компонента успешно установлена.");
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтруктуруПараметров(Знач СтрокаПараметров)
	
	Результат = Новый Структура;
	
	МассивПараметров = СтрРазделить(СтрокаПараметров, ";");
	Для каждого ЭлементМассива Из МассивПараметров Цикл
		Позиция = Найти(ЭлементМассива, "=");
		Если Позиция > 0 Тогда
			Ключ     = Лев(ЭлементМассива, Позиция - 1);
			Значение = Сред(ЭлементМассива, Позиция + 1);
			Попытка
				Результат.Вставить(Ключ, Значение);
			Исключение
				Сообщить(СтрШаблон(
					("Не смог получить значение из строки запуска: %1."),
					Ключ
				));
			КонецПопытки;
		Иначе
			Если НЕ ПустаяСтрока(ЭлементМассива) Тогда 
				Попытка
					Результат.Вставить(ЭлементМассива, Истина);
				Исключение
					Сообщить(СтрШаблон(
						("Не смог получить значение из строки запуска: %1."),
						ЭлементМассива
					));
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;	 
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции	

&НаКлиенте
Процедура ЗаписатьВЛог(ТекстОшибки)
	ЗТ = Новый ЗаписьТекста(ИмяФайлаЛога,"UTF-8",,Истина); 
	ЗТ.ЗаписатьСтроку("" + ТекущаяДата() + ": " + ТекстОшибки); 
	ЗТ.Закрыть();
КонецПроцедуры 