
#Область ОписаниеПеременных

&НаКлиенте
Перем ШаблонКоординат;// Строка

&НаКлиенте
Перем ШаблонПодсказки;// Строка

&НаКлиенте
Перем ШаблонРамки;// Строка

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	#Если Не ВебКлиент Тогда
		Отказ = Истина;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбработатьСообщенияОтВнешнегоСайта" Тогда
		ОбработатьСообщенияОтВнешнегоСайтаНаСервере(Параметр);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьПроизвольныйТекстJS(Команда)
	Если ПустаяСтрока(ПолеТекста) Тогда
		ПоказатьПредупреждение( ,"Введите текст JS или используйте пример текста");
		Возврат;
	КонецЕсли;
	ПолеОтвета.ПолучитьЭлементы().Очистить();
	РаботаСБраузером.СообщениеВнешнемуСайтуПроизвольныйТекстJS(ПолеТекста);
КонецПроцедуры

&НаКлиенте
Процедура ПримерТекстаJS(Команда)
	ПолеТекста = ШаблонКоординат;
КонецПроцедуры


&НаКлиенте
Процедура ПоказатьРамку(Команда)
	ТекстСкрипта = СтрШаблон(ШаблонРамки, "Главное");
	РаботаСБраузером.СообщениеВнешнемуСайтуПроизвольныйТекстJS(ТекстСкрипта);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРамкуVA(Команда)
	ТекстСкрипта = СтрШаблон(ШаблонРамки, "Vanessa Interactive");
	РаботаСБраузером.СообщениеВнешнемуСайтуПроизвольныйТекстJS(ТекстСкрипта);
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаГлавное(Команда)
	
	Если Не ПустаяСтрока(ТекстПодсказки) Тогда
		Текст = ТекстПодсказки;
	Иначе
		Текст = "Показываю подсистему ""Главное"" ";
	КонецЕсли;
	
	ТекстСкрипта = СтрШаблон(ШаблонПодсказки, "Главное", Текст);
	РаботаСБраузером.СообщениеВнешнемуСайтуПроизвольныйТекстJS(ТекстСкрипта);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаVI(Команда)
	
	Если Не ПустаяСтрока(ТекстПодсказки) Тогда
		Текст = ТекстПодсказки;
	Иначе
		Текст = "Показываю подсистему ""Vanessa Interactive"" ";
	КонецЕсли;
	
	ТекстСкрипта = СтрШаблон(ШаблонПодсказки, "Vanessa Interactive", Текст);
	РаботаСБраузером.СообщениеВнешнемуСайтуПроизвольныйТекстJS(ТекстСкрипта);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьШагВVA(Команда)
	
	Ванесса = VanessaInteractiveКлиент.ПолучитьФормуVanessaAutomation();
	Ванесса.УстановитьТекстФичаФайла(ТекстФичаФайла(Сценарий));
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ИспользоватьКомпонентуVanessaExt", Ложь);
	Ванесса.УстановитьНастройкиПриЗапускеБезОткрытияФормы(СтруктураНастроек);
	
	Ванесса.ВыполнитьСценарииБезОткрытияФормы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьСообщенияОтВнешнегоСайтаНаСервере(ДанныеОтвета)
	
	ТипОтвета = ТипЗнч(ДанныеОтвета);
	Если ТипОтвета <> Тип("Структура") Тогда
		Сообщить(ДанныеОтвета);
		Возврат;
	КонецЕсли;
	
	Дерево = РеквизитФормыВЗначение("ПолеОтвета");
	НоваяСтрока = Дерево.Строки.Добавить();
	НоваяСтрока.Ключ = ТипОтвета;
	
	Для Каждого Свойство Из ДанныеОтвета Цикл
		НоваяПодчиненнаяСтрока = НоваяСтрока.Строки.Добавить();
		НоваяПодчиненнаяСтрока.Ключ = Свойство.Ключ;
		НоваяПодчиненнаяСтрока.Значение = Свойство.Значение;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ПолеОтвета");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстФичаФайла(Сценарий)
	Возврат Справочники.Web_Сценарии.ПолучитьТекстСценария(Сценарий);
КонецФункции

#КонецОбласти

#Область Инициализация

#Если Клиент или ВебКлиент Тогда
	
	ШаблонКоординат = "function pos(){
	|let r = theme('Главное').getBoundingClientRect();
	|return {
	|x: Math.round(r.left + r.width / 2) + window.screenLeft,
	|y: Math.round(r.top + r.height / 2) + window.screenTop + window.outerHeight - window.innerHeight,		
	|sw: screen.width, 
	|sh:screen.height,
	|}
	|}; 
	|return JSON.stringify(pos());";
	
	ШаблонПодсказки = "let p = {
	|text: '%2',
	|shape: 'rect',
	|timeout: 5000
	|};
	|let r = theme('%1').getBoundingClientRect();
	|let node = document.createElement('div');
	|node.style.position = ""absolute"";
	|node.style.left = r.left + 'px';
	|node.style.top = r.top + 'px';
	|	node.style.width = r.width + 'px';
	|node.style.height = r.height + 'px';
	|node.style.zIndex = 9999;
	|document.body.appendChild(node);
	|node.getBoundingClientRect = () => {
	|return {
		|left: r.left,
		|top: r.top,
		|width: r.width,
		|height: r.height
	|}
	|};
	|enjoyhint = new EnjoyHint();
	|enjoyhint.set([{
	|selector: node,
	|description: p.text,
	|showSkip: false,
	|shape: p.shape
	|}]);
	|enjoyhint.run();
	|$('.enjoyhint_close_btn').hide();
	|setTimeout(() => (enjoyhint.stop(), node.remove()), p.timeout);";
	
	ШаблонРамки = "{
	|let p = {
	|""name"": ""%1"",
	|""timeout"": ""5000"",
	|""size"": ""3"",
	|""color"": ""red""
	|};
	|let r = theme(p.name).getBoundingClientRect();
	|let coordinates = {left: r.x - 5, top: r.y - 5, width: r.width + 5, height: r.height + 5};
	|border(coordinates, p.timeout, p);
	|}";
	
#КонецЕсли

#КонецОбласти
