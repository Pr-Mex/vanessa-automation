#Область ОписаниеПеременных

Перем ФлагОтрицанияДляСообщения;
Перем ТекстСообщенийПользователю;

#КонецОбласти

//Служебная строка. Не удалять. #КонецОбласти ОписаниеПеременных

#Область ОбщееОписаниеПеременных

Перем СтатусыРезультатаТестирования;

#КонецОбласти

#Область ОбщиеПроцедурыИФункции

Функция ПолучитьТекстСообщенияПользователю(Текст)
	Если ТекстСообщенийПользователю <> Неопределено Тогда
		Значение = ТекстСообщенийПользователю[Текст];
		Если ЗначениеЗаполнено(Значение) Тогда
			Возврат Значение;
		КонецЕсли;	 
	КонецЕсли;	 

	Возврат Текст; 
КонецФункции	 


// { Plugin interface
Функция ОписаниеПлагина(ВозможныеТипыПлагинов) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("Тип", ВозможныеТипыПлагинов.Утилита);
	Результат.Вставить("Идентификатор", Метаданные().Имя);
	Результат.Вставить("Представление", "УтвержденияBDD");
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
КонецФункции

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	ТекстСообщенийПользователю = КонтекстЯдраПараметр.ТекстСообщенийПользователю;
КонецПроцедуры
// } Plugin interface

Функция ФорматДСО(Знач ДопСообщениеОшибки)
	Если ДопСообщениеОшибки = "" Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат Символы.ПС + ДопСообщениеОшибки;
КонецФункции

Процедура ВызватьОшибкуПроверки(Знач СообщениеОшибки)
	Префикс = "["+ СтатусыРезультатаТестирования.ОшибкаПроверки + "]";
	ТекстСообщения = ПолучитьТекстСообщенияПользователю("%1 %2");
	ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",Префикс);
	ТекстСообщения = СтрЗаменить(ТекстСообщения,"%2",СообщениеОшибки);
	ВызватьИсключение ТекстСообщения;
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункции

Функция Что(Знач ПроверяемоеЗначение, Знач Сообщение = "") Экспорт
	ЭтотОбъект.ПроверяемоеЗначение = ПроверяемоеЗначение;
	ЭтотОбъект.ДопСообщениеОшибки = Сообщение;
	ЭтотОбъект.ФлагОтрицания = Ложь;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Не_() Экспорт
	ЭтотОбъект.ФлагОтрицания = Истина;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ЭтоНе() Экспорт
	Возврат Не_();
КонецФункции

Функция Метод(Знач ИмяМетода, Знач ПараметрыМетода = Неопределено) Экспорт
	ЭтотОбъект.ИмяМетода = ИмяМетода;
	ЭтотОбъект.ПараметрыМетода = ПараметрыМетода;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ЭтоИстина() Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение = Истина) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("является истиной");
		ТекстСообщения = СформироватьСообщениеОбОшибке(Формат(ПроверяемоеЗначение, "БЛ=Ложь; БИ=Истина"),ТекстСообщения);
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ЕстьИстина() Экспорт
	Возврат ЭтоИстина();
КонецФункции

Функция ЭтоЛожь() Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение = Ложь) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("является ложью");
		ТекстСообщения = СформироватьСообщениеОбОшибке(Формат(ПроверяемоеЗначение, "БЛ=Ложь; БИ=Истина"), ТекстСообщения);
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ЕстьЛожь() Экспорт
	Возврат ЭтоЛожь();
КонецФункции

Функция Равно(Знач ОжидаемоеЗначение) Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение = ОжидаемоеЗначение) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("равно");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения + " (" + ОжидаемоеЗначение + ").");
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Больше(Знач МеньшееЗначение) Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение > МеньшееЗначение) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("больше");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения + " (" + МеньшееЗначение + ").");
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция БольшеИлиРавно(Знач МеньшееИлиРавноеЗначение) Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение >= МеньшееИлиРавноеЗначение) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("больше или равно");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения + " (" + МеньшееИлиРавноеЗначение + ").");
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Минимум(Знач МинимальноеЗначение) Экспорт
	Возврат БольшеИлиРавно(МинимальноеЗначение);
КонецФункции

Функция МеньшеИлиРавно(Знач БольшееИлиРавноеЗначение) Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение <= БольшееИлиРавноеЗначение) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("меньше или равно");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения + " (" + БольшееИлиРавноеЗначение + ").");
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Максимум(Знач МаксимальноеЗначение) Экспорт
	Возврат МеньшеИлиРавно(МаксимальноеЗначение);
КонецФункции

Функция Меньше(Знач БольшееЗначение) Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение < БольшееЗначение) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("меньше");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения + " (" + БольшееЗначение + ").");
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Заполнено() Экспорт
	Если Не ЛогическоеВыражениеВерно(ЗначениеЗаполнено(ПроверяемоеЗначение)) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("является заполненным");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения);
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Существует() Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение <> Неопределено И ПроверяемоеЗначение <> Null) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("существует");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения);
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ЭтоНеопределено() Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение = Неопределено) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("является неопределено");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения);
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ЕстьНеопределено() Экспорт
	Возврат ЭтоНеопределено();
КонецФункции

Функция ЭтоNull() Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение = Null) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("является NULL");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения);
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ЕстьNull() Экспорт
	Возврат ЭтоNull();
КонецФункции

Функция ИмеетТип(Знач ТипИлиИмяТипа) Экспорт
	ОжидаемыйТип = ?(ТипЗнч(ТипИлиИмяТипа) = Тип("Строка"), Тип(ТипИлиИмяТипа), ТипИлиИмяТипа);
	ТипПроверяемогоЗначения = ТипЗнч(ПроверяемоеЗначение);
	Если Не ЛогическоеВыражениеВерно(ТипПроверяемогоЗначения = ОжидаемыйТип) Тогда
		//эти строки нужны для автообработки - начало
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("тип");
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("имеет тип");
		//эти строки нужны для автообработки - окончание
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПолучитьТекстСообщенияПользователю("тип") + " - " + ТипПроверяемогоЗначения, ПолучитьТекстСообщенияПользователю("имеет тип") + " (" + ОжидаемыйТип + ").");
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Между(Знач НачальноеЗначение, Знач КонечноеЗначение) Экспорт
	Если Не ЛогическоеВыражениеВерно(ПроверяемоеЗначение >= НачальноеЗначение И ПроверяемоеЗначение <= КонечноеЗначение) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("между");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения + " (" + НачальноеЗначение + ") и (" + КонечноеЗначение + ").");
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Содержит(Знач ИскомоеЗначение) Экспорт
	Перем ИскомоеЗначениеНайдено;
	
	ТипПроверяемоегоЗначения = ТипЗнч(ПроверяемоеЗначение);
	Если ТипПроверяемоегоЗначения = Тип("Строка") Тогда
		ИскомоеЗначениеНайдено = Найти(ПроверяемоеЗначение, ИскомоеЗначение) > 0;
	ИначеЕсли ТипПроверяемоегоЗначения = Тип("Массив") Или ТипПроверяемоегоЗначения = Тип("ФиксированныйМассив") Тогда
		ИскомоеЗначениеНайдено = ПроверяемоеЗначение.Найти(ИскомоеЗначение) <> Неопределено;
	ИначеЕсли ТипПроверяемоегоЗначения = Тип("Структура") Или ТипПроверяемоегоЗначения = Тип("ФиксированнаяСтруктура")
			Или ТипПроверяемоегоЗначения = Тип("Соответствие") Или ТипПроверяемоегоЗначения = Тип("ФиксированноеСоответствие") Тогда
		Для каждого КлючЗначение Из ПроверяемоеЗначение Цикл
			ИскомоеЗначениеНайдено = КлючЗначение.Значение = ИскомоеЗначение;
			Если ИскомоеЗначениеНайдено Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипПроверяемоегоЗначения = Тип("СписокЗначений") Тогда
		ИскомоеЗначениеНайдено = ПроверяемоеЗначение.НайтиПоЗначению(ИскомоеЗначение) <> Неопределено;
	КонецЕсли;
	
	Если ИскомоеЗначениеНайдено = Неопределено Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("Утверждение <Содержит> не умеет работать с типом <%1>.%2");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",ТипПроверяемоегоЗначения);
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%2",ФорматДСО(ДопСообщениеОшибки));
		ТекстСообщения = ТекстСообщения;
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Если Не ЛогическоеВыражениеВерно(ИскомоеЗначениеНайдено) Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("содержит");
		ТекстСообщения = СформироватьСообщениеОбОшибке(ПроверяемоеЗначение, ТекстСообщения + " (" + ИскомоеЗначение + ").");
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ИмеетДлину(Знач ОжидаемаяДлина) Экспорт
	Перем ФактическаяДлина;
	
	ТипПроверяемоегоЗначения = ТипЗнч(ПроверяемоеЗначение);
	Если ТипПроверяемоегоЗначения = Тип("Строка") Тогда
		ФактическаяДлина = СтрДлина(ПроверяемоеЗначение);
	ИначеЕсли ТипПроверяемоегоЗначения = Тип("Массив") Или ТипПроверяемоегоЗначения = Тип("ФиксированныйМассив")
			Или ТипПроверяемоегоЗначения = Тип("Структура") Или ТипПроверяемоегоЗначения = Тип("ФиксированнаяСтруктура")
			Или ТипПроверяемоегоЗначения = Тип("Соответствие") Или ТипПроверяемоегоЗначения = Тип("ФиксированноеСоответствие")
			Или ТипПроверяемоегоЗначения = Тип("СписокЗначений") Тогда
		ФактическаяДлина = ПроверяемоеЗначение.Количество();
	КонецЕсли;
	
	Если ФактическаяДлина = Неопределено Тогда
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("Утверждение <ИмеетДлину> не умеет работать с типом <%1>.%2");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",ТипПроверяемоегоЗначения);
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%2",ФорматДСО(ДопСообщениеОшибки));
		ТекстСообщения = ТекстСообщения;
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Если Не ЛогическоеВыражениеВерно(ФактическаяДлина = ОжидаемаяДлина) Тогда
		//эти строки нужны для автообработки - начало
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("с длиной");
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("имеет длину");
		//эти строки нужны для автообработки - окончание
		ТекстСообщения = СформироватьСообщениеОбОшибке("<" +ПроверяемоеЗначение + "> " + ПолучитьТекстСообщенияПользователю("с длиной") + " " + ФактическаяДлина, ПолучитьТекстСообщенияПользователю("имеет длину") + " (" + ОжидаемаяДлина + ").");
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ВыбрасываетИсключение(Знач ФрагментИсключения = "") Экспорт
	Контекст = ПроверяемоеЗначение;
	СтрокаПараметры = "";
	Если ТипЗнч(ПараметрыМетода) = Тип("Массив") Тогда
		Для Сч = 0 По ПараметрыМетода.Количество() - 1 Цикл
			СтрокаПараметры = СтрокаПараметры + ",ПараметрыМетода[" + Сч + "]";
		КонецЦикла;
		СтрокаПараметры = Сред(СтрокаПараметры, 2);
	КонецЕсли;
	СтрокаДляВыполнения = "Контекст." + ИмяМетода + "(" + СтрокаПараметры + ")";
	
	ИсключениеВозникло = Ложь;
	Попытка
		Выполнить(СтрокаДляВыполнения);
	Исключение
		ИсключениеВозникло = Истина;
		ТекстИсключения = ОписаниеОшибки();
	КонецПопытки;
	
	Если Не ЛогическоеВыражениеВерно(ИсключениеВозникло И Найти(ТекстИсключения, ФрагментИсключения) > 0) Тогда
		//служебная строка. не удалять.
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("не"); 
		ТекстСообщения = ТекстСообщения + " ";
		
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("содержащее текст <%1>, а был текст <%2>");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",ФрагментИсключения); 
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%2",ТекстИсключения); 
		Часть2         = ТекстСообщения;
		
		ТекстСообщения = ПолучитьТекстСообщенияПользователю("Ожидали, что %1 %2 выбросит исключение %3 %4");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",СтрокаДляВыполнения); 
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%2",?(ФлагОтрицанияДляСообщения, " " + ПолучитьТекстСообщенияПользователю("не") + " ", " ")); 
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%3",?(ЗначениеЗаполнено(ФрагментИсключения), Часть2, "")); 
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%4",ФорматДСО(ДопСообщениеОшибки)); 
		
		ВызватьОшибкуПроверки(ТекстСообщения);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
КонецФункции

// { Helpers
Функция ЛогическоеВыражениеВерно(Знач ЛогическоеВыражение)
	Результат = ФлагОтрицания <> ЛогическоеВыражение;
	ФлагОтрицанияДляСообщения = ФлагОтрицания;
	ФлагОтрицания = Ложь;
	
	Возврат Результат;
КонецФункции

Функция СформироватьСообщениеОбОшибке(Знач ПроверяемоеЗначение, Знач Ожидание)
	ТекстСообщения = ПолучитьТекстСообщенияПользователю("Ожидали, что проверяемое значение (%1) %2 %3 %4");
	ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",ПроверяемоеЗначение); 
	ТекстСообщения = СтрЗаменить(ТекстСообщения,"%2",?(ФлагОтрицанияДляСообщения, " " + ПолучитьТекстСообщенияПользователю("не") + " ", " ")); 
	ТекстСообщения = СтрЗаменить(ТекстСообщения,"%3",Ожидание); 
	ТекстСообщения = СтрЗаменить(ТекстСообщения,"%4",ФорматДСО(ДопСообщениеОшибки)); 
	Возврат ТекстСообщения;
КонецФункции
// } Helpers

#КонецОбласти

//#КонецОбласти ПроцедурыИФункции

#Область ИнициализацияПеременных

СтатусыРезультатаТестирования = Новый Структура;
СтатусыРезультатаТестирования.Вставить("ОшибкаПроверки", "Failed");
СтатусыРезультатаТестирования.Вставить("НеизвестнаяОшибка", "Broken");
СтатусыРезультатаТестирования.Вставить("ТестПропущен", "Pending");
СтатусыРезультатаТестирования = Новый ФиксированнаяСтруктура(СтатусыРезультатаТестирования);

#КонецОбласти

//#КонецОбласти ИнициализацияПеременных