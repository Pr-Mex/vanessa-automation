
///////////////////////////////////////////////////
//Служебные функции и процедуры
///////////////////////////////////////////////////

&НаКлиенте
// контекст фреймворка Vanessa-Automation
Перем Ванесса;
 
&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов. Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт;
 
&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев. Существует, пока открыта форма Vanessa-Automation.
Перем КонтекстСохраняемый Экспорт;

// Делает отключение модуля
&НаКлиенте
Функция ОтключениеМодуля() Экспорт

	Ванесса = Неопределено;
	Контекст = Неопределено;
	КонтекстСохраняемый = Неопределено;

КонецФункции

&НаКлиенте
// Функция экспортирует список шагов, которые реализованы в данной внешней обработке.
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;
	
	ВсеТесты = Новый Массив;

	//описание параметров
	//Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,ОписаниеШага,ТипШага,Транзакция,Параметр);

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯЗапоминаюЗначениеПоляВБуферОбмена(Парам01)","ЯЗапоминаюЗначениеПоляВБуферОбмена","И я запоминаю значение поля ""ИмяПоля"" в буфер обмена","Помещает значение, типа текст или двоичные данные, указанного поля в буфер обмена. Необходимо включить использование компоненты VanessaExt. Поле ищется по заголовку.", "Прочее.Буфер обмена");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯЗапоминаюЗначениеПоляСИменемВБуферОбмена(Парам01)","ЯЗапоминаюЗначениеПоляСИменемВБуферОбмена","И я запоминаю значение поля с именем ""ИмяПоля"" в буфер обмена","Помещает значение, типа текст или двоичные данные, указанного поля в буфер обмена. Необходимо включить использование компоненты VanessaExt.  Поле ищется по имени.", "Прочее.Буфер обмена");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯВставляюТекстовоеЗначениеБуфераОбменаВПолеСИменем(Парам01)","ЯВставляюТекстовоеЗначениеБуфераОбменаВПолеСИменем(Парам01)","И я вставляю текстовое значение буфера обмена в поле с именем """" ","Помещает текстовое значение буфера обмена в указанное поле. Необходимо включить использование компоненты VanessaExt.", "Прочее.Буфер обмена");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯЗапоминаюЗначениеБуфераОбменаВПеременную(Парам01)","ЯЗапоминаюЗначениеБуфераОбменаВПеременную","И я запоминаю значение буфера обмена в переменную ""ИмяПеременной""","Запоминает значение буфера обмена в переменную. Необходимо включить использование компоненты VanessaExt.","Прочее.Буфер обмена");

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВТаблицеЯЗапоминаюЗначениеПоляВБуферОбмена(Парам01,Парам02)","ВТаблицеЯЗапоминаюЗначениеПоляВБуферОбмена","И в таблице ""ИмяТаблицы"" я запоминаю значение поля ""Заголовок поля"" в буфер обмена","Помещает значение, типа текст или двоичные данные, указанного поля в буфер обмена. Необходимо включить использование компоненты VanessaExt. Поле ищется по заголовку.", "Прочее.Буфер обмена");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВТаблицеЯЗапоминаюЗначениеПоляСИменемВБуферОбмена(Парам01,Парам02)","ВТаблицеЯЗапоминаюЗначениеПоляСИменемВБуферОбмена","И в таблице ""ИмяТаблицы"" я запоминаю значение поля с именем ""ИмяПоля"" в буфер обмена","Помещает значение, типа текст или двоичные данные, указанного поля в буфер обмена. Необходимо включить использование компоненты VanessaExt.  Поле ищется по имени.", "Прочее.Буфер обмена");
	
	Возврат ВсеТесты;
КонецФункции
	
&НаСервере
// Служебная функция.
Функция ПолучитьМакетСервер(ИмяМакета)
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектСервер.ПолучитьМакет(ИмяМакета);
КонецФункции
	
&НаКлиенте
// Служебная функция для подключения библиотеки создания fixtures.
Функция ПолучитьМакетОбработки(ИмяМакета) Экспорт
	Возврат ПолучитьМакетСервер(ИмяМакета);
КонецФункции



///////////////////////////////////////////////////
//Работа со сценариями
///////////////////////////////////////////////////

&НаКлиенте
// Функция выполняется перед началом каждого сценария
Функция ПередНачаломСценария() Экспорт
	
КонецФункции

&НаКлиенте
// Функция выполняется перед окончанием каждого сценария
Функция ПередОкончаниемСценария() Экспорт
	
КонецФункции



///////////////////////////////////////////////////
//Реализация шагов
///////////////////////////////////////////////////

&НаКлиенте
//И я запоминаю значение поля с именем """" в буфер обмена
//@ЯЗапоминаюЗначениеПоляСИменемВБуферОбмена(Парам01)
Функция ЯЗапоминаюЗначениеПоляСИменемВБуферОбмена(ИмяПоля) Экспорт
	
	ЯЗапоминаюЗначениеПоляВБуферОбмена(ИмяПоля, Истина);
	
КонецФункции

&НаКлиенте
//И я запоминаю значение поля """" в буфер обмена
//@ЯЗапоминаюЗначениеПоляВБуферОбмена(Парам01)
Функция ЯЗапоминаюЗначениеПоляВБуферОбмена(ИмяРеквизита, ИскатьПоИмени = Ложь) Экспорт
	
	Значение = Ванесса.ПолучитьЗначениеРеквизитаОткрытойФормыПоЗаголовку(ИмяРеквизита, ИскатьПоИмени);
	Ванесса.ПоместитьВБуферОбменаVA(Значение);
	
КонецФункции

&НаКлиенте
//И в таблице "ИмяТаблицы" я запоминаю значение поля "Заголовок поля" в буфер обмена
//@ВТаблицеЯЗапоминаюЗначениеПоляВБуферОбмена(Парам01,Парам02)
Функция ВТаблицеЯЗапоминаюЗначениеПоляВБуферОбмена(ИмяТаблицы,ИмяРеквизита, ИскатьПоИмени = Ложь) Экспорт
	ТаблицаФормы = Ванесса.НайтиТЧПоИмени(ИмяТаблицы);
	Если ИскатьПоИмени Тогда
		ПолеРеквизит = Ванесса.НайтиРеквизитТаблицы(ИмяТаблицы,ИмяРеквизита,ИскатьПоИмени,ТаблицаФормы);
	Иначе	
		ПолеРеквизит = Ванесса.НайтиРеквизитТаблицы(ИмяТаблицы,ИмяРеквизита,ИскатьПоИмени,ТаблицаФормы);
	КонецЕсли;	 
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ЯчейкаТаблицы", Истина);
	ДопПараметры.Вставить("ТаблицаФормы", ТаблицаФормы);
	Значение = Ванесса.ПолучитьПредставлениеДанныхЭлементаФормы(ПолеРеквизит, ДопПараметры);
	Ванесса.ПоместитьВБуферОбменаVA(Значение);
	
КонецФункции

&НаКлиенте
//И в таблице "ИмяТаблицы" я запоминаю значение поля с именем "ИмяПоля" в буфер обмена
//@ВТаблицеЯЗапоминаюЗначениеПоляСИменемВБуферОбмена(Парам01,Парам02)
Функция ВТаблицеЯЗапоминаюЗначениеПоляСИменемВБуферОбмена(ИмяТаблицы,ИмяРеквизита) Экспорт
	ВТаблицеЯЗапоминаюЗначениеПоляВБуферОбмена(ИмяТаблицы,ИмяРеквизита, Истина);
КонецФункции


&НаКлиенте
//И я вставляю текстовое значение буфера обмена в поле с именем ""Номер""
//@ЯВставляюТекстовоеЗначениеБуфераОбменаВПолеСИменем(Парам01)
Функция ЯВставляюТекстовоеЗначениеБуфераОбменаВПолеСИменем(Парам01) Экспорт
	
	Текст = Ванесса.БуферОбменаVA.Текст;
	ПолеРеквизит = Ванесса.НайтиРеквизитОткрытойФормыПоЗаголовку(Парам01, Истина, Истина, Тип("ТестируемоеПолеФормы"));
	Ванесса.ВвестиВПолеТекст(ПолеРеквизит, Текст);
	
КонецФункции

&НаКлиенте
//И я запоминаю значение буфера обмена в переменную "ИмяПеременной"
//@ЯЗапоминаюЗначениеБуфераОбменаВПеременную(Парам01)
Функция ЯЗапоминаюЗначениеБуфераОбменаВПеременную(ИмяПеременной) Экспорт
	Значение = Ванесса.БуферОбменаVA.Текст;
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Значение);
КонецФункции
