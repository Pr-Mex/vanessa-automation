///////////////////////////////////////////////////
//Служебные функции и процедуры
///////////////////////////////////////////////////

&НаКлиенте
// контекст фреймворка Vanessa-Automation
Перем Ванесса;

&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов. Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт;

&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев. Существует, пока открыта форма Vanessa-Automation.
Перем КонтекстСохраняемый Экспорт;

// Делает отключение модуля
&НаКлиенте
Функция ОтключениеМодуля() Экспорт

	Ванесса = Неопределено;
	Контекст = Неопределено;
	КонтекстСохраняемый = Неопределено;

КонецФункции

&НаКлиенте
// Функция экспортирует список шагов, которые реализованы в данной внешней обработке.
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;
	
	ВсеТесты = Новый Массив;
	
	//описание параметров
	//Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,ОписаниеШага,ТипШага,Транзакция,Параметр);
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯНажимаюСочетаниеКлавиш(Текст)",
		"ЯНажимаюСочетаниеКлавиш",
		"И я нажимаю сочетание клавиш ""Ctrl+O""",
		"Эмуляция нажатия клавиш при помощи внешней компоненты VanessaExt.
		|Допустимо передавать следующие имена клавиш:
			|<LBUTTON> (1)
			|<RBUTTON> (2)
			|<CANCEL> (3)
			|<MBUTTON> (4)
			|<XBUTTON1> (5)
			|<XBUTTON2> (6)
			|<BACK> (8)
			|<TAB> (9)
			|<CLEAR> (12)
			|<RETURN> (13)
			|<ENTER> (13)
			|<SHIFT> (16)
			|<CONTROL> (17)
			|<CTRL> (17)
			|<ALT> (18)
			|<MENU> (18)
			|<PAUSE> (19)
			|<BREAK> (19)
			|<CAPITAL> (20)
			|<KANA> (21)
			|<HANGUEL> (21)
			|<HANGUL> (21)
			|<JUNJA> (23)
			|<FINAL> (24)
			|<HANJA> (25)
			|<KANJI> (25)
			|<ESC> (27)
			|<ESCAPE> (27)
			|<CONVERT> (28)
			|<NONCONVERT> (29)
			|<ACCEPT> (30)
			|<MODECHANGE> (31)
			|<SPACE> (32)
			|<PRIOR> (33)
			|<NEXT> (34)
			|<END> (35)
			|<HOME> (36)
			|<LEFT> (37)
			|<UP> (38)
			|<RIGHT> (39)
			|<DOWN> (40)
			|<SELECT> (41)
			|<PRINT> (42)
			|<EXECUTE> (43)
			|<SNAPSHOT> (44)
			|<INS> (45)
			|<INSERT> (45)
			|<DEL> (46)
			|<DELETE> (46)
			|<HELP> (47)
			|<0> (48)
			|<1> (49)
			|<2> (50)
			|<3> (51)
			|<4> (52)
			|<5> (53)
			|<6> (54)
			|<7> (55)
			|<8> (56)
			|<9> (57)
			|<A> (65)
			|<B> (66)
			|<C> (67)
			|<D> (68)
			|<E> (69)
			|<F> (70)
			|<G> (71)
			|<H> (72)
			|<I> (73)
			|<J> (74)
			|<K> (75)
			|<L> (76)
			|<M> (77)
			|<N> (78)
			|<O> (79)
			|<P> (80)
			|<Q> (81)
			|<R> (82)
			|<S> (83)
			|<T> (84)
			|<U> (85)
			|<V> (86)
			|<W> (87)
			|<X> (88)
			|<Y> (89)
			|<Z> (90)
			|<WIN> (91)
			|<LWIN> (91)
			|<RWIN> (92)
			|<APPS> (93)
			|<SLEEP> (95)
			|<NUMPAD0> (96)
			|<NUMPAD1> (97)
			|<NUMPAD2> (98)
			|<NUMPAD3> (99)
			|<NUMPAD4> (100)
			|<NUMPAD5> (101)
			|<NUMPAD6> (102)
			|<NUMPAD7> (103)
			|<NUMPAD8> (104)
			|<NUMPAD9> (105)
			|<MULTIPLY> (106)
			|<*> (106)
			|<ADD> (107)
			|<+> (107)
			|<SEPARATOR> (108)
			|<SUBTRACT> (109)
			|<-> (109)
			|<DECIMAL> (110)
			|<DECIMAL> (110)
			|<.> (110)
			|<DIVIDE> (111)
			|</> (111)
			|<F1> (112)
			|<F2> (113)
			|<F3> (114)
			|<F4> (115)
			|<F5> (116)
			|<F6> (117)
			|<F7> (118)
			|<F8> (119)
			|<F9> (120)
			|<F10> (121)
			|<F11> (122)
			|<F12> (123)
			|<F13> (124)
			|<F14> (125)
			|<F15> (126)
			|<F16> (127)
			|<F17> (128)
			|<F18> (129)
			|<F19> (130)
			|<F20> (131)
			|<F21> (132)
			|<F22> (133)
			|<F23> (134)
			|<F24> (135)
			|<NUMLOCK> (144)
			|<SCROLL> (145)
			|<LSHIFT> (160)
			|<RSHIFT> (161)
			|<LCONTROL> (162)
			|<RCONTROL> (163)
			|<LMENU> (164)
			|<RMENU> (165)
			|<BROWSER_BACK> (166)
			|<BROWSER_FORWARD> (167)
			|<BROWSER_REFRESH> (168)
			|<BROWSER_STOP> (169)
			|<BROWSER_SEARCH> (170)
			|<BROWSER_FAVORITES> (171)
			|<BROWSER_HOME> (172)
			|<VOLUME_MUTE> (173)
			|<VOLUME_DOWN> (174)
			|<VOLUME_UP> (175)
			|<MEDIA_NEXT_TRACK> (176)
			|<MEDIA_PREV_TRACK> (177)
			|<MEDIA_STOP> (178)
			|<MEDIA_PLAY_PAUSE> (179)
			|<LAUNCH_MAIL> (180)
			|<LAUNCH_MEDIA_SELECT> (181)
			|<LAUNCH_APP1> (182)
			|<LAUNCH_APP2> (183)
			|<OEM_1> (186)
			|<;> (186)
			|<OEM_PLUS> (187)
			|<=> (187)
			|<OEM_COMMA> (188)
			|<,> (188)
			|<OEM_MINUS> (189)
			|<-> (189)
			|<OEM_PERIOD> (190)
			|<.> (190)
			|<OEM_2> (191)
			|</> (191)
			|<OEM_3> (192)
			|<`> (192)
			|<OEM_4> (219)
			|<[> (219)
			|<OEM_5> (220)
			|<\> (220)
			|<OEM_6> (221)
			|<]> (221)
			|<OEM_7> (222)
			|<'> (222)
			|<OEM_8> (223)
			|<OEM_102> (226)
			|<PROCESSKEY> (229)
			|<PACKET> (231)
			|<ATTN> (246)
			|<CRSEL> (247)
			|<EXSEL> (248)
			|<EREOF> (249)
			|<PLAY> (250)
			|<ZOOM> (251)
			|<NONAME> (252)
			|<PA1> (253)
			|<OEM_CLEAR> (254)
		|",
		"Прочее.VanessaExt.Эмуляция нажатия клавиши клавиатуры");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯВращаюКолесоМыши(Направление)",
		"ЯВращаюКолесоМыши",
		"И я вращаю колесо мыши ""Ctrl+Forward""",
		"Эмуляция вращения колеса мыши при помощи внешней компоненты VanessaExt. Необхоимо указать направление вращения: ""Forward"" или ""Back""",
		"Прочее.VanessaExt.Колесо мышки");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯВыбираюФайлВК(ИмяФайла)",
		"ЯВыбираюФайлВК",
		"И я выбираю файл ""ИмяФайла"" ВК",
		"Открывает произвольный внешний файл, например файл внешней обработки с помощью эмуляции клавиатуры. Необходимо включить использование внешней компоненты."
		+ "Для выполнения шага необходимо разрешение на использование синхронных вызовов. "
		+ "Окно клиента тестирования должно быть активно. "
		+ "В тексте сценария при указании имени файла для Windows не забывайте использовать двойной обратный слеш.",
		"Файлы.Выбор файла");
	
		Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯВывожуВКонсольТекст(Текст)",
		"ЯВывожуВКонсольТекст",
		"Тогда я вывожу в консоль текст ""ПроизвольныйТекст""",
		"Выводит в консоль родительского процесса произвольный текст.
		|Если в конце строки нет перевода строки - шаг добавит его автоматически.",
		"Прочее.Вывод текста в консоль");

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯПоказываюУвеличениеФрагментаЭкрана(ТабПарам)",
		"ЯПоказываюУвеличениеФрагментаЭкрана",
		"И я показываю увеличение фрагмента экрана
		|		|'left'|'100'|
		|		|'top'|'100'|
		|		|'width'|'400'|
		|		|'height'|'400'|
		|		|'factor'|'2'|
		|		|'shape'|'2'|",
		"Показывает окно с увеличенным фрагментом экрана под мышкой. 
		|left, top - положение левого верхнего угла
		|width, height - размеры области увеличения
		|factor - кратность увеличения
		|shape - форма окна: 0..2 (0 - прямоугольник, 1 - прямоугольник со скругленными краями, 2 - круг/овал)",
		"Прочее.Автоинструкции.Экранная лупа");

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯЗакрываюУвеличениеФрагментаЭкрана()",
		"ЯЗакрываюУвеличениеФрагментаЭкрана",
		"И я закрываю увеличение фрагмента экрана",
		"Скрывает окно с увеличенным фрагментом экрана",
		"Прочее.Автоинструкции.Экранная лупа");

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ЯПеремещаюМышьВЦентрЭкрана()",
		"ЯПеремещаюМышьВЦентрЭкрана",
		"И я перемещаю мышь в центр экрана",
		"Перемещает курсор мышки в центр экрана.",
		"Прочее.Автоинструкции.Мышь");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ЯЭмулируюВводТекста(Парам01)",
		"ЯЭмулируюВводТекста",
		"И я эмулирую ввод текста ""Текст""",
		"Эмулирует ввод текста в текущее поле ввода.",
		"Прочее.Автоинструкции.Ввод текста");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ЯПолучаюДанныеПроцессаПоЗаголовкуОкна(Парам01)",
		"ЯПолучаюДанныеПроцессаПоЗаголовкуОкна",
		"И я получаю данные процесса по заголовку окна ""Заголовок""",
		"Получает данные процесса по его заголовку.
		|При поиске окна использовать символы * для поиска по шаблону.
		|Создаёт переменные типа строка: Class, ProcessId, Title, Window",
		"Прочее.Получить данные процесса");
	
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ЯРисуюРамкуПоКоординатам(Парам01,Парам02,Парам03,Парам04,ТабПарам)",
		"ЯРисуюРамкуПоКоординатам",
		"И Я рисую рамку по координатам 'ФигураX' 'ФигураY' 'ФигураW' 'ФигураH'
		|        |'Цвет'         | 'Красный'    |
		|        |'Длительность' | '2000'       |
		|        |'Прозрачность' | '127'        |
		|        |'Толщина'      | '2'          |",
		"Отрисовка рамки по координатам ""ТочкаX"" ""ТочкаY"" ""Длина"" ""Ширина"". Также указывается цвет рамки, задержка итд.",
		"Прочее.VanessaExt.Фигуры по координатам");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ЯРисуюЭллипсПоКоординатам(Парам01,Парам02,Парам03,Парам04,ТабПарам)",
		"ЯРисуюЭллипсПоКоординатам",
		"И Я рисую эллипс по координатам 'ФигураX' 'ФигураY' 'ФигураW' 'ФигураH'
		|        |'Цвет'         | 'Красный'    |
		|        |'Длительность' | '2000'       |
		|        |'Прозрачность' | '127'        |
		|        |'Толщина'      | '2'          |",
		"Отрисовка эллипса (овала) по координатам ""ТочкаX"" ""ТочкаY"" ""Длина"" ""Ширина"". Также указывается цвет рамки, задержка итд.",
		"Прочее.VanessaExt.Фигуры по координатам");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ЯРисуюТеньПоКоординатамТекстНадписи(Парам01,Парам02,Парам03,Парам04,ТекстНадписи,ТабПарам)",
		"ЯРисуюТеньПоКоординатамТекстНадписи",
		"И Я рисую тень по координатам 'ФигураX' 'ФигураY' 'ФигураW' 'ФигураH' текст надписи 'ТекстНадписи'
		|        |'Цвет'         | 'Красный'    |
		|        |'Длительность' | '2000'       |
		|        |'Прозрачность' | '127'        |
		|        |'Толщина'      | '2'          |",
		"Отображение затенения прямоугольником по координатам ""ТочкаX"" ""ТочкаY"" ""Длина"" ""Ширина"" с подсказкой в виде текста. Также указывается цвет рамки, задержка итд.",
		"Прочее.VanessaExt.Фигуры по координатам");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ЯРисуюНадписьПоТочкамТекстНадписи(Парам01,Парам02,ТекстНадписи,ТабПарам)",
		"ЯРисуюНадписьПоТочкамТекстНадписи",
		"И Я рисую надпись по точкам 'ФигураX' 'ФигураY' текст надписи 'ТекстНадписи'
		|        |'ЦветШрифта'    | 'Красный'    |
		|        |'РазмерШрифта'  | '14'         |
		|        |'Длительность'  | '2000'       |
		|        |'Прозрачность'  | '127'        |
		|        |'Толщина'       | '2'          |",
		"Отрисовка надписи цветом на экране в позиции ""ТочкаX"" ""ТочкаX"" текст надписи ""Текст подсказки"". Также указывается цвет текста, размер шрифта итд.",
		"Прочее.VanessaExt.Фигуры по координатам");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ЯРисуюПодсказку_балонПоТочкамТекстНадписи(Парам01,Парам02,Парам03,ТабПарам)",
		"ЯРисуюПодсказку_балонПоТочкамТекстНадписи",
		"И Я рисую подсказку-балон по точкам 'ФигураX' 'ФигураY' текст надписи 'ТекстНадписи'
		|        |'ЦветШрифта'      | 'Красный'   |
		|        |'РазмерШрифта'    | '14'        |
		|        |'Длительность'    | '2000'      |
		|        |'Прозрачность'    | '127'       |
		|        |'Толщина'         | '2'         |",
		"Отрисовка подсказки в виде облака с надписью на экране в позиции ""ТочкаX"" ""ТочкаX"" текст надписи ""Текст подсказки"". Также указывается цвет текста, размер шрифта итд.",
		"Прочее.VanessaExt.Фигуры по координатам");
		
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"КликПоКоординатам(X,Y)",
		"КликПоКоординатам",
		"И клик по координатам ""800"" ""600""",
		"Выполняет клик мыши по заданным координатам.",
		"Прочее.VanessaExt.Мышь по координатам");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ДвойнойКликПоКоординатам(X,Y)",
		"ДвойнойКликПоКоординатам",
		"И двойной клик по координатам ""800"" ""600""",
		"Выполняет двойной клик мыши по заданным координатам.",
		"Прочее.VanessaExt.Мышь по координатам");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"ПравыйКликПоКоординатам(X,Y)",
		"ПравыйКликПоКоординатам",
		"И правый клик по координатам ""800"" ""600""",
		"Выполняет правый клик мыши по заданным координатам.",
		"Прочее.VanessaExt.Мышь по координатам");

		Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,
		"КурсорККоординатам(X,Y)",
		"КурсорККоординатам",
		"И курсор к координатам ""800"" ""600""",
		"Выполняет перемещение курсора мыши к заданным координатам.",
		"Прочее.VanessaExt.Мышь по координатам");

	Возврат ВсеТесты;
КонецФункции

&НаСервере
// Служебная функция.
Функция ПолучитьМакетСервер(ИмяМакета)
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектСервер.ПолучитьМакет(ИмяМакета);
КонецФункции

&НаКлиенте
// Служебная функция для подключения библиотеки создания fixtures.
Функция ПолучитьМакетОбработки(ИмяМакета) Экспорт
	Возврат ПолучитьМакетСервер(ИмяМакета);
КонецФункции


///////////////////////////////////////////////////
//Работа со сценариями
///////////////////////////////////////////////////

&НаКлиенте
// Функция выполняется перед началом каждого сценария
Функция ПередНачаломСценария() Экспорт
	
КонецФункции

&НаКлиенте
// Функция выполняется перед окончанием каждого сценария
Функция ПередОкончаниемСценария() Экспорт
	
КонецФункции


///////////////////////////////////////////////////
//Реализация шагов
///////////////////////////////////////////////////

&НаКлиенте
Функция ДанныеПараметровИзТаблицы(ДанныеПараметров, ТабПарам)
	Если ТабПарам = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого СтрокаТабПарам Из ТабПарам Цикл
		ДанныеПараметров.Вставить(СтрокаТабПарам.Кол1, СтрокаТабПарам.Кол2);
	КонецЦикла;
КонецФункции

&НаКлиенте
Функция ОбернутьВАпострофы(Стр)
	Если Прав(СокрП(Стр), 1) = ")" Тогда
		Возврат Стр;
	КонецЕсли;
	
	Возврат "'" + Стр + "'";
КонецФункции

&НаКлиенте
Функция ПолучитьКлавиши() Экспорт
	
	Map = New Map;
	Map.Insert("LBUTTON", 1);
	Map.Insert("RBUTTON", 2);
	Map.Insert("CANCEL", 3);
	Map.Insert("MBUTTON", 4);
	Map.Insert("XBUTTON1", 5);
	Map.Insert("XBUTTON2", 6);
	Map.Insert("BACK", 8);
	Map.Insert("TAB", 9);
	Map.Insert("CLEAR", 12);
	Map.Insert("RETURN", 13);
	Map.Insert("ENTER", 13);
	Map.Insert("SHIFT", 16);
	Map.Insert("CONTROL", 17);
	Map.Insert("CTRL", 17);
	Map.Insert("ALT", 18);
	Map.Insert("MENU", 18);
	Map.Insert("PAUSE", 19);
	Map.Insert("BREAK", 19);
	Map.Insert("CAPITAL", 20);
	Map.Insert("KANA", 21);
	Map.Insert("HANGUEL", 21);
	Map.Insert("HANGUL", 21);
	Map.Insert("JUNJA", 23);
	Map.Insert("FINAL", 24);
	Map.Insert("HANJA", 25);
	Map.Insert("KANJI", 25);
	Map.Insert("ESC", 27);
	Map.Insert("ESCAPE", 27);
	Map.Insert("CONVERT", 28);
	Map.Insert("NONCONVERT", 29);
	Map.Insert("ACCEPT", 30);
	Map.Insert("MODECHANGE", 31);
	Map.Insert("SPACE", 32);
	Map.Insert("PRIOR", 33);
	Map.Insert("NEXT", 34);
	Map.Insert("END", 35);
	Map.Insert("HOME", 36);
	Map.Insert("LEFT", 37);
	Map.Insert("UP", 38);
	Map.Insert("RIGHT", 39);
	Map.Insert("DOWN", 40);
	Map.Insert("SELECT", 41);
	Map.Insert("PRINT", 42);
	Map.Insert("EXECUTE", 43);
	Map.Insert("SNAPSHOT", 44);
	Map.Insert("INS", 45);
	Map.Insert("INSERT", 45);
	Map.Insert("DEL", 46);
	Map.Insert("DELETE", 46);
	Map.Insert("HELP", 47);
	Map.Insert("0", 48);
	Map.Insert("1", 49);
	Map.Insert("2", 50);
	Map.Insert("3", 51);
	Map.Insert("4", 52);
	Map.Insert("5", 53);
	Map.Insert("6", 54);
	Map.Insert("7", 55);
	Map.Insert("8", 56);
	Map.Insert("9", 57);
	Map.Insert("A", 65);
	Map.Insert("B", 66);
	Map.Insert("C", 67);
	Map.Insert("D", 68);
	Map.Insert("E", 69);
	Map.Insert("F", 70);
	Map.Insert("G", 71);
	Map.Insert("H", 72);
	Map.Insert("I", 73);
	Map.Insert("J", 74);
	Map.Insert("K", 75);
	Map.Insert("L", 76);
	Map.Insert("M", 77);
	Map.Insert("N", 78);
	Map.Insert("O", 79);
	Map.Insert("P", 80);
	Map.Insert("Q", 81);
	Map.Insert("R", 82);
	Map.Insert("S", 83);
	Map.Insert("T", 84);
	Map.Insert("U", 85);
	Map.Insert("V", 86);
	Map.Insert("W", 87);
	Map.Insert("X", 88);
	Map.Insert("Y", 89);
	Map.Insert("Z", 90);
	Map.Insert("WIN", 91);
	Map.Insert("LWIN", 91);
	Map.Insert("RWIN", 92);
	Map.Insert("APPS", 93);
	Map.Insert("SLEEP", 95);
	Map.Insert("NUMPAD0", 96);
	Map.Insert("NUMPAD1", 97);
	Map.Insert("NUMPAD2", 98);
	Map.Insert("NUMPAD3", 99);
	Map.Insert("NUMPAD4", 100);
	Map.Insert("NUMPAD5", 101);
	Map.Insert("NUMPAD6", 102);
	Map.Insert("NUMPAD7", 103);
	Map.Insert("NUMPAD8", 104);
	Map.Insert("NUMPAD9", 105);
	Map.Insert("MULTIPLY", 106);
	Map.Insert("*", 106);
	Map.Insert("ADD", 107);
	Map.Insert("+", 107);
	Map.Insert("SEPARATOR", 108);
	Map.Insert("SUBTRACT", 109);
	Map.Insert("-", 109);
	Map.Insert("DECIMAL", 110);
	Map.Insert("DECIMAL", 110);
	Map.Insert(".", 110);
	Map.Insert("DIVIDE", 111);
	Map.Insert("/", 111);
	Map.Insert("F1", 112);
	Map.Insert("F2", 113);
	Map.Insert("F3", 114);
	Map.Insert("F4", 115);
	Map.Insert("F5", 116);
	Map.Insert("F6", 117);
	Map.Insert("F7", 118);
	Map.Insert("F8", 119);
	Map.Insert("F9", 120);
	Map.Insert("F10", 121);
	Map.Insert("F11", 122);
	Map.Insert("F12", 123);
	Map.Insert("F13", 124);
	Map.Insert("F14", 125);
	Map.Insert("F15", 126);
	Map.Insert("F16", 127);
	Map.Insert("F17", 128);
	Map.Insert("F18", 129);
	Map.Insert("F19", 130);
	Map.Insert("F20", 131);
	Map.Insert("F21", 132);
	Map.Insert("F22", 133);
	Map.Insert("F23", 134);
	Map.Insert("F24", 135);
	Map.Insert("NUMLOCK", 144);
	Map.Insert("SCROLL", 145);
	Map.Insert("LSHIFT", 160);
	Map.Insert("RSHIFT", 161);
	Map.Insert("LCONTROL", 162);
	Map.Insert("RCONTROL", 163);
	Map.Insert("LMENU", 164);
	Map.Insert("RMENU", 165);
	Map.Insert("BROWSER_BACK", 166);
	Map.Insert("BROWSER_FORWARD", 167);
	Map.Insert("BROWSER_REFRESH", 168);
	Map.Insert("BROWSER_STOP", 169);
	Map.Insert("BROWSER_SEARCH", 170);
	Map.Insert("BROWSER_FAVORITES", 171);
	Map.Insert("BROWSER_HOME", 172);
	Map.Insert("VOLUME_MUTE", 173);
	Map.Insert("VOLUME_DOWN", 174);
	Map.Insert("VOLUME_UP", 175);
	Map.Insert("MEDIA_NEXT_TRACK", 176);
	Map.Insert("MEDIA_PREV_TRACK", 177);
	Map.Insert("MEDIA_STOP", 178);
	Map.Insert("MEDIA_PLAY_PAUSE", 179);
	Map.Insert("LAUNCH_MAIL", 180);
	Map.Insert("LAUNCH_MEDIA_SELECT", 181);
	Map.Insert("LAUNCH_APP1", 182);
	Map.Insert("LAUNCH_APP2", 183);
	Map.Insert("OEM_1", 186);
	Map.Insert(";", 186);
	Map.Insert("OEM_PLUS", 187);
	Map.Insert("=", 187);
	Map.Insert("OEM_COMMA", 188);
	Map.Insert(",", 188);
	Map.Insert("OEM_MINUS", 189);
	Map.Insert("-", 189);
	Map.Insert("OEM_PERIOD", 190);
	Map.Insert(".", 190);
	Map.Insert("OEM_2", 191);
	Map.Insert("/", 191);
	Map.Insert("OEM_3", 192);
	Map.Insert("`", 192);
	Map.Insert("OEM_4", 219);
	Map.Insert("[", 219);
	Map.Insert("OEM_5", 220);
	Map.Insert("\", 220);
	Map.Insert("OEM_6", 221);
	Map.Insert("]", 221);
	Map.Insert("OEM_7", 222);
	Map.Insert("'", 222);
	Map.Insert("OEM_8", 223);
	Map.Insert("OEM_102", 226);
	Map.Insert("PROCESSKEY", 229);
	Map.Insert("PACKET", 231);
	Map.Insert("ATTN", 246);
	Map.Insert("CRSEL", 247);
	Map.Insert("EXSEL", 248);
	Map.Insert("EREOF", 249);
	Map.Insert("PLAY", 250);
	Map.Insert("ZOOM", 251);
	Map.Insert("NONAME", 252);
	Map.Insert("PA1", 253);
	Map.Insert("OEM_CLEAR", 254);
	Возврат Map;
	
КонецФункции

&НаКлиенте
Функция ЯНажимаюСочетаниеКлавиш(Текст) Экспорт
	
	СоответствиеКлавиш = ПолучитьКлавиши();
	КодыКлавиш = Новый Массив;
	НаборКлавиш = Ванесса.РазложитьСтрокуВМассивПодстрокКлиент(Текст, "+");
	Для каждого Стр Из НаборКлавиш Цикл
		Стр = ВРег(СокрЛП(Стр));
		Код = СоответствиеКлавиш.Получить(Стр);
		Если Код = Неопределено Тогда
			ТекстСообщения = Ванесса.Локализовать("Не удалось определить код клавиши <%1>.");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Стр);
			ВызватьИсключение ТекстСообщения;
		Иначе
			КодыКлавиш.Добавить(Код);
		КонецЕсли;
	КонецЦикла;
	
	ТекстJSON = Ванесса.ЗаписатьОбъектJSON(КодыКлавиш);
	Если НЕ Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить использование внешней компоненты VanessaExt.");
	КонецЕсли;	 
	
	Ванесса.ЗапретитьВыполнениеШагов();
	ДопПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеНачатьВызовЭмуляцияНажатияКлавиши", ЭтаФорма, ДопПараметры);
	Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовЭмуляцияНажатияКлавиши(ОписаниеОповещения, ТекстJSON);
	
КонецФункции

&НаКлиенте
Функция ОкончаниеНачатьВызовЭмуляцияНажатияКлавиши(ДвоичныеДанные, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	Ванесса.ПродолжитьВыполнениеШагов();
КонецФункции

&НаКлиенте
//И я открываю внешний файл "ИмяФайла"
Функция ЯВращаюКолесоМыши(Текст) Экспорт
	
	Направления = Новый Соответствие;
	Направления.Вставить("FORWARD", 1);
	Направления.Вставить("ВПЕРЁД", 1);
	Направления.Вставить("ВПЕРЕД", 1);
	Направления.Вставить("BACK", - 1);
	Направления.Вставить("НАЗАД", - 1);
	
	СоответствиеКлавиш = ПолучитьКлавиши();
	Направление = Неопределено;
	КодыКлавиш = Новый Массив;
	НаборКлавиш = Ванесса.РазложитьСтрокуВМассивПодстрокКлиент(Текст, "+");
	Для каждого Стр Из НаборКлавиш Цикл
		Стр = ВРег(СокрЛП(Стр));
		Напр = Направления.Получить(Стр);
		Если Напр = Неопределено Тогда
			Код = СоответствиеКлавиш.Получить(Стр);
			Если Код = Неопределено Тогда
				ТекстСообщения = Ванесса.Локализовать("Не удалось определить код клавиши <%1>.");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Стр);
				ВызватьИсключение ТекстСообщения;
			Иначе
				КодыКлавиш.Добавить(Код);
			КонецЕсли;
		Иначе
			Направление = Напр;
		КонецЕсли;
	КонецЦикла;
	
	Если Направление = Неопределено Тогда
		ТекстСообщения = Ванесса.Локализовать("Не удалось определить направление вращения мыши <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Текст);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;

	Если КодыКлавиш.Количество() > 0 Тогда
		ТекстJSON = Ванесса.ЗаписатьОбъектJSON(КодыКлавиш);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.ЭмуляцияКолесаМыши(Направление, ТекстJSON);
	Иначе
		Ванесса.ВнешняяКомпонентаДляСкриншотов.ЭмуляцияКолесаМыши(Направление, Неопределено);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ЯВыбираюФайлВКПодтверждениеВыбораФайла()
	
	Попытка
		Ванесса.ВнешняяКомпонентаДляСкриншотов.Пауза(500);
		Если Ванесса.ТекущийTestclientЭтоWebКлиент Тогда
			Ванесса.ВыполнитьJavaScriptБраузер("document.getElementById('fileSelectDialogOk').click()");
		КонецЕсли; 
	Исключение
		Ванесса.ПродолжитьВыполнениеШагов(Истина, ОписаниеОшибки());
	КонецПопытки;
	
	Ванесса.ПродолжитьВыполнениеШагов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЯВыбираюФайлВКОткрытиеДиалогаФайла()
	Попытка
		Ванесса.ВнешняяКомпонентаДляСкриншотов.Пауза(500);
		Если Ванесса.ТекущийTestclientЭтоWebКлиент Тогда
			Ванесса.ВыполнитьJavaScriptБраузер("document.getElementById('fileSelectButton').click()");
		КонецЕсли;
		
		Ванесса.ВнешняяКомпонентаДляСкриншотов.Пауза(500);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.ЭмуляцияВводаТекста(ПараметрИмяФайла, 0);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.Пауза(500);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.ЭмуляцияНажатияКлавиши("[13]");
		
		ПодключитьОбработчикОжидания("ЯВыбираюФайлВКПодтверждениеВыбораФайла",0.1, Истина);
	Исключение
		Ванесса.ПродолжитьВыполнениеШагов(Истина, ОписаниеОшибки());
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
//"И я выбираю файл ""ИмяФайла"" ВК"
Функция ЯВыбираюФайлВК(ИмяФайла) Экспорт
	Если НЕ Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить использование внешней компоненты VanessaExt.");
	КонецЕсли;	      
	
	ПараметрИмяФайла = ИмяФайла;
	
	Ванесса.ЗапретитьВыполнениеШагов();
	
	Ванесса.ВнешняяКомпонентаДляСкриншотов.ЭмуляцияНажатияКлавиши("[17,79]");
	
	ПодключитьОбработчикОжидания("ЯВыбираюФайлВКОткрытиеДиалогаФайла",0.1, Истина);
	
КонецФункции

&НаКлиенте
//"Тогда я вывожу в консоль текст ""ПроизвольныйТекст\n"""
Функция ЯВывожуВКонсольТекст(Текст) Экспорт
	
	Если НЕ Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить использование внешней компоненты VanessaExt.");
	КонецЕсли;	 

	Ванесса.ЗапретитьВыполнениеШагов();
	Если Прав(Текст, 1) <> Символы.ПС Тогда
		ТекстКонсоли = Текст + Символы.ПС;
	Иначе	
		ТекстКонсоли = Текст;
	КонецЕсли;	 
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВыводаТекстаНаКонсоль", ЭтаФорма);
	Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовВывестиВКонсоль(ОписаниеОповещения, ТекстКонсоли, 65001);
	
КонецФункции

&НаКлиенте
Функция ОкончаниеВыводаТекстаНаКонсоль(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если Результат Тогда
		Ванесса.ПродолжитьВыполнениеШагов();
	Иначе	
		Ванесса.ПродолжитьВыполнениеШагов(Истина, Ванесса.Локализовать("Не удалось вывести текст в консоль."));
	КонецЕсли;	 
	
КонецФункции

&НаКлиенте
//И я показываю увеличение фрагмента экрана
//ЯПоказываюУвеличениеФрагментаЭкрана(ТабПарам)
Функция ЯПоказываюУвеличениеФрагментаЭкрана(ТабПарам) Экспорт
	
	ДанныеПараметров = Новый Соответствие;
	ДанныеПараметров.Вставить("left", 		100);
	ДанныеПараметров.Вставить("top", 		100);
	ДанныеПараметров.Вставить("width", 		400);
	ДанныеПараметров.Вставить("height", 	400);
	ДанныеПараметров.Вставить("factor",     2);
	ДанныеПараметров.Вставить("shape", 	    2);
	
	ДанныеПараметровИзТаблицы(ДанныеПараметров, ТабПарам);

	ДанныеПараметров["left"] 	= Число(ДанныеПараметров["left"]);
	ДанныеПараметров["top"] 	= Число(ДанныеПараметров["top"]);
	ДанныеПараметров["width"] 	= Число(ДанныеПараметров["width"]);
	ДанныеПараметров["height"] 	= Число(ДанныеПараметров["height"]);
	ДанныеПараметров["factor"] 	= Число(ДанныеПараметров["factor"]);
	ДанныеПараметров["shape"] 	= Число(ДанныеПараметров["shape"]);

	Если НЕ Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить использование внешней компоненты VanessaExt.");
	КонецЕсли;	 

	Ванесса.ЗапретитьВыполнениеШагов();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваУвеличениеФрагмента", ЭтаФорма);
	Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовПоказатьУвеличение(ОписаниеОповещения,
		ДанныеПараметров["left"], ДанныеПараметров["top"], 
		ДанныеПараметров["width"], ДанныеПараметров["height"],
		ДанныеПараметров["factor"], ДанныеПараметров["shape"]);
	
КонецФункции

&НаКлиенте
//И я закрываю увеличение фрагмента экрана
//ЯЗакрываюУвеличениеФрагментаЭкрана()
Функция ЯЗакрываюУвеличениеФрагментаЭкрана() Экспорт
	
	Если НЕ Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить использование внешней компоненты VanessaExt.");
	КонецЕсли;	 

	Ванесса.ЗапретитьВыполнениеШагов();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваУвеличениеФрагмента", ЭтаФорма);
	Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовСкрытьУвеличение(ОписаниеОповещения);
	
КонецФункции

&НаКлиенте
Функция ОкончаниеВызоваУвеличениеФрагмента(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Ванесса.ПродолжитьВыполнениеШагов();
	
КонецФункции

&НаКлиенте
//И я перемещаю мышь в центр экрана
//@ЯПеремещаюМышьВЦентрЭкрана()
Функция ЯПеремещаюМышьВЦентрЭкрана() Экспорт
	ИнформацияЭкранаКлиента = Вычислить("ПолучитьИнформациюЭкрановКлиента()[0]");
	Ширина = ИнформацияЭкранаКлиента.Ширина;
	Высота = ИнформацияЭкранаКлиента.Высота;
	
	Ванесса.ВнешняяКомпонентаДляСкриншотов.УстановитьПозициюКурсора(Цел(Ширина/2), Цел(Высота/2));
	
КонецФункции

&НаКлиенте
//И я эмулирую ввод текста "Текст"
//@ЯЭмулируюВводТекста()
Функция ЯЭмулируюВводТекста(Текст) Экспорт
	Ванесса.ВнешняяКомпонентаДляСкриншотов.ЭмуляцияВводаТекста(Строка(Текст), 100);
КонецФункции

&НаКлиенте
//И я получаю данные процесса по заголовку окна "Заголовок"
//@ЯПолучаюДанныеПроцессаПоЗаголовкуОкна(Парам01)
Функция ЯПолучаюДанныеПроцессаПоЗаголовкуОкна(ПараметрЗаголовок) Экспорт
	
	Если НЕ Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить использование внешней компоненты VanessaExt.");
	КонецЕсли;	 
	
	Ванесса.ЗапретитьВыполнениеШагов();
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ПараметрЗаголовок", ПараметрЗаголовок);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолученСписокОконОС", ЭтаФорма, ДопПараметры);
	Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьПолучениеСписокОкон(ОписаниеОповещения);
	
КонецФункции

&НаКлиенте
Функция СвойстваПроцессаДляСозданияПеременных()
	
	Массив = Новый Массив;
	Массив.Добавить("Class");
	Массив.Добавить("ProcessId");
	Массив.Добавить("Title");
	Массив.Добавить("Window");
	
	Возврат Массив;
	
КонецФункции	 

&НаКлиенте
Процедура ПолученСписокОконОС(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрЗаголовок = ДополнительныеПараметры.ПараметрЗаголовок;
	
	ПроцессНайден = Ложь;
	Данные = Ванесса.ПрочитатьСтрокуJSON(Результат);
	Для Каждого ДанныеПроцесса Из Данные Цикл
		Если НЕ ДанныеПроцесса.Свойство("Title") Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если НЕ ЗначениеЗаполнено(ДанныеПроцесса.Title) Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если Ванесса.СтрокаСоответствуетШаблону(ДанныеПроцесса.Title, ПараметрЗаголовок) Тогда
			ПроцессНайден = Истина;
			
			СвойстваПроцессаДляСозданияПеременных = СвойстваПроцессаДляСозданияПеременных();
			
			Для Каждого Ключ Из СвойстваПроцессаДляСозданияПеременных Цикл
				Попытка
					Контекст.Вставить(Ключ, XMLСтрока(ДанныеПроцесса[Ключ]));
				Исключение
				КонецПопытки;
			КонецЦикла;	 
			
			Прервать;
		КонецЕсли;	 
		
	КонецЦикла;	 
	
	Если НЕ ПроцессНайден Тогда
		ТекстСообщения = Ванесса.Локализовать("Не удалось найти процесс по заголовку окна <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ПараметрЗаголовок);
		Ванесса.ПродолжитьВыполнениеШагов(Истина, ТекстСообщения);
		Возврат;
	КонецЕсли;	 
	
	Ванесса.ПродолжитьВыполнениеШагов();
		
КонецПроцедуры



&НаКлиенте
Функция ПустоеОповещениеСПродолжениемВыполненияСценария(Парам1, Парам2, ДопПараметры) Экспорт
	Если ТипЗнч(ДопПараметры) = Тип("Структура") И ДопПараметры.Свойство("ИмяОбработчика") Тогда
		Если ДопПараметры.ИмяОбработчика <> "НеВыполнятьОбработчик" Тогда
			ПодключитьОбработчикОжидания(ДопПараметры.ИмяОбработчика, 0.1, Истина);
		КонецЕсли;	
	Иначе
		Ванесса.ПродолжитьВыполнениеШагов();
	КонецЕсли;
КонецФункции 

&НаКлиенте
Функция ПараметрыРисования(ТабПарам)

	ПереданныеПараметры = ПереданныеПараметры(ТабПарам);
	АбсолютныйЦвет = ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры, "Цвет", "Color");
	
	Если ПереданныеПараметры.Свойство("Толщина") Тогда
		Толщина = ПереданныеПараметры.Толщина;
	ИначеЕсли ПереданныеПараметры.Свойство("Width") Тогда
		Толщина = ПереданныеПараметры.Width;
	Иначе
		Толщина = "3";
	КонецЕсли;
	
	Если ПереданныеПараметры.Свойство("Длительность") Тогда
		Длительность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Duration") Тогда
		Длительность = ПереданныеПараметры.Duration;
	Иначе
		Длительность = "10000";
	КонецЕсли;	 
	
	Если ПереданныеПараметры.Свойство("Прозрачность") Тогда
		Прозрачность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Transparency") Тогда
		Прозрачность = ПереданныеПараметры.Transparency;
	Иначе
		Прозрачность = "255";
	КонецЕсли;	 
	
	Если ПереданныеПараметры.Свойство("Задержка") Тогда
		Задержка = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("frameDelay") Тогда
		Задержка = ПереданныеПараметры.Transparency;
	Иначе
		Задержка = "20";
	КонецЕсли;	 
	
	ЦветШрифта = ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры, "ЦветШрифта", "fontColor");
	
	Если ПереданныеПараметры.Свойство("Шрифт") Тогда
		Шрифт = ПереданныеПараметры.Шрифт;
	ИначеЕсли ПереданныеПараметры.Свойство("font") Тогда
		Шрифт = ПереданныеПараметры.font;
	Иначе
		Шрифт = "Calibri";
	КонецЕсли;	 
	
	Если ПереданныеПараметры.Свойство("РазмерШрифта") Тогда
		РазмерШрифта = ПереданныеПараметры.РазмерШрифта;
	ИначеЕсли ПереданныеПараметры.Свойство("fontsize") Тогда
		РазмерШрифта = ПереданныеПараметры.fontsize;
	Иначе
		РазмерШрифта = "12";
	КонецЕсли;	 

	ПараметрыРисования = Новый Структура;
	ПараметрыРисования.Вставить("color", Ванесса.ЦветЧислом(АбсолютныйЦвет));
	ПараметрыРисования.Вставить("transparency", Число(Прозрачность));
	ПараметрыРисования.Вставить("duration", Число(Длительность));
	ПараметрыРисования.Вставить("frameDelay", Число(Задержка));
	ПараметрыРисования.Вставить("thickness", Число(Толщина));
	ПараметрыРисования.Вставить("fontName", Шрифт);
	ПараметрыРисования.Вставить("fontSize", Число(РазмерШрифта));
	ПараметрыРисования.Вставить("fontColor", Ванесса.ЦветЧислом(ЦветШрифта));
	
	Возврат ПараметрыРисования;
	
КонецФункции 

&НаКлиенте
Функция ПереданныеПараметры(ТабПарам)
	Структура = Новый Структура;
	Для Каждого СтрокаТабПарам Из ТабПарам Цикл
		Попытка
			Структура.Вставить(СтрокаТабПарам.Кол1, СтрокаТабПарам.Кол2);
		Исключение
			Ванесса.СообщитьПользователю(Ванесса.Локализовать(ОписаниеОшибки()));
		КонецПопытки;
	КонецЦикла;	 
	
	Возврат Структура;
КонецФункции	

&НаКлиенте
// Получает абсолютный цвет из параметров
//
Функция ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры, Имя1,Имя2)
	
	Цвет = WebЦвета.ЗеленыйЛес;
	Если ПереданныеПараметры.Свойство(Имя1) Тогда
		Попытка
			Цвет = WebЦвета[ПереданныеПараметры[Имя1]];
		Исключение
			Ванесса.СообщитьПользователю(Ванесса.Локализовать(ОписаниеОшибки()));
		КонецПопытки;
	ИначеЕсли ПереданныеПараметры.Свойство(Имя2) Тогда
		Попытка
			Цвет = WebЦвета[ПереданныеПараметры[Имя1]];
		Исключение
			Ванесса.СообщитьПользователю(Ванесса.Локализовать(ОписаниеОшибки()));
		КонецПопытки;
	КонецЕсли;	 
	АбсолютныйЦвет = Ванесса.АбсолютыйЦвет(Цвет);
	
	Возврат АбсолютныйЦвет;
	
КонецФункции   

&НаКлиенте
// Обработка отрисовки: "ФигураX" "ФигураY" "ФигураW" "ФигураH"
Функция ОбработкаОтрисовкиФигурыПоКоординатам(ФигураX,ФигураY,ФигураW,ФигураH, ТабПараметрыРисования, ИмяПроцедуры, ТекстНадписи = Неопределено)
	
	//сообщить(ФигураX);сообщить(ФигураY);сообщить(ФигураW);сообщить(ФигураH); сообщить(ИмяПроцедуры); сообщить(ТекстНадписи);
	Left 	= Число(ФигураX);
	Top 	= Число(ФигураY);
	Right 	= Left + Число(ФигураW);
	Bottom 	= Top + Число(ФигураH);
	
	ДопПараметры = Неопределено;

	Если Left <= Right И Top <= Bottom Тогда
		
		Ванесса.ЗапретитьВыполнениеШагов();
		
		НастройкиРисованияПоУмолчанию = Ванесса.ПолучитьНастройкиРисования();
		НастройкиРисования = Ванесса.ДополнитьПараметрыОтрисовкиПереданныеПользователем(НастройкиРисованияПоУмолчанию, ПараметрыРисования(ТабПараметрыРисования));
		Ванесса.ПроверитьКорректностьПараметровДляВыполненияУроков(НастройкиРисования);
		НастройкиРисованияJSON = Ванесса.ЗаписатьОбъектJSON(НастройкиРисования); 
		ОписаниеОповещения = Новый ОписаниеОповещения("ПустоеОповещениеСПродолжениемВыполненияСценария", ЭтаФорма, ДопПараметры); 
		
		ТекстПроцедуры = 
		"Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовНарисоватьПрямоугольник(
		|	ОписаниеОповещения,
		|	НастройкиРисованияJSON,
		|	Left - НастройкиРисования.thickness,
		|	Top - НастройкиРисования.thickness,
		|	Right - Left + НастройкиРисования.thickness*2,
		|	Bottom - Top + НастройкиРисования.thickness*2   
		|	,ТекстНадписи);";   
		
		ТекстПроцедуры = СтрЗаменить(ТекстПроцедуры, 
			".НачатьВызовНарисоватьПрямоугольник(", 
			"." + ИмяПроцедуры + "(");
		
		Если ТекстНадписи = Неопределено Тогда
			ТекстПроцедуры = СтрЗаменить(ТекстПроцедуры, 
				",ТекстНадписи", "");
		КонецЕсли;
		
		Выполнить(ТекстПроцедуры);
		
	КонецЕсли;	 
	
КонецФункции  

&НаКлиенте
// Обработка надписи: "ТочкаX" "ТочкаY" "Подсказка"
Функция ОбработкаОтрисовкиНадписиПоТочкам(ТочкаX,ТочкаY,ТекстНадписи,ТабПарам, ИмяПроцедуры)
	
	Left 	= Число(ТочкаX);
	Top 	= Число(ТочкаY);
	
	Ванесса.ЗапретитьВыполнениеШагов();
	НастройкиРисованияПоУмолчанию = Ванесса.ПолучитьНастройкиРисования();
	НастройкиРисования = Ванесса.ДополнитьПараметрыОтрисовкиПереданныеПользователем(НастройкиРисованияПоУмолчанию, ПараметрыРисования(ТабПарам));
	НастройкиРисованияJSON = Ванесса.ЗаписатьОбъектJSON(НастройкиРисования); 
	
	ДопПараметры = Неопределено;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПустоеОповещениеСПродолжениемВыполненияСценария", ЭтаФорма, ДопПараметры); 
	
	ТекстПроцедуры = 
	"Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовПоказатьТекст(
	|	ОписаниеОповещения, НастройкиРисованияJSON,
	|	Left, Top, ТекстНадписи)";
		
	ТекстПроцедуры = СтрЗаменить(ТекстПроцедуры, 
		".НачатьВызовПоказатьТекст(", 
		"." + ИмяПроцедуры + "(");
	
	Выполнить(ТекстПроцедуры);
	
КонецФункции



///////////////////////////////////////////////////
//Реализация шагов
///////////////////////////////////////////////////


&НаКлиенте
//И Я рисую рамку по координатам 'ФигураX' 'ФигураY' 'ФигураW' 'ФигураH' 'ТабПараметрыРисования'
//@ЯРисуюРамкуПоКоординатам(Парам01,Парам02,Парам03,Парам04,ТабПарам)
Функция ЯРисуюРамкуПоКоординатам(Парам01,Парам02,Парам03,Парам04,ТабПарам) Экспорт
	ОбработкаОтрисовкиФигурыПоКоординатам(Парам01,Парам02,Парам03,Парам04, ТабПарам, "НачатьВызовНарисоватьПрямоугольник");
КонецФункции

&НаКлиенте
//И Я рисую эллипс по координатам 'ФигураX' 'ФигураY' 'ФигураW' 'ФигураH' 'ТабПараметрыРисования'
//@ЯРисуюЭллипсПоКоординатам(Парам01,Парам02,Парам03,Парам04,ТабПарам)
Функция ЯРисуюЭллипсПоКоординатам(Парам01,Парам02,Парам03,Парам04,ТабПарам) Экспорт
	ОбработкаОтрисовкиФигурыПоКоординатам(Парам01,Парам02,Парам03,Парам04, ТабПарам, "НачатьВызовНарисоватьЭллипс");
КонецФункции

&НаКлиенте
//И Я рисую тень по координатам 'ФигураX' 'ФигураY' 'ФигураW' 'ФигураH' текст надписи 'ТекстНадписи' 'ТабПараметрыРисования'
//@ЯРисуюТеньПоКоординатамТекстНадписи(Парам01,Парам02,Парам03,Парам04,ТекстНадписи,ТабПарам)
Функция ЯРисуюТеньПоКоординатамТекстНадписи(Парам01,Парам02,Парам03,Парам04,ТекстНадписи,ТабПарам) Экспорт
	//Ванесса.ПосмотретьЗначение(Парам01,Истина);
	ОбработкаОтрисовкиФигурыПоКоординатам(Парам01,Парам02,Парам03,Парам04, ТабПарам, "НачатьВызовНарисоватьТень", ТекстНадписи);
КонецФункции

&НаКлиенте
//И Я рисую надпись по точкам 'ФигураX' 'ФигураY' текст надписи 'ТекстНадписи' 'ТабПараметрыРисования'
//@ЯРисуюНадписьПоТочкамТекстНадписи(Парам01,Парам02,ТекстНадписи,ТабПарам)
Функция ЯРисуюНадписьПоТочкамТекстНадписи(Парам01,Парам02,ТекстНадписи,ТабПарам) Экспорт  
	ОбработкаОтрисовкиНадписиПоТочкам(Парам01,Парам02,ТекстНадписи,ТабПарам, "НачатьВызовПоказатьТекст");
КонецФункции

&НаКлиенте
//И Я рисую подсказку-балон по точкам 'ФигураX' 'ФигураY' текст надписи 'ТекстНадписи' 'ТабПараметрыРисования'
//@ЯРисуюПодсказку_балонПоТочкамТекстНадписи(Парам01,Парам02,Парам03,Парам04)
Функция ЯРисуюПодсказку_балонПоТочкамТекстНадписи(Парам01,Парам02,Парам03,ТабПарам) Экспорт
	ОбработкаОтрисовкиНадписиПоТочкам(Парам01,Парам02,Парам03,ТабПарам, "НачатьВызовПоказатьПодсказку");
КонецФункции

&НаКлиенте
//И клик по координатам '800' '600'
//@КликПоКоординатам(X,Y)
Функция КликПоКоординатам(Знач X, Знач Y) Экспорт
	
	ДанныеКоординат = ПолучитьДанныеКоординат(X, Y);	
	Ванесса.ВыполнитьПеремещениеМышкиВнешнейКомпонентой(ДанныеКоординат.X, ДанныеКоординат.Y);	
	Ванесса.ВнешняяКомпонентаДляСкриншотов.ЭмуляцияНажатияМыши(0);	
	
КонецФункции

&НаКлиенте
//И двойной клик по координатам '800' '600'
//@ДвойнойКликПоКоординатам(X,Y)
Функция ДвойнойКликПоКоординатам(Знач X, Знач Y) Экспорт
	
	ДанныеКоординат = ПолучитьДанныеКоординат(X, Y);	
	Ванесса.ВыполнитьПеремещениеМышкиВнешнейКомпонентой(ДанныеКоординат.X, ДанныеКоординат.Y);	
	Ванесса.ВнешняяКомпонентаДляСкриншотов.ЭмуляцияДвойногоНажатия();
	
КонецФункции

&НаКлиенте
//И правый клик по координатам '800' '600'
//@ПравыйКликПоКоординатам(X,Y)
Функция ПравыйКликПоКоординатам(Знач X, Знач Y) Экспорт
	
	ДанныеКоординат = ПолучитьДанныеКоординат(X, Y);	
	Ванесса.ВыполнитьПеремещениеМышкиВнешнейКомпонентой(ДанныеКоординат.X, ДанныеКоординат.Y);	
	Ванесса.ВнешняяКомпонентаДляСкриншотов.ЭмуляцияНажатияМыши(1);	
	
КонецФункции

&НаКлиенте
//И курсор к координатам '800' '600'
//@КурсорККоординатам(X,Y)
Функция КурсорККоординатам(Знач X, Знач Y) Экспорт
	
	ДанныеКоординат = ПолучитьДанныеКоординат(X, Y);	
	Ванесса.ВыполнитьПеремещениеМышкиВнешнейКомпонентой(ДанныеКоординат.X, ДанныеКоординат.Y);	
	
КонецФункции

&НаКлиенте
Функция ПолучитьДанныеКоординат(Знач X, Знач Y)
	
	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
	X = ОписаниеТипаЧисло.ПривестиЗначение(X);
	Y = ОписаниеТипаЧисло.ПривестиЗначение(Y);

	ДанныеКоординат = Новый Структура("X, Y", X, Y);
	Ванесса.ПересчитатьДанныеКоординатСУчетомМасштабирования(ДанныеКоординат, X, Y);
	
	Возврат Новый Структура("X, Y", X, Y);
	
КонецФункции
 
