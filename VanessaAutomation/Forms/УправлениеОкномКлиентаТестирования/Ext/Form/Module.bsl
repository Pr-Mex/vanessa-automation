&НаКлиенте
Перем ВнешняяКомпонента;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МестоположениеВнешнейКомпонентыДляСкриншотов = Параметры.МестоположениеВнешнейКомпонентыДляСкриншотов;
	ДескрипторОсновногоОкна = Параметры.ДескрипторОсновногоОкнаТекущегоКлиентаТестирования;
	PID = Параметры.PIDТекущегоКлиентаТестирования;
	ЭтоLinux = Параметры.ЭтоLinux;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтоLinux Тогда
		Возврат;
	КонецЕсли;
	
	Если ВнешняяКомпонента = Неопределено Тогда
		
		НачатьПодключениеВнешнейКомпоненты(
			Новый ОписаниеОповещения("ПослеПопыткиПодключенияВнешнейКомпоненты", ЭтаФорма),
			МестоположениеВнешнейКомпонентыДляСкриншотов, "WindowCaptureComponent", ТипВнешнейКомпоненты.Native
		);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ВнешняяКомпонента = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроизвольныйРазмер(Команда)

	УстановитьРазмер(ПозицияПоГоризонтали, ПозицияПоВертикали, РазмерПоГоризонтали, РазмерПоВертикали);
	
КонецПроцедуры

&НаКлиенте
Процедура Размер800х600(Команда)
	УстановитьРазмер(0, 0, 800, 600);
КонецПроцедуры

&НаКлиенте
Процедура Размер960х720(Команда)
	УстановитьРазмер(0, 0, 960, 720);
КонецПроцедуры

&НаКлиенте
Процедура Размер1024х768(Команда)
	УстановитьРазмер(0, 0, 1024, 768);
КонецПроцедуры

&НаКлиенте
Процедура Размер1280х960(Команда)
	УстановитьРазмер(0, 0, 1280, 960);
КонецПроцедуры

&НаКлиенте
Процедура Широкий960х540(Команда)
	УстановитьРазмер(0, 0, 960, 540);
КонецПроцедуры

&НаКлиенте
Процедура Широкий1024х576(Команда)
	УстановитьРазмер(0, 0, 1024, 576);
КонецПроцедуры

&НаКлиенте
Процедура Широкий1280х720(Команда)
	УстановитьРазмер(0, 0, 1280, 720);
КонецПроцедуры

&НаКлиенте
Процедура Широкий1600х900(Команда)
	УстановитьРазмер(0, 0, 1600, 900);
КонецПроцедуры

&НаКлиенте
Процедура СделатьСнимок(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолученСнимокОкна", ЭтаФорма);
	ВнешняяКомпонента.НачатьВызовПолучитьСнимокПроцесса(ОписаниеОповещения, PID);
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьСнимкиДочернихОкон(Команда)
	
	СписокОкон.Очистить();
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолученСписокОкон", ЭтаФорма);
	ВнешняяКомпонента.НачатьВызовПолучитьСписокОкон(ОписаниеОповещения, PID);
	
КонецПроцедуры

&НаКлиенте
Процедура Масштаб50(Команда)
	ИзменитьМасштабКартинки(Элементы.ДанныеКартинки, 50);
КонецПроцедуры

&НаКлиенте
Процедура Масштаб75(Команда)
	ИзменитьМасштабКартинки(Элементы.ДанныеКартинки, 75);
КонецПроцедуры

&НаКлиенте
Процедура Масштаб100(Команда)
	ИзменитьМасштабКартинки(Элементы.ДанныеКартинки, 100);
КонецПроцедуры

&НаКлиенте
Процедура Масштаб50_1(Команда)
	ИзменитьМасштабКартинки(Элементы.ДанныеКартинки1, 50);
КонецПроцедуры

&НаКлиенте
Процедура Масштаб75_1(Команда)
	ИзменитьМасштабКартинки(Элементы.ДанныеКартинки1, 75);
КонецПроцедуры

&НаКлиенте
Процедура Масштаб100_1(Команда)
	ИзменитьМасштабКартинки(Элементы.ДанныеКартинки1, 100);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеПопыткиПодключенияВнешнейКомпоненты(Результат, Параметры) Экспорт
	
	Если Результат Тогда
		
		ВнешняяКомпонента = Новый("AddIn.WindowCaptureComponent.WindowsControl");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПрочитатьСтрокуJSON(ТекстJSON)
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(ТекстJSON);
	
	Возврат ПрочитатьJSON(ЧтениеJSON);
	
КонецФункции

&НаКлиенте
Процедура УстановитьРазмер(ПозицияПоГоризонтали, ПозицияПоВертикали, РазмерПоГоризонтали, РазмерПоВертикали)
	
	ВнешняяКомпонента.УстановитьРазмерОкна(ДескрипторОсновногоОкна, РазмерПоГоризонтали, РазмерПоВертикали);
	ВнешняяКомпонента.УстановитьПозициюОкна(ДескрипторОсновногоОкна, ПозицияПоГоризонтали, ПозицияПоВертикали);
	ВнешняяКомпонента.РазрешитьИзменятьРазмер(ДескрипторОсновногоОкна, НЕ ЗапретитьИзменятьРазмер);
	ВнешняяКомпонента.АктивироватьОкно(ДескрипторОсновногоОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученСнимокОкна(Значение, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Значение) Тогда
		ДанныеКартинки = ПоместитьВоВременноеХранилище(Значение, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученСписокОкон(Значение, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если Значение = "" Тогда
		Возврат;
	КонецЕсли;
	
	Данные = ПрочитатьСтрокуJSON(Значение);
	Для Каждого Стр из Данные Цикл
		
		Если Стр.Class = "Internet Explorer_Hidden"
			Или Стр.Class = "V8Window"
			Или Стр.Class = "V8ConfirmationWindowTaxi" Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = СписокОкон.Добавить();
		
		ДвоичныеДанные = ВнешняяКомпонента.ПолучитьСнимокОкна(Стр.window);
		Стр.Вставить("ДвоичныеДанныеКартинки", ДвоичныеДанные);
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОконПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СписокОкон.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ДанныеКартинки1 = "";
		Возврат;
	КонецЕсли;
	
	ДанныеКартинки1 = ПоместитьВоВременноеХранилище(
		ТекущиеДанные.ДвоичныеДанныеКартинки, 
		Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьМасштабКартинки(Элемент, Размер)
	Если ЗначениеЗаполнено(ЭтаФорма[Элемент.Имя]) Тогда
		Элемент.Масштаб = Размер;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
