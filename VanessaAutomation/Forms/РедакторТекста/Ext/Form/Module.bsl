#Область ОписаниеПеременных

&НаКлиенте
Перем Ванесса;

&НаКлиенте
Перем КешОтступов;

#КонецОбласти

#Область ЭкспортныеПроцедурыИФункции

// Делает вставки виджетов
&НаКлиенте
Процедура ОтобразитьДанныеВиджетов() Экспорт
	ДополнительныеПараметры = Новый Структура;
	СтрокиПодсценариевВиджетов = Новый Массив;
	СтрокиНеявныхОбластей = Новый Массив;
	ОбновитьВставкиКодаVanessaEditor(СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей);
	Ванесса.РаскраситьСтрокиVanessaEditor(СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей);
КонецПроцедуры 

// Возвращает ид стрки дерева по данным виджета
&НаКлиенте
Функция ИдСтрокиДереваПоНомеруСтрокиВВиджете(ИдВиджета, НомерСтроки) Экспорт
	ДанныеВиджета = Ванесса.ДанныеВиджетаПоИд(ИдВиджета);
	Для Каждого Элем Из ДанныеВиджета Цикл
		Если Элем.Значение = НомерСтроки Тогда
			Возврат Элем.Ключ;
		КонецЕсли;	 
	КонецЦикла;	 
	
	Возврат Неопределено;
КонецФункции	 

// Выполняет шаги, которые выделены в редакторе
&НаКлиенте
Процедура ВыполнитьВыделенныйТекстVanessaEditor() Экспорт
	ВыделеннаяОбласть = Ванесса.VanessaEditor.getSelection();
	
	ДанныеДиапазонаСтрокДляВыполненияСценария = Новый Структура;
	ДанныеДиапазонаСтрокДляВыполненияСценария.Вставить("ВыполнениеДиапазонаШаговVanessaEditor", Истина);
	ДанныеДиапазонаСтрокДляВыполненияСценария.Вставить("НачалоДиапазона", ВыделеннаяОбласть.startLineNumber);
	ДанныеДиапазонаСтрокДляВыполненияСценария.Вставить("ОкончаниеДиапазона", ВыделеннаяОбласть.endLineNumber);
	
	Ванесса.ВыполнитьСценарии(, ДанныеДиапазонаСтрокДляВыполненияСценария);
КонецПроцедуры 

// Устанавливает флаг модифицированности у редактора
&НаКлиенте
Процедура УстановитьФлагМодифицированностиVanessaEditor() Экспорт
	Ванесса.БылиИзмененияДанныхVanessaEditor = Истина;
	Ванесса.Модифицированность = Истина;
КонецПроцедуры 

// Выполняет отрисовку точек останова в редакторе
&НаКлиенте
Процедура ОтрисоватьБрейкпоинтыVanessaEditor() Экспорт
	Данные = Новый Массив;

	БрейкпоинтыVanessaEditor = Ванесса.БрейкпоинтыVanessaEditor();
	
	Для Каждого Элем Из БрейкпоинтыVanessaEditor Цикл
		БрейкПоинт = Новый Структура;
		БрейкПоинт.Вставить("lineNumber", Элем.НомерСтроки);
		БрейкПоинт.Вставить("enable", Элем.Включен);
		БрейкПоинт.Вставить("codeWidget", Элем.ИдВиджета);
		Данные.Добавить(БрейкПоинт);
	КонецЦикла;	 

	Ванесса.VanessaEditor.decorateBreakpoints(Ванесса.ЗаписатьОбъектJSON(Данные));
КонецПроцедуры 

// Загружает данные по точкам останова из редактора
&НаКлиенте
Процедура ЗагрузитьБрейкпоинтыИзVanessaEditor() Экспорт
	БрейкпоинтыVanessaEditor = Ванесса.БрейкпоинтыVanessaEditor();
	БрейкпоинтыVanessaEditorВиджеты = Ванесса.БрейкпоинтыVanessaEditorВиджеты();
	Брейкпоинты = Ванесса.Брейкпоинты();
	
	Брейкпоинты.Очистить();
	БрейкпоинтыVanessaEditorВиджеты.Очистить();
	Для Каждого Элем Из БрейкпоинтыVanessaEditor Цикл
		Если Элем.Включен Тогда
			Если Элем.ИдВиджета = 0 Тогда //основная фича
				Брейкпоинты.Добавить(Элем.НомерСтроки);
			Иначе
				ДанныеВиджета = Ванесса.ДанныеВиджетаПоИд(Элем.ИдВиджета);
				Если ДанныеВиджета <> Неопределено Тогда
					Для Каждого ТекДанныеВиджета Из ДанныеВиджета Цикл
						Если ТекДанныеВиджета.Значение = Элем.НомерСтроки Тогда
							ИдСтроки = ТекДанныеВиджета.Ключ;
							БрейкпоинтыVanessaEditorВиджеты.Добавить(ИдСтроки);
							Прервать;
						КонецЕсли;	 
					КонецЦикла;	 
				КонецЕсли;	 
			КонецЕсли;	 
		КонецЕсли;	 
	КонецЦикла;	 
КонецПроцедуры 

// Обновляет данные по точкам останова в редакторе
&НаКлиенте
Процедура ОбновитьДанныеПоБрейкпоинтамVanessaEditor(Данные) Экспорт
	ДанныеVanessaEditor = Ванесса.ПрочитатьОбъектJSON(Данные);

	БрейкпоинтыVanessaEditor = Ванесса.БрейкпоинтыVanessaEditor();
	
	БрейкпоинтыVanessaEditor.Очистить();
	Для Каждого Элем Из ДанныеVanessaEditor Цикл
		ДанныеБрейкПоинта = Новый Структура;
		ДанныеБрейкПоинта.Вставить("НомерСтроки", Элем.lineNumber);
		ДанныеБрейкПоинта.Вставить("ИдВиджета", Элем.codeWidget);
		ДанныеБрейкПоинта.Вставить("Включен", Истина);
		
		БрейкпоинтыVanessaEditor.Добавить(ДанныеБрейкПоинта);
	КонецЦикла;	 

КонецПроцедуры 

// Копирует текст ошибки в буфер обмена
&НаКлиенте
Процедура СкопироватьТекстОшибкиВБуферОбменаVanessaEditor(ДанныеОшибки) Экспорт
	ТекстОшибки = ДанныеОшибки.ErrorText;
	ОписаниеОповещения = Новый ОписаниеОповещения("СкопироватьТекстОшибкиВБуферОбменаVanessaEditorПродолжение", ЭтаФорма);
	БуферОбмена = Ванесса.БуферОбмена();
	Если БуферОбмена = Неопределено Тогда
		ВызватьИсключение Ванесса.ПодставитьПараметрыВСтроку(
			Ванесса.Локализовать("Необходимо выключить использование компоненты <VanessaExt>.")
		);
	КонецЕсли;	 
	БуферОбмена.НачатьВызовЗаписатьТекст(ОписаниеОповещения, ТекстОшибки);
КонецПроцедуры 

// Загружает текст фича файла в редактор
&НаКлиенте
Процедура ЗагрузитьФичаФайлVanessaEditor(ДополнительныеПараметры) Экспорт
	Если НЕ Ванесса.Объект.ИспользоватьРедакторVanessaEditor Тогда
		Возврат;
	КонецЕсли;	 
	
	НайденныеПроблемы = Ванесса.НайденныеПроблемы();
	
	НайденныеПроблемы.Очистить();
	VanessaEditor = Ванесса.VanessaEditor;
	VanessaEditor.decorateProblems(Ванесса.ЗаписатьОбъектJSON(Новый Массив));
	VanessaEditor.clearRuntimeProgress();
	
	Если Ванесса.ФайлСуществуетКомандаСистемы(Ванесса.Объект.КаталогФич) Тогда
		Если НЕ Ванесса.ЭтоКаталогКомандаСистемы(Ванесса.Объект.КаталогФич) Тогда
			Попытка
				Текст = Новый ЧтениеТекста;
				Текст.Открыть(Ванесса.Объект.КаталогФич, "UTF-8");
				ТекстФичи = Текст.Прочитать();
				Текст.Закрыть();
				
				Ванесса.ИгнорироватьИзмененияVanessaEditor = Истина;
				СброситьФлагМодифицированностиVanessaEditor();
				НадоУстановитьТекст = Истина;
				Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
					И ДополнительныеПараметры.Свойство("ЗагрузкаФичиИзVanessaEditor")
					И ДополнительныеПараметры.ЗагрузкаФичиИзVanessaEditor Тогда
					НадоУстановитьТекст = Ложь;
				КонецЕсли;	 
				
				Если НадоУстановитьТекст Тогда
					VanessaEditor.setContent(ТекстФичи);
				КонецЕсли;
				
				Если ДополнительныеПараметры.НадоОбновитьВставкиVanessaEditor ИЛИ НадоУстановитьТекст Тогда
					СтрокиПодсценариевВиджетов = ДополнительныеПараметры.СтрокиПодсценариевВиджетов;
					СтрокиНеявныхОбластей = ДополнительныеПараметры.СтрокиНеявныхОбластей;
					ОбновитьВставкиКодаVanessaEditor(СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей);
					ДополнительныеПараметры.Вставить("СтрокиПодсценариевВиджетов", СтрокиПодсценариевВиджетов);
					ДополнительныеПараметры.Вставить("СтрокиНеявныхОбластей", СтрокиНеявныхОбластей);
				КонецЕсли;	 
				
				ТекущаяПозицияVanessaEditor = Ванесса.ТекущаяПозицияVanessaEditor;
				Если ТекущаяПозицияVanessaEditor <> Неопределено Тогда
					Ванесса.ОбновитьТекущуюСтрокуРедактораСРасчетомВиджета(ТекущаяПозицияVanessaEditor);
					Ванесса.ТекущаяПозицияVanessaEditor = Неопределено;
				КонецЕсли;	 
				
			Исключение
				ВызватьИсключение Ванесса.ПодставитьПараметрыВСтроку(
					Ванесса.Локализовать("Не получилось прочитать файл <%1>."),
					Ванесса.Объект.КаталогФич
				);
			КонецПопытки;
		КонецЕсли;	 
	КонецЕсли;	 
	
	УстановитьСписокШаговVanessaEditor(Ванесса.Объект.ЯзыкГенератораGherkin);
	
	Если ДополнительныеПараметры.Свойство("ДанныеОшибки") Тогда
		ШагиДляVanessaEditor = Новый Массив;
		ШагиДляVanessaEditor.Добавить(ДополнительныеПараметры.ДанныеОшибки.НомерСтроки);
		Ванесса.VanessaEditor.setRuntimeProgress("error", Ванесса.ЗаписатьОбъектJSON(ШагиДляVanessaEditor));
		
		ДанныеПоОшибке = Новый Структура;
		ДанныеПоОшибке.Вставить("id", Строка(Новый УникальныйИдентификатор));
		ДанныеПоОшибке.Вставить("LineNumber", ДополнительныеПараметры.ДанныеОшибки.НомерСтроки);
		ДанныеПоОшибке.Вставить("ErrorText", ДополнительныеПараметры.ДанныеОшибки.ОписаниеОшибки);
		
		ПоказатьОшибкуВРедактореVanessaEditor(ДополнительныеПараметры.ДанныеОшибки.НомерСтроки, "", ДанныеПоОшибке,
			ДополнительныеПараметры.ДанныеОшибки.ОписаниеОшибки, Неопределено, Неопределено)
		
	КонецЕсли;	 
	
КонецПроцедуры 

// Устанавливет набор ключевых слов в редактор.
&НаКлиенте
Процедура УстановитьКлючевыеСловаVanessaEditor() Экспорт
	Ванесса.VanessaGherkinProvider.setKeywords(ПолучитьКлючевыеСловаGherkin());
КонецПроцедуры 

// Устанавливет шагу статус текущего шага в редакторе.
&НаКлиенте
Процедура УстановитьТекущийШагVanessaEditor(ТекШаг) Экспорт
	УстановитьСтатусШага("current", ТекШаг, Истина);
КонецПроцедуры 

// Устанавливает шагу статус выполнен в редакторе.
&НаКлиенте
Процедура УстановитьВыполненныйШагVanessaEditor(ТекШаг) Экспорт
	УстановитьСтатусШага("complete", ТекШаг, Ложь);
КонецПроцедуры 

// Устанавливает шагу статус выполнен в редакторе.
&НаКлиенте
Процедура УстановитьУпавшийШагVanessaEditor(ТекШаг, ТекстОшибки, ОписаниеОшибкиКраткоVanessaEditor, СтрокаШага) Экспорт
	УстановитьСтатусШага("error", ТекШаг, Ложь);
	
	ИдВСтрокеВиджета = Неопределено;
	ИдВиджета = Неопределено;
	ОпределитьДанныеВиджета(ИдВСтрокеВиджета, ИдВиджета, ТекШаг.ИдСтроки);
	Если ИдВСтрокеВиджета = Неопределено Тогда
		ИдВСтрокеВиджета = 0;
		НомерСтрокиВФиче = ТекШаг.НомерСтрокиВФиче;
	Иначе	
		НомерСтрокиВФиче = ИдВСтрокеВиджета;
	КонецЕсли;	 
	
	ДанныеПоОшибке = Новый Структура;
	ДанныеПоОшибке.Вставить("id", Строка(Новый УникальныйИдентификатор));
	ДанныеПоОшибке.Вставить("LineNumber", ТекШаг.НомерСтрокиВФиче);
	ДанныеПоОшибке.Вставить("ErrorText", ТекстОшибки);
	
	Если ИдВиджета = Неопределено И ТекШаг.ЭтоШагПодсценария Тогда
		НомерСтрокиВФиче = ТекШаг.СтрокаШагаПодсценарияФичи;
	КонецЕсли;	 
	
	ПоказатьОшибкуВРедактореVanessaEditor(НомерСтрокиВФиче, ИдВиджета, ДанныеПоОшибке,
		ОписаниеОшибкиКраткоVanessaEditor, ТекШаг, СтрокаШага);
	
КонецПроцедуры 

// Устанавливает значения переменных для работы автокомплита.
&НаКлиенте
Процедура УстановитьЗначенияПеременныхVanessaEditor() Экспорт
	Если НЕ Ванесса.Объект.ИспользоватьРедакторVanessaEditor Тогда
		Возврат;
	КонецЕсли;	 
	
	Ванесса.VanessaGherkinProvider.setVariables(ПолучитьЗначенияПеременныхVanessaEditor());
КонецПроцедуры 

// Устанавливает список шагов для работы автокомплита редактора
&НаКлиенте
Процедура УстановитьСписокШаговVanessaEditor(Язык) Экспорт
	Если НЕ Ванесса.Объект.ИспользоватьРедакторVanessaEditor Тогда
		Возврат;
	КонецЕсли;	
	
	Ванесса.VanessaGherkinProvider.setStepList(ПолучитьСписокШаговVanessaEditor(Язык));
КонецПроцедуры 

// Устанавливает список команд редактора
&НаКлиенте
Процедура УстановитьСписокКомандVanessaEditor() Экспорт
	Ванесса.VanessaEditor.addCommands(ПолучитьСписокКомандVanessaEditor());
КонецПроцедуры 

// Выгружает данные и очищает память редактора
&НаКлиенте
Процедура ВыгрузитьДанныеVanessaEditor() Экспорт
	Ванесса.VanessaEditor = Неопределено;
	Ванесса.VanessaGherkinProvider = Неопределено;
	Если Ванесса.Объект.ИспользоватьРедакторVanessaEditor Тогда
		Ванесса.Элементы.VanessaEditor.Document.defaultView.location = "about:blank";
	КонецЕсли;	 
КонецПроцедуры 

// Устанавливает тему редактора
&НаКлиенте
Процедура УстановитьТемуVanessaEditor() Экспорт
	Если НЕ Ванесса.Объект.ИспользоватьРедакторVanessaEditor Тогда
		Возврат;
	КонецЕсли;	
	
	Ванесса.VanessaEditor.setTheme(Ванесса.Объект.ТемаРедактораVanessaEditor);
КонецПроцедуры 

// Возвращает краткий текст ошибки для отображения в редакторе
&НаКлиенте
Функция ОписаниеОшибкиКраткоVanessaEditor(Знач Стр) Экспорт
	Пока Найти(Стр, "}:") > 0 Цикл
		Поз = Найти(Стр, "}:");
		Стр = СокрЛ(Сред(Стр, Поз + 2));
	КонецЦикла;	 
	
	Возврат Стр; 
КонецФункции	 

// Получает значение таблицы Gherkin по текущей строке редактора
&НаКлиенте
Функция ТаблицаGherkinИзVanessaEditor(ТекущаяСтрокаVanessaEditor, МинСтр, МаксСтр, ДлинаМаксСтр) Экспорт
	VanessaEditor = Ванесса.VanessaEditor;
	ТекущаяСтрокаVanessaEditor = VanessaEditor.getPosition().LineNumber;
	ПолныйТекстСтроки = VanessaEditor.getLineContent(ТекущаяСтрокаVanessaEditor);
	ТекстСтроки = СокрЛ(ПолныйТекстСтроки);
	
	МинСтр = ТекущаяСтрокаVanessaEditor;
	МаксСтр = ТекущаяСтрокаVanessaEditor;
	ДлинаМаксСтр = СтрДлина(ПолныйТекстСтроки);
	
	МассивСтрокДляРедактирования = Новый Массив;
	МассивСтрокДляРедактирования.Добавить(ТекстСтроки);
	
	ТекНомСтр = ТекущаяСтрокаVanessaEditor;
	Пока Истина Цикл
		ТекНомСтр = ТекНомСтр - 1;
		ТекстСтроки = СокрЛ(VanessaEditor.getLineContent(ТекНомСтр));
		Если Лев(ТекстСтроки, 1) <> "|" Тогда
			Прервать;
		КонецЕсли;	
		
		МинСтр = ТекНомСтр;
		
		МассивСтрокДляРедактирования.Вставить(0, ТекстСтроки);
	КонецЦикла;	
	
	ТекНомСтр = ТекущаяСтрокаVanessaEditor;
	Пока Истина Цикл
		ТекНомСтр = ТекНомСтр + 1;
		Стр = VanessaEditor.getLineContent(ТекНомСтр);
		ТекстСтроки = СокрЛ(Стр);
		Если Лев(ТекстСтроки, 1) <> "|" Тогда
			Прервать;
		КонецЕсли;	
		
		МаксСтр = ТекНомСтр;
		ДлинаМаксСтр = СтрДлина(Стр);
		
		МассивСтрокДляРедактирования.Добавить(ТекстСтроки);
	КонецЦикла;	
	
	Возврат МассивСтрокДляРедактирования;
	
КонецФункции	 

// Включает чтение активной формы клиента тестирования
&НаКлиенте
Процедура ВключитьЧтениеДанныхТекущейФормыVanessaEditor() Экспорт
	Если НЕ Ванесса.Объект.ИспользоватьРедакторVanessaEditor Тогда
		Возврат;
	КонецЕсли;	 
	
	Если НЕ Ванесса.Объект.ИспользоватьДанныеТекущейФормыПриПодбореШагов Тогда
		Возврат;
	КонецЕсли;	 
	
	ПодключитьОбработчикОжидания("ЧтениеДанныхТекущейФормыVanessaEditor", 2, Ложь);
КонецПроцедуры 

// Выключает чтение активной формы клиента тестирования
&НаКлиенте
Процедура ВыключитьЧтениеДанныхТекущейФормыVanessaEditor()Экспорт
	ОтключитьОбработчикОжидания("ЧтениеДанныхТекущейФормыVanessaEditor");
КонецПроцедуры 

// Устанавливает значения текущей формы в объект VanessaGherkinProvider
&НаКлиенте
Процедура УстановитьЗначенияЭлементовТекущегоОкнаVanessaEditor() Экспорт
	Если НЕ Ванесса.Объект.ИспользоватьРедакторVanessaEditor Тогда
		Возврат;
	КонецЕсли;	
	
	Ванесса.VanessaGherkinProvider.setElements(ПолучитьЗначенияЭлементовТекущегоОкнаVanessaEditor());
КонецПроцедуры 

// Сохраняет данные текущей формы
&НаКлиенте
Процедура ЗапомнитьДанныеТекущейФормыVanessaEditor(Данные, ТекущиеЗначения) Экспорт
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	БылиИзменения = Ложь;
	
	УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "Заголовок окна", Данные.ЗаголовокОкна, БылиИзменения);
	УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "Заголовок формы", Данные.ЗаголовокФормы, БылиИзменения);
	УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "ИмяФормы", Данные.ИмяФормы, БылиИзменения);
	Если Данные.Свойство("ТекТаблица") Тогда
		УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "ИмяТаблицы", Данные.ТекТаблица.Имя, БылиИзменения);
		УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "ИмяКолонки", Данные.ТекЭлемент.Имя, БылиИзменения);
		УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "ИмяПоля", Данные.ТекЭлемент.Имя, БылиИзменения);
		УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "Заголовок колонки", Данные.ТекЭлемент.ТекстЗаголовка, БылиИзменения);
		УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "Заголовок поля", Данные.ТекЭлемент.ТекстЗаголовка, БылиИзменения);
	Иначе
		Если ТипЗнч(Данные.ТекЭлемент) = Тип("ТестируемоеПолеФормы") Тогда
			УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "ИмяПоля", Данные.ТекЭлемент.Имя, БылиИзменения);
			УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "Заголовок поля", Данные.ТекЭлемент.ТекстЗаголовка, БылиИзменения);
			Попытка
				ЗначениеПоля = ЗначениеПоляТекущегоПоляVanessaEditor(Данные.ТекЭлемент);
			Исключение
				ЗначениеПоля = "";
			КонецПопытки;
			УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "ЗначениеПоля", ЗначениеПоля, БылиИзменения);
			
			Если Данные.ТекЭлемент.Вид = ВидПоляФормы.ПолеПереключателя Тогда
				Попытка
					ОтображаемыйТекст = Данные.ТекЭлемент.ПолучитьОтображаемыйТекст();
				Исключение
					ОтображаемыйТекст = "";
				КонецПопытки;
				
				УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "Значение переключателя", ОтображаемыйТекст, БылиИзменения);
			КонецЕсли;	 
		ИначеЕсли ТипЗнч(Данные.ТекЭлемент) = Тип("ТестируемаяКнопкаФормы") Тогда
			УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "ИмяКнопки", Данные.ТекЭлемент.Имя, БылиИзменения);
			УстановитьЗначениеТекущейФормыVanessaEditor(ТекущиеЗначения, "Заголовок кнопки", Данные.ТекЭлемент.ТекстЗаголовка, БылиИзменения);
		КонецЕсли;	 
	КонецЕсли;	 
	
	Если БылиИзменения Тогда
		УстановитьЗначенияЭлементовТекущегоОкнаVanessaEditor();
	КонецЕсли;	 
КонецПроцедуры 

// Сбрасывает флаги модифицированность для формы VA и для редактора
&НаКлиенте
Процедура СброситьФлагМодифицированностиVanessaEditor() Экспорт
	Ванесса.БылиИзмененияДанныхVanessaEditor = Ложь;
	Ванесса.Модифицированность = Ложь;
КонецПроцедуры 

// Сохраняет текст из редактора в файл.
&НаКлиенте
Процедура СохранитьТекстИзVanessaEditorВФайл() Экспорт
	ТекстИзVanessaEditor = Ванесса.VanessaEditor.getContent();
	СброситьФлагМодифицированностиVanessaEditor();
	
	ЗТ = Новый ЗаписьТекста(Ванесса.Объект.КаталогФич,"UTF-8", , Ложь); 
	ЗТ.Записать(ТекстИзVanessaEditor); 
	ЗТ.Закрыть();
КонецПроцедуры 

// Сохраняет текст из редактора в файл и выполняет асинхронный обработчик.
&НаКлиенте
Функция СохранитьТекстИзVanessaEditorВФайлИВыполнитьОбработчик(ДопПараметры = Неопределено) Экспорт
	СохранитьТекстИзVanessaEditorВФайл();
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = Новый Структура;
	КонецЕсли;	 
	ДопПараметры.Вставить("ВыполнитьСценарии", Истина);
	ДопПараметры.Вставить("ЗагрузкаФичиИзVanessaEditor", Истина);
	Ванесса.ЗагрузитьФичи(ДопПараметры);
КонецФункции	 

// Возвращает строку для установки списка доступных шагов в редактор.
&НаКлиенте
Функция ДанныеШаговДляРедактора(СтруктураПараметров) Экспорт
	
	ТаблицаИзвестныхStepDefinition = СтруктураПараметров.ТаблицаИзвестныхStepDefinition;
	ТаблицаУжеСуществующихСценариев = СтруктураПараметров.ТаблицаУжеСуществующихСценариев;
	МодульПарсерФич = СтруктураПараметров.МодульПарсерФич;
	Язык = СтруктураПараметров.Язык;
	Ванесса = СтруктураПараметров.Ванесса;
	
	ДанныеДляПеревода = Неопределено;
	ДанныеКатегорийШагов = Неопределено;
	ДанныеПереводаШагов = Неопределено;
	Если Язык <> "ru" Тогда
		ДанныеДляПеревода = ПолучитьДанныеДляПеревода(СтруктураПараметров.КешДанныеПеревода);
		ДанныеКатегорийШагов = ДанныеДляПеревода.ДанныеКатегорийШагов;
		ДанныеПереводаШагов = ДанныеДляПеревода.ДанныеПереводаШагов;
	КонецЕсли;	 
	
	ДанныеФичи = Новый Структура;
	ДанныеФичи.Вставить("ЯзыкФичи", Язык);
		
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ЗаписатьНачалоМассива();
	Для Каждого СтрокаТаблицаИзвестныхStepDefinition Из ТаблицаИзвестныхStepDefinition Цикл
		ВтораяЧастьСтрокиПозиция = -1;
		
		ПримерИспользования = СтрокаТаблицаИзвестныхStepDefinition.ПредставлениеТеста;
		ПримерИспользования_ru = ПримерИспользования;
		ОписаниеШага = СтрокаТаблицаИзвестныхStepDefinition.ОписаниеШага;
		ТипШага = СтрокаТаблицаИзвестныхStepDefinition.ТипШага;
		Если Язык <> "ru" Тогда
			ДанныеПереводаШага = ДанныеПереводаШагов[СтрокаТаблицаИзвестныхStepDefinition.СтрокаДляПоиска];
			Если ДанныеПереводаШага <> Неопределено Тогда
				ПримерИспользования = УбратьЛишниеСимволыИзПредставленияШага(ДанныеПереводаШага.ТекстПереводаШаг);
				ОписаниеШага = ДанныеПереводаШага.ТекстПереводаОписание;
				ТипШага = ПеревестиТипШага(ДанныеКатегорийШагов, ТипШага);
			КонецЕсли;	 
		КонецЕсли;	 
		
		МодульПарсерФич.ПолучитьКлючевоеСлово(ДанныеФичи,
			ПримерИспользования, ВтораяЧастьСтрокиПозиция);
		ИмяШагаБезКлючевогоСлова = СокрЛП(Сред(ПримерИспользования, ВтораяЧастьСтрокиПозиция));
		
		Если ПустаяСтрока(ИмяШагаБезКлючевогоСлова) Тогда
			Продолжить;
		КонецЕсли;	 
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("filterText");
		ЗаписьJSON.ЗаписатьЗначение(ИмяШагаБезКлючевогоСлова);
		ЗаписьJSON.ЗаписатьИмяСвойства("insertText");
		ЗаписьJSON.ЗаписатьЗначение(ПримерИспользования);
		ЗаписьJSON.ЗаписатьИмяСвойства("sortText");
		ЗаписьJSON.ЗаписатьЗначение(ПолучитьСтрокуСортировки(ПримерИспользования_ru));
		ЗаписьJSON.ЗаписатьИмяСвойства("documentation");
		ЗаписьJSON.ЗаписатьЗначение(ОписаниеШага);
		ЗаписьJSON.ЗаписатьИмяСвойства("kind");
		ЗаписьJSON.ЗаписатьЗначение(ТипШага(СтрокаТаблицаИзвестныхStepDefinition));
		ЗаписьJSON.ЗаписатьИмяСвойства("section");
		ЗаписьJSON.ЗаписатьЗначение(ТипШага);
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	КонецЦикла;	 
	
	Для Каждого СтрокаТаблицаУжеСуществующихСценариев Из ТаблицаУжеСуществующихСценариев Цикл
		ПримерИспользования = СтрокаТаблицаУжеСуществующихСценариев.ПримерИспользования;
		ПримерИспользования_ru = ПримерИспользования;
		ОписаниеШага = СтрокаТаблицаУжеСуществующихСценариев.ОписаниеШага;
		ТипШага = СтрокаТаблицаУжеСуществующихСценариев.ТипШага;
		Если Язык <> "ru" Тогда
			ДанныеПереводаШага = ДанныеПереводаШагов[СтрокаТаблицаУжеСуществующихСценариев.Снипет];
			Если ДанныеПереводаШага <> Неопределено Тогда
				ПримерИспользования = УбратьЛишниеСимволыИзПредставленияШага(ДанныеПереводаШага.ТекстПереводаШаг);
				ОписаниеШага = ДанныеПереводаШага.ТекстПереводаОписание;
				ТипШага = ПеревестиТипШага(ДанныеКатегорийШагов, ТипШага);
			КонецЕсли;	 
		КонецЕсли;	 
		
		ВтораяЧастьСтрокиПозиция = -1;
		МодульПарсерФич.ПолучитьКлючевоеСлово(ДанныеФичи, ПримерИспользования, ВтораяЧастьСтрокиПозиция);
		ИмяШагаБезКлючевогоСлова = СокрЛП(Сред(ПримерИспользования, ВтораяЧастьСтрокиПозиция));
		
		Если ПустаяСтрока(ИмяШагаБезКлючевогоСлова) Тогда
			ПримерИспользования = "Дано " + СтрокаТаблицаУжеСуществующихСценариев.ИмяСценария;
			ВтораяЧастьСтрокиПозиция = -1;
			МодульПарсерФич.ПолучитьКлючевоеСлово(ДанныеФичи, ПримерИспользования, ВтораяЧастьСтрокиПозиция);
			ИмяШагаБезКлючевогоСлова = СокрЛП(Сред(ПримерИспользования, ВтораяЧастьСтрокиПозиция));
		КонецЕсли;	 
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("filterText");
		ЗаписьJSON.ЗаписатьЗначение(ИмяШагаБезКлючевогоСлова);
		ЗаписьJSON.ЗаписатьИмяСвойства("insertText");
		ЗаписьJSON.ЗаписатьЗначение(ПримерИспользования);
		ЗаписьJSON.ЗаписатьИмяСвойства("sortText");
		ЗаписьJSON.ЗаписатьЗначение(ПолучитьСтрокуСортировки(ПримерИспользования_ru));
		ЗаписьJSON.ЗаписатьИмяСвойства("documentation");
		ЗаписьJSON.ЗаписатьЗначение(ОписаниеШага);
		ЗаписьJSON.ЗаписатьИмяСвойства("kind");
		ЗаписьJSON.ЗаписатьЗначение(17);
		ЗаписьJSON.ЗаписатьИмяСвойства("section");
		ЗаписьJSON.ЗаписатьЗначение(ТипШага);
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	КонецЦикла;	 
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	Данные = ЗаписьJSON.Закрыть();
	
	Возврат Данные;
КонецФункции	 

// Делает первичную инициализацию модуля.
&НаКлиенте
Функция ИнициализацияФормы(ВладелецФормы) Экспорт
	Ванесса = ВладелецФормы;
	КешОтступов = Новый Соответствие;
КонецФункции	 

// Активизирует строку дерева шагов, которая соответствует текущей строке редактора.
&НаКлиенте
Функция АктивизироватьСтрокуДереваСоответствующуюVanessaEditor() Экспорт
	ПозицияVanessaEditor = Ванесса.VanessaEditor.getPosition();
	ТекущаяСтрокаVanessaEditor = ПозицияVanessaEditor.LineNumber;
	ТекстСтроки = Ванесса.VanessaEditor.getLineContent(ТекущаяСтрокаVanessaEditor, ПозицияVanessaEditor.codeWidget);
	НашлиСтроку = Ложь;
	Если НЕ ЭтоЗначимаяСтрокаVanessaEditor(ТекстСтроки) Тогда
		Пока Истина Цикл
			ТекущаяСтрокаVanessaEditor = ТекущаяСтрокаVanessaEditor + 1;
			Попытка
				ТекстСтроки = Ванесса.VanessaEditor.getLineContent(ТекущаяСтрокаVanessaEditor);
			Исключение
				Прервать;
			КонецПопытки;
			
			Если ТекстСтроки = Неопределено Тогда
				Прервать;
			КонецЕсли;	 
			
			Если ЭтоЗначимаяСтрокаVanessaEditor(ТекстСтроки) Тогда
				НашлиСтроку = Истина;
				Прервать;
			КонецЕсли;	 
		КонецЦикла;	
	Иначе
		НашлиСтроку = Истина;
	КонецЕсли;	 
	
	Если НЕ НашлиСтроку Тогда
		Возврат Ложь;
	КонецЕсли;	 
	
	Если ПустаяСтрока(ПозицияVanessaEditor.codeWidget) Тогда
		ТекущиеДанные = Ванесса.Элементы.ДеревоТестов.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.НомерСтрокиВФиче <> ТекущаяСтрокаVanessaEditor Тогда
				Ванесса.СделатьСтрокуДереваТекущейПоНомеруСтрокиФичи(ТекущаяСтрокаVanessaEditor);
			КонецЕсли;
		Иначе	
			Ванесса.СделатьСтрокуДереваТекущейПоНомеруСтрокиФичи(ТекущаяСтрокаVanessaEditor);
		КонецЕсли;	
	Иначе
		ИдСтрокиДерева = ИдСтрокиДереваПоНомеруСтрокиВВиджете(ПозицияVanessaEditor.codeWidget, ПозицияVanessaEditor.LineNumber);
		Если ИдСтрокиДерева <> Неопределено Тогда
			Ванесса.СделатьСтрокуДереваТекущейПоИдСтроки(ИдСтрокиДерева);
		КонецЕсли;	 
	КонецЕсли;	 
	
	Возврат Истина;
КонецФункции

// Делает переход из редактора в дерево шагов.
&НаКлиенте
Процедура ПерейтиВДеревоШаговVanessaEditor() Экспорт
	Если Ванесса.БылиИзмененияДанныхVanessaEditor И Ванесса.Объект.ИспользоватьРедакторVanessaEditor Тогда
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ВыполнитьПроцедуруЧерезОбработчикОжидания", "ПерейтиВДеревоШаговVanessaEditor");
		СохранитьТекстИзVanessaEditorВФайлИВыполнитьОбработчик(ДопПараметры);
		Возврат;
	КонецЕсли;	 
	
	АктивизироватьСтрокуДереваСоответствующуюVanessaEditor();
	Ванесса.Объект.ИспользоватьРедакторVanessaEditor = Ложь;
	Ванесса.УстановитьУсловноеОформление();
КонецПроцедуры 

// Показывает подробную информацию о шаге.
&НаКлиенте
Процедура ПоказатьПодробнуюИнформациюОШаге(Знач Стр) Экспорт
	Поз = Найти(Стр, "#info:");
	Стр = Сред(Стр, Поз + 6);
	Стр = НРег(СтрЗаменить(Стр,"-",""));
	
	Язык = Ванесса.Объект.ЯзыкГенератораGherkin;
	
	СтруктураПараметров = СтруктураПараметровДляПолученияДанныхШагов(Язык);
	
	ДанныеДляПеревода = Неопределено;
	ДанныеКатегорийШагов = Неопределено;
	ДанныеПереводаШагов = Неопределено;
	Если Язык <> "ru" Тогда
		ДанныеДляПеревода = ПолучитьДанныеДляПеревода(СтруктураПараметров.КешДанныеПеревода);
		ДанныеКатегорийШагов = ДанныеДляПеревода.ДанныеКатегорийШагов;
		ДанныеПереводаШагов = ДанныеДляПеревода.ДанныеПереводаШагов;
	КонецЕсли;	 
	
	Для Каждого СтрокаТаблицаИзвестныхStepDefinition Из Ванесса.ТаблицаИзвестныхStepDefinition Цикл
		Если СтрокаТаблицаИзвестныхStepDefinition.СтрокаДляПоиска = Стр Тогда
			ДанныеШага = Новый Структура;
			ДанныеШага.Вставить("ИмяФайла", СтрокаТаблицаИзвестныхStepDefinition.ИмяФайла);
			ДанныеШага.Вставить("ОписаниеШага", СтрокаТаблицаИзвестныхStepDefinition.ОписаниеШага);
			ДанныеШага.Вставить("ПредставлениеТеста", СтрокаТаблицаИзвестныхStepDefinition.ПредставлениеТеста);
			ДанныеШага.Вставить("ТипШага", СтрокаТаблицаИзвестныхStepDefinition.ТипШага);
			ДанныеШага.Вставить("ИмяПроцедуры", СтрокаТаблицаИзвестныхStepDefinition.СтрокаРеальнойПроцедуры);
			
			ОткрытьФорму(Ванесса.ПолучитьИмяОбработкиVA() + ".Форма.ПодробнаяИнформацияОШаге", ДанныеШага, Ванесса);
			Возврат;
		КонецЕсли;	 
		
		Если Язык <> "ru" Тогда
			ДанныеПереводаШага = ДанныеПереводаШагов[СтрокаТаблицаИзвестныхStepDefinition.СтрокаДляПоиска];
			Если ДанныеПереводаШага <> Неопределено И ДанныеПереводаШага.СтрокаДляПоискаПеревод = Стр Тогда
				ПримерИспользования = УбратьЛишниеСимволыИзПредставленияШага(ДанныеПереводаШага.ТекстПереводаШаг);
				ОписаниеШага = ДанныеПереводаШага.ТекстПереводаОписание;
				ТипШага = ПеревестиТипШага(ДанныеКатегорийШагов, СтрокаТаблицаИзвестныхStepDefinition.ТипШага);
				
				ДанныеШага = Новый Структура;
				ДанныеШага.Вставить("ИмяФайла", СтрокаТаблицаИзвестныхStepDefinition.ИмяФайла);
				ДанныеШага.Вставить("ОписаниеШага", ОписаниеШага);
				ДанныеШага.Вставить("ПредставлениеТеста", ПримерИспользования);
				ДанныеШага.Вставить("ТипШага", ТипШага);
				ДанныеШага.Вставить("ИмяПроцедуры", СтрокаТаблицаИзвестныхStepDefinition.СтрокаРеальнойПроцедуры);
				
				ОткрытьФорму(Ванесса.ПолучитьИмяОбработкиVA() + ".Форма.ПодробнаяИнформацияОШаге", ДанныеШага, Ванесса);
				Возврат;
			КонецЕсли;	 
		КонецЕсли;	 
	КонецЦикла;	 
	
	ТаблицаУжеСуществующихСценариев = Ванесса.ТаблицаУжеСуществующихСценариев();
	Для Каждого СтрокаТаблицаУжеСуществующихСценариев Из ТаблицаУжеСуществующихСценариев Цикл
		Если СтрокаТаблицаУжеСуществующихСценариев.Снипет = Стр Тогда
			ДанныеШага = Новый Структура;
			ДанныеШага.Вставить("ИмяФайла", СтрокаТаблицаУжеСуществующихСценариев.ИмяФайла);
			ДанныеШага.Вставить("ОписаниеШага", СтрокаТаблицаУжеСуществующихСценариев.ОписаниеШага);
			ДанныеШага.Вставить("ПредставлениеТеста", СтрокаТаблицаУжеСуществующихСценариев.ПримерИспользования);
			ДанныеШага.Вставить("ТипШага", СтрокаТаблицаУжеСуществующихСценариев.ТипШага);
			
			ОткрытьФорму(Ванесса.ПолучитьИмяОбработкиVA() + ".Форма.ПодробнаяИнформацияОШаге", ДанныеШага, Ванесса);
			Возврат;
		КонецЕсли;	 
		
		Если Язык <> "ru" Тогда
			ДанныеПереводаШага = ДанныеПереводаШагов[СтрокаТаблицаУжеСуществующихСценариев.Снипет];
			Если ДанныеПереводаШага <> Неопределено И ДанныеПереводаШага.СтрокаДляПоискаПеревод = Стр Тогда
				ПримерИспользования = УбратьЛишниеСимволыИзПредставленияШага(ДанныеПереводаШага.ТекстПереводаШаг);
				ОписаниеШага = ДанныеПереводаШага.ТекстПереводаОписание;
				ТипШага = ПеревестиТипШага(ДанныеКатегорийШагов, СтрокаТаблицаУжеСуществующихСценариев.ТипШага);
				
				ДанныеШага = Новый Структура;
				ДанныеШага.Вставить("ИмяФайла", СтрокаТаблицаУжеСуществующихСценариев.ИмяФайла);
				ДанныеШага.Вставить("ОписаниеШага", ОписаниеШага);
				ДанныеШага.Вставить("ПредставлениеТеста", ПримерИспользования);
				ДанныеШага.Вставить("ТипШага", ТипШага);
				
				ОткрытьФорму(Ванесса.ПолучитьИмяОбработкиVA() + ".Форма.ПодробнаяИнформацияОШаге", ДанныеШага, Ванесса);
				Возврат;
			КонецЕсли;	 
		КонецЕсли;	 
		
	КонецЦикла;	 
КонецПроцедуры

// Устанавливает некоторые строки, которые используются в редакторе.
&НаКлиенте
Процедура УстановитьЛокализациюСообщенийVanessaEditor() Экспорт
	Ванесса.VanessaGherkinProvider.setSyntaxMsg(Ванесса.Локализовать("Синтаксическая ошибка: шаг не найден."));
КонецПроцедуры 

// Раскрашивает строку подсценария, когда весь подсенарий выполнен
&НаКлиенте
Функция ОбновитьСтатусВыполненногоПодсценарияФичи(НомерСтроки) Экспорт
	ШагиДляVanessaEditor = Новый Массив;
	ШагиДляVanessaEditor.Добавить(НомерСтроки);
	Ванесса.VanessaEditor.setRuntimeProgress("complete", Ванесса.ЗаписатьОбъектJSON(ШагиДляVanessaEditor), "");
КонецФункции	 

// Открывает гиперссылку в редакторе по умолчанию
&НаКлиенте
Процедура ОткрытьГиперссылкуВРедактореПоУмолчанию(Знач Стр) Экспорт
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьГиперссылкуВРедактореПоУмолчаниюОповещение", ЭтаФорма);
	НачатьЗапускПриложения(ОписаниеОповещения, Стр);
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьГиперссылкуВРедактореПоУмолчаниюОповещение(КодВозврата, ДополнительныеПараметры) Экспорт
	Если КодВозврата <> 0 Тогда 
		Ванесса.Отладка(КодВозврата);
	КонецЕсли;	 
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПервуюСтроку(Знач Стр)
	Если Найти(Стр, Символы.ПС) = 0 Тогда
		Возврат Стр; 
	КонецЕсли;	 
	
	МассивСтрок = Ванесса.РазложитьСтрокуВМассивПодстрокКлиент(Стр, Символы.ПС);
	Возврат МассивСтрок[0]; 
КонецФункции	 

&НаКлиенте
Процедура УстановитьСтатусШага(СтатусШага, ТекШаг, ЭтоТекущийШаг);
	ИдВСтрокеВиджета = Неопределено;
	ИдВиджета = Неопределено;
	ОпределитьДанныеВиджета(ИдВСтрокеВиджета, ИдВиджета, ТекШаг.ИдСтроки);

	Если ИдВиджета = Неопределено Тогда
		Если НЕ ТекШаг.ЭтоШагПодсценария Тогда
			Если ЭтоТекущийШаг Тогда
				Ванесса.VanessaEditor.setCurrentProgress(ТекШаг.НомерСтрокиВФиче);
			Иначе	
				ШагиДляVanessaEditor = Новый Массив;
				ШагиДляVanessaEditor.Добавить(ТекШаг.НомерСтрокиВФиче);
				Ванесса.VanessaEditor.setRuntimeProgress(СтатусШага, Ванесса.ЗаписатьОбъектJSON(ШагиДляVanessaEditor));
			КонецЕсли;	 
		КонецЕсли;	 
	Иначе	
		Если ЭтоТекущийШаг Тогда
			Ванесса.VanessaEditor.setCurrentProgress(ИдВСтрокеВиджета, ИдВиджета);
		Иначе	
			ШагиДляVanessaEditor = Новый Массив;
			ШагиДляVanessaEditor.Добавить(ИдВСтрокеВиджета);
			Ванесса.VanessaEditor.setRuntimeProgress(СтатусШага, Ванесса.ЗаписатьОбъектJSON(ШагиДляVanessaEditor), ИдВиджета);
		КонецЕсли;	 
	КонецЕсли;	 
	
КонецПроцедуры 

&НаКлиенте
Процедура ОпределитьДанныеВиджета(ИдВСтрокеВиджета, ИдВиджета, ИдСтроки)
	ИдВиджета = Ванесса.ИдВиджетаПоИдСтроки(ИдСтроки);
	Если ИдВиджета <> Неопределено Тогда
		ДанныеВиджета = Ванесса.ДанныеВиджетаПоИд(ИдВиджета);
		Если ДанныеВиджета <> Неопределено Тогда
			ИдВСтрокеВиджета = ДанныеВиджета[ИдСтроки];
		КонецЕсли;	 
	КонецЕсли;	 
КонецПроцедуры 

&НаКлиенте
Процедура РаскраситьСтрокиРедактора()
	СтрокиПодсценариевВиджетов = Новый Массив;
	СтрокиНеявныхОбластей = Новый Массив;
	РаскраситьСтрокиРедактораРекурсивно(Ванесса.Объект.ДеревоТестов, СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей);
	Ванесса.УстановитьДанныеДляРаскраскиСтрок(СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей);
КонецПроцедуры 

&НаКлиенте
Процедура ОбновитьВставкиКодаVanessaEditor(СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей)
	Ванесса.VanessaEditor.clearRuntimeCodes();
	Ванесса.ОчиститьДанныеВиджетов();
	
	Если НЕ Ванесса.Объект.ПоказыватьСтрокиПодсценариевVanessaEdittor Тогда
		РаскраситьСтрокиРедактора();
		Возврат;
	КонецЕсли;	 
	
	ОбновитьВставкиКодаVanessaEditorРекурсивно(Ванесса.Объект.ДеревоТестов, 0, СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей);
КонецПроцедуры 

&НаКлиенте
Процедура ТекстВставкиПоСтрокеДереваРекурсивно(СтрокаДерева, СтрокаОтступ, ТекстВставки, ТекУровень, ДанныеВиджета, СтрокиПодсценариев)
	Для Каждого ТекСтрока Из СтрокаДерева.ПолучитьЭлементы() Цикл
		ТекстВставки.Добавить(СтрокаОтступ + ТекстОтступа(ТекУровень) + ТекСтрока.Имя);
		ДанныеВиджета.Вставить(ТекСтрока.ИДСтроки, ТекстВставки.Количество());
		
		Если ТекСтрока.Тип = "ШагСценарий" Тогда
			СтрокиПодсценариев.Добавить(ТекстВставки.Количество());
		КонецЕсли;	 
		
		ТекстВставкиПоСтрокеДереваРекурсивно(ТекСтрока, СтрокаОтступ, ТекстВставки, ТекУровень + 1, ДанныеВиджета, СтрокиПодсценариев);
	КонецЦикла;	 
	
КонецПроцедуры 

&НаКлиенте
Функция ТекстОтступа(ТекУровень)
	Значение = КешОтступов[ТекУровень];
	Если Значение <> Неопределено Тогда
		Возврат Значение; 
	КонецЕсли;	 
	
	Стр = "	";
	Для Сч = 1 По ТекУровень Цикл
		Стр = Стр + "		";
	КонецЦикла;
	
	КешОтступов.Вставить(ТекУровень, Стр);
	
	Возврат Стр;
КонецФункции	 

&НаКлиенте
Функция ТекстВставкиПоСтрокеДерева(СтрокаДерева, СтрокаОтступ, ДанныеВиджета, СтрокиПодсценариев)
	ТекстВставки = Новый Массив;
	ТекстВставкиПоСтрокеДереваРекурсивно(СтрокаДерева, СтрокаОтступ, ТекстВставки, 0, ДанныеВиджета, СтрокиПодсценариев);
	
	Возврат Ванесса.МассивВСтроку(ТекстВставки, Символы.ПС); 
КонецФункции	 

&НаКлиенте
Процедура СделатьВставкуВиджетаСПодчиненнымиСтрокамиVanessaEditor(СтрокаДерева, ТекУровень, СтрокиПодсценариевВиджетов)
	ТекстСтроки = Ванесса.VanessaEditor.getLineContent(СтрокаДерева.НомерСтрокиВФиче);
	ТекстСтрокиСокрЛ = СокрЛ(ТекстСтроки);
	СтрокаОтступ = Лев(ТекстСтроки, СтрДлина(ТекстСтроки) - СтрДлина(ТекстСтрокиСокрЛ));
	ДанныеВиджета = Новый Соответствие;
	СтрокиПодсценариев = Новый Массив;
	ИДВиджета = Ванесса.VanessaEditor.showRuntimeCode(СтрокаДерева.НомерСтрокиВФиче,
		ТекстВставкиПоСтрокеДерева(СтрокаДерева, СтрокаОтступ, ДанныеВиджета, СтрокиПодсценариев));
		
	Для Каждого НомеСтрокиВиджета Из СтрокиПодсценариев Цикл
		СтрокиПодсценариевВиджетов.Добавить(Новый Структура("НомерСтроки,ИдВиджета", НомеСтрокиВиджета, ИДВиджета));
	КонецЦикла;	 
		
	Ванесса.ДобавитьДанныеВиджета(ИДВиджета, ДанныеВиджета);	
КонецПроцедуры 

&НаКлиенте
Процедура РаскраситьСтрокиРедактораРекурсивно(СтрокаДерева, СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей)
	Для Каждого ТекСтрока Из СтрокаДерева.ПолучитьЭлементы() Цикл
		Если ТекСтрока.Тип = "ШагСценарий" Тогда
			Ванесса.VanessaEditor.setLineStyle(ТекСтрока.НомерСтрокиВФиче, "", Ложь, Ложь, Истина);
			СтрокиПодсценариевВиджетов.Добавить(Новый Структура("НомерСтроки,ИдВиджета", ТекСтрока.НомерСтрокиВФиче, ""));
			Продолжить;
		ИначеЕсли ТекСтрока.Тип = "Область" Тогда
			Если Лев(СокрЛ(ТекСтрока.Имя), 1) <> "*" И ПустаяСтрока(ТекСтрока.ДопТип) Тогда
				Ванесса.VanessaEditor.setLineStyle(ТекСтрока.НомерСтрокиВФиче, "", Истина, Ложь, Ложь);
				СтрокиНеявныхОбластей.Добавить(Новый Структура("НомерСтроки,ИдВиджета", ТекСтрока.НомерСтрокиВФиче, ""));
			КонецЕсли;	 
		КонецЕсли;	 
		
		РаскраситьСтрокиРедактораРекурсивно(ТекСтрока, СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей);
	КонецЦикла;	 
КонецПроцедуры 

&НаКлиенте
Процедура ОбновитьВставкиКодаVanessaEditorРекурсивно(СтрокаДерева, ТекУровень, СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей)
	Для Каждого ТекСтрока Из СтрокаДерева.ПолучитьЭлементы() Цикл
		Если ТекСтрока.Тип = "ШагСценарий" Тогда
			СтрокиПодсценариевВиджетов.Добавить(Новый Структура("НомерСтроки,ИдВиджета", ТекСтрока.НомерСтрокиВФиче, ""));
			СделатьВставкуВиджетаСПодчиненнымиСтрокамиVanessaEditor(ТекСтрока, ТекУровень, СтрокиПодсценариевВиджетов);
			Продолжить;
		ИначеЕсли ТекСтрока.Тип = "Пример" Тогда
			ТекРодитель = ТекСтрока.ПолучитьРодителя();
			ЭлементыРодителя = ТекРодитель.ПолучитьЭлементы();
			НомерСтрокиВСтрокахРодителя = ЭлементыРодителя.Индекс(ТекСтрока);
			Если НомерСтрокиВСтрокахРодителя > 0 Тогда
				СделатьВставкуВиджетаСПодчиненнымиСтрокамиVanessaEditor(ТекСтрока, ТекУровень, СтрокиПодсценариевВиджетов);
			КонецЕсли;	 
			Продолжить;
		ИначеЕсли ТекСтрока.Тип = "Область" Тогда
			Если Лев(СокрЛ(ТекСтрока.Имя), 1) <> "*" И ПустаяСтрока(ТекСтрока.ДопТип) Тогда
				СтрокиНеявныхОбластей.Добавить(Новый Структура("НомерСтроки,ИдВиджета", ТекСтрока.НомерСтрокиВФиче, ""));
			КонецЕсли;	 
		КонецЕсли;	 
		
		ОбновитьВставкиКодаVanessaEditorРекурсивно(ТекСтрока, ТекУровень + 1, СтрокиПодсценариевВиджетов, СтрокиНеявныхОбластей);
	КонецЦикла;	 
КонецПроцедуры 

&НаКлиенте
Процедура СкопироватьТекстОшибкиВБуферОбменаVanessaEditorПродолжение(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	Ванесса.СообщитьПользователю(Ванесса.Локализовать("Текст ошибки скопирован в буфер обмена."));
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКлючевыеСловаGherkin()
	МодульПарсерФич = Ванесса.МодульПарсерФич();
	
	КлючевыеСловаGherkin = МодульПарсерФич.КлючевыеСловаGherkin();
	
	Возврат Ванесса.ЗаписатьОбъектJSON(КлючевыеСловаGherkin); 
	
КонецФункции	 

&НаКлиенте
Функция ПолучитьЗначенияПеременныхVanessaEditor()
	СлужебныеПеременныеКонтекста = Ванесса.СлужебныеПеременныеКонтекста();
	
	ДанныеПеременных = Новый Соответствие;
	Для Каждого Элем Из Ванесса.ОбъектКонтекст Цикл
		Если СлужебныеПеременныеКонтекста[Элем.Ключ] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		
		Если Найти(Элем.Ключ, "_Цикл_") > 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		ДанныеПеременных.Вставить(Элем.Ключ, Строка(Элем.Значение));
	КонецЦикла;	 
	Для Каждого Элем Из Ванесса.ОбъектКонтекстСохраняемый Цикл
		Если СлужебныеПеременныеКонтекста[Элем.Ключ] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		
		Если Найти(Элем.Ключ, "_Цикл_") > 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		ДанныеПеременных.Вставить(Элем.Ключ, Строка(Элем.Значение));
	КонецЦикла;	 
	Возврат Ванесса.ЗаписатьОбъектJSON(ДанныеПеременных);
КонецФункции	 

&НаКлиенте
Функция ПолучитьСписокШаговVanessaEditor(Язык)
	#Если НЕ ВебКлиент Тогда
		
	СтруктураПараметров = СтруктураПараметровДляПолученияДанныхШагов(Язык);
	
	Возврат ДанныеШаговДляРедактора(СтруктураПараметров); 
	
	#КонецЕсли		
КонецФункции	 

&НаКлиенте
Функция ПолучитьСписокКомандVanessaEditor()
	КомандыVanessaEditor = Новый Массив;
	
	Модификаторы = Новый Массив;
	Модификаторы.Добавить("CtrlCmd");
	КомандыVanessaEditor.Добавить(Новый Структура("eventId,keyCode,keyMod", "Ctrl+F5", "F5", Модификаторы));
	
	Модификаторы = Новый Массив;
	Модификаторы.Добавить("Alt");
	КомандыVanessaEditor.Добавить(Новый Структура("eventId,keyCode,keyMod", "Alt+F5", "F5", Модификаторы));
	
	КомандыVanessaEditor.Добавить(Новый Структура("eventId, errorLink", "ErrorDetails", Ванесса.Локализовать("Детали")));
	КомандыVanessaEditor.Добавить(Новый Структура("eventId, errorLink", "CopyError", Ванесса.Локализовать("Скопировать текст ошибки")));
	
	Возврат Ванесса.ЗаписатьОбъектJSON(КомандыVanessaEditor);
КонецФункции	 

&НаКлиенте
Процедура ЧтениеДанныхТекущейФормыVanessaEditor()
	Данные = Ванесса.МодульИсследовательФормы().ПолучитьТекущийЭлементФормы();
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;	 
	
	ЗапомнитьДанныеТекущейФормыVanessaEditor(Данные, Ванесса.ЗначенияЭлементовТекущегоОкнаVanessaEditor);
КонецПроцедуры 

&НаКлиенте
Функция ПолучитьЗначенияЭлементовТекущегоОкнаVanessaEditor()
	Возврат Ванесса.ЗаписатьОбъектJSON(Ванесса.ЗначенияЭлементовТекущегоОкнаVanessaEditor);
КонецФункции	 

&НаКлиенте
Функция ЗначениеПоляТекущегоПоляVanessaEditor(ЭлементФормы)
	ЗначениеПоля = "";
	Если ЭлементФормы.Вид = ВидПоляФормы.ПолеВвода Тогда
		Попытка
			ЗначениеПоля = ЭлементФормы.ПолучитьТекстРедактирования();
		Исключение
			// если поле невидимое - тогда получим представление данных
			ЗначениеПоля = ЭлементФормы.ПолучитьПредставлениеДанных();
		КонецПопытки;
	Иначе	
		ЗначениеПоля = ЭлементФормы.ПолучитьПредставлениеДанных();
	КонецЕсли;	 
	
	Возврат ЗначениеПоля; 
КонецФункции	 

&НаКлиенте
Процедура УстановитьЗначениеТекущейФормыVanessaEditor(ЗначенияЭлементовТекущегоОкнаVanessaEditor, Ключ, Значение, БылиИзменения);
	ТекЗначение = ЗначенияЭлементовТекущегоОкнаVanessaEditor[Ключ];
	Если ТекЗначение <> Значение Тогда
		БылиИзменения = Истина;
		ЗначенияЭлементовТекущегоОкнаVanessaEditor.Вставить(Ключ, Значение);
	КонецЕсли;	 
КонецПроцедуры 

&НаКлиенте
Функция ЭтоЗначимаяСтрокаVanessaEditor(Знач Стр)
	Если ПустаяСтрока(Стр) Тогда
		Возврат Ложь;
	КонецЕсли;	 
	
	Стр = СокрЛ(Стр);
	Если Лев(Стр, 1) = "#" ИЛИ Лев(Стр, 2) = "//" Тогда
		Возврат Ложь;
	КонецЕсли;	 
	
	Возврат Истина;
КонецФункции	 

&НаКлиенте
Функция ТипШага(ДанныеШага)
	Результат = 0;
	
	Если ДанныеШага.ТипШагаВДереве = "Условие" Тогда
		Результат = 5;
	ИначеЕсли ДанныеШага.ТипШагаВДереве = "Цикл" Тогда
		Результат = 8;
	ИначеЕсли Найти(ДанныеШага.ПредставлениеТеста, "в течение") Тогда
		Результат = 7;
	ИначеЕсли Найти(ДанныеШага.ПредставлениеТеста, "файл") > 0 Тогда
		Результат = 23;
	ИначеЕсли Найти(ДанныеШага.ТипШага, "Прочее.") > 0 Тогда
		Результат = 4;
	КонецЕсли;	 
	
	Возврат Результат;
КонецФункции	 

&НаСервереБезКонтекста
Функция ПолучитьДанныеДляПеревода(КешДанныеПеревода)
	ДанныеПеревода = Новый Структура;
	ДанныеПереводаШагов = Новый Соответствие;
	ДанныеКатегорийШагов = Новый Соответствие;
	Если ЗначениеЗаполнено(КешДанныеПеревода) Тогда
		ТаблицаПеревода = ПолучитьИзВременногоХранилища(КешДанныеПеревода.ТаблицаПеревода);
		Для Каждого СтрокаТаблицаПеревода Из ТаблицаПеревода Цикл
			//Если ПустаяСтрока(СтрокаТаблицаПеревода.КлючевоеСлово) Тогда
			//	Продолжить;
			//КонецЕсли;	 
			
			Если СтрокаТаблицаПеревода.ОригиналРусскийОписание = "Категория шагов" Тогда
				ДанныеКатегорийШагов.Вставить(СтрокаТаблицаПеревода.ОригиналРусскийШаг, СтрокаТаблицаПеревода.ТекстПереводаШаг);
				Продолжить;
			КонецЕсли;	 
			
			ДанныеШага = Новый Структура;
			ДанныеШага.Вставить("ТекстПереводаШаг", СтрокаТаблицаПеревода.ТекстПереводаШаг);
			ДанныеШага.Вставить("ТекстПереводаОписание", СтрокаТаблицаПеревода.ТекстПереводаОписание);
			ДанныеШага.Вставить("СтрокаДляПоискаПеревод", СтрокаТаблицаПеревода.СтрокаДляПоискаПеревод);
			
			ДанныеПереводаШагов.Вставить(СтрокаТаблицаПеревода.СтрокаДляПоискаРусский, ДанныеШага);
		КонецЦикла;	 
	КонецЕсли;	 
	
	ДанныеПеревода.Вставить("ДанныеПереводаШагов", ДанныеПереводаШагов);
	ДанныеПеревода.Вставить("ДанныеКатегорийШагов", ДанныеКатегорийШагов);
	
	Возврат ДанныеПеревода;
КонецФункции	

&НаКлиенте
Функция УбратьЛишниеСимволыИзПредставленияШага(Знач Стр)
	Стр = СтрЗаменить(Стр,"%1 ","");
	Стр = СтрЗаменить(Стр,"%2 ","");
	Стр = СтрЗаменить(Стр,"%3 ","");
	Стр = СтрЗаменить(Стр,"%4 ","");
	Стр = СтрЗаменить(Стр,"%5 ","");
	Стр = СтрЗаменить(Стр,"%6 ","");
	Возврат Стр;
КонецФункции	 

&НаКлиенте
Функция ПеревестиТипШага(ДанныеКатегорийШагов, Знач ТипШага)
	МассивСтрок = Ванесса.РазложитьСтрокуВМассивПодстрокКлиент(ТипШага, ".");
	Для Сч = 0 По МассивСтрок.Количество()-1 Цикл
		ЗначениеПеревода = ДанныеКатегорийШагов[МассивСтрок[Сч]];
		Если ЗначениеПеревода = Неопределено Тогда
			МассивСтрок[Сч] = "";
			Продолжить;
		КонецЕсли;	 
		
		МассивСтрок[Сч] = ЗначениеПеревода;
	КонецЦикла;	
	
	Результат = Ванесса.МассивВСтроку(МассивСтрок, ".");
	Если Прав(Результат, 1) = "." Тогда
		Результат = Лев(Результат, СтрДлина(Результат) - 1)
	КонецЕсли;	 
	Возврат Результат;
	
КонецФункции	 

&НаКлиенте
Функция ПолучитьСтрокуСортировки(Знач Стр)
	Стр = СтрЗаменить(Стр,"с именем ","");
	Возврат Стр; 
КонецФункции	 

&НаКлиенте
Функция СтруктураПараметровДляПолученияДанныхШагов(Язык)
	МодульПарсерФич = Ванесса.МодульПарсерФич();
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТаблицаИзвестныхStepDefinition", Ванесса.ТаблицаИзвестныхStepDefinition);
	СтруктураПараметров.Вставить("ТаблицаУжеСуществующихСценариев", Ванесса.ТаблицаУжеСуществующихСценариев());
	СтруктураПараметров.Вставить("МодульПарсерФич", МодульПарсерФич);
	СтруктураПараметров.Вставить("Язык", Язык);
	СтруктураПараметров.Вставить("Ванесса", Ванесса);
	
	Если Язык <> "ru" Тогда
		Ванесса.ПеревестиТекст("", Язык);
		СтруктураПараметров.Вставить("КешДанныеПеревода", Ванесса.КешДанныеПеревода());
	КонецЕсли;	 
	
	Возврат СтруктураПараметров; 
КонецФункции	 

&НаКлиенте
Процедура ПоказатьОшибкуВРедактореVanessaEditor(НомерСтрокиВФиче, ИдВиджета, ДанныеПоОшибке, ТекстОшибки, ТекШаг, СтрокаШага)
	Если ТекШаг <> Неопределено Тогда
		Ванесса.СохранитьДанныеОбОшибкеVanessaEditor(ТекШаг, ДанныеПоОшибке, ТекстОшибки, СтрокаШага);
	КонецЕсли;	 
	Ванесса.VanessaEditor.showRuntimeError(НомерСтрокиВФиче, ИдВиджета, Ванесса.ЗаписатьОбъектJSON(ДанныеПоОшибке),
		ПолучитьПервуюСтроку(ТекстОшибки));
КонецПроцедуры 

#КонецОбласти