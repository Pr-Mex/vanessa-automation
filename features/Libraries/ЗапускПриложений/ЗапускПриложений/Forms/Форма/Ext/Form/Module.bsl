
///////////////////////////////////////////////////
//Служебные функции и процедуры
///////////////////////////////////////////////////

&НаКлиенте
// контекст фреймворка Vanessa-Behavior
Перем Ванесса;
 
&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов. Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт;
 
&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев. Существует, пока открыта форма Vanessa-Behavior.
Перем КонтекстСохраняемый Экспорт;

&НаКлиенте
// Функция экспортирует список шагов, которые реализованы в данной внешней обработке.
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;
	
	ВсеТесты = Новый Массив;

	//описание параметров
	//Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,ОписаниеШага,ТипШага,Транзакция,Параметр);

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯВыполняюСкриптSikuliX(Парам01)","ЯВыполняюСкриптSikuliX","И я выполняю скрипт SikuliX ""ИмяСкрипта""","Выполняет скрипт SikuliX. Скрипт сначала ищется в кталоге проекта, за тем в каталогах библиотек, затем в стандартных скриптах Vanessa-Automation.","Прочее.SikuliX");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯОткрываюКартинкуВОсновномРедакторе(Парам01)","ЯОткрываюКартинкуВОсновномРедакторе","Дано я открываю картинку в основном редакторе ""ИмяКартинки""","Вызывает команду системы. Будет открыто приложение, которое ассоциировано для данного типа файлов.","Прочее.Открыть картинку");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯЖдуКогдаЗавершитсяПроцессСИДПроцессаВТечениеСекунд(Парам01, Парам02)","ЯЖдуКогдаЗавершитсяПроцессСИДПроцессаВТечениеСекунд","И я жду когда завершится процесс с ИД процесса ""PID"" в течение 10 секунд","Ожидает завершение процесса по его PID.","Прочее.Ожидание завершения процесса");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯЖдуКогдаЗакроетсяTestClientВТечениеСекунд(Парам01,Парам02)","ЯЖдуКогдаЗакроетсяTestClientВТечениеСекунд","И я жду когда закроется TestClient ""ПодготовкаОкружения"" в течение 10 секунд","Ожидает завершения сеанса тест клиента по имени его профиля.","Прочее.Ожидание завершения процесса");
	
	Возврат ВсеТесты;
КонецФункции
	
&НаСервере
// Служебная функция.
Функция ПолучитьМакетСервер(ИмяМакета)
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектСервер.ПолучитьМакет(ИмяМакета);
КонецФункции
	
&НаКлиенте
// Служебная функция для подключения библиотеки создания fixtures.
Функция ПолучитьМакетОбработки(ИмяМакета) Экспорт
	Возврат ПолучитьМакетСервер(ИмяМакета);
КонецФункции



///////////////////////////////////////////////////
//Работа со сценариями
///////////////////////////////////////////////////

&НаКлиенте
// Процедура выполняется перед началом каждого сценария
Процедура ПередНачаломСценария() Экспорт
	
КонецПроцедуры

&НаКлиенте
// Процедура выполняется перед окончанием каждого сценария
Процедура ПередОкончаниемСценария() Экспорт
	
КонецПроцедуры



///////////////////////////////////////////////////
//Реализация шагов
///////////////////////////////////////////////////

&НаКлиенте
Функция ИскатьСкриптSikuliXВКаталоге(КаталогДляПоиска,ИмяФайла)
	СписокКаталогов = Новый СписокЗначений;
	СписокФайлов    = Новый СписокЗначений;
	Ванесса.НайтиФайлыКомандаСистемы(КаталогДляПоиска,СписокКаталогов,СписокФайлов,Истина);
	Для Каждого Элем Из СписокКаталогов Цикл
		Если НРег(Элем.Значение.Имя) = НРег(ИмяФайла) Тогда
			Возврат Элем.Значение.ПолноеИмя;
		КонецЕсли;	 
	КонецЦикла;	 
КонецФункции	 

&НаКлиенте
Функция ПолныйПутьКСкрипту(Знач ИмяСкрипта)
	Если Прав(ИмяСкрипта,7) <> ".sikuli" Тогда
		ИмяСкрипта = ИмяСкрипта + ".sikuli";
	КонецЕсли;	 
	
	//проверка, что передали полный путь
	Если Ванесса.ФайлСуществуетКомандаСистемы(ИмяСкрипта) Тогда
		Возврат ИмяСкрипта;
	КонецЕсли;	 
	
	//Поиск в каталоге проекта
	ПолныйПуть = ИскатьСкриптSikuliXВКаталоге(Ванесса.Объект.КаталогПроекта,ИмяСкрипта);
	Если ПолныйПуть <> Неопределено Тогда
		Возврат ПолныйПуть; 
	КонецЕсли;	 
	
	//Поиск в каталогах бибилотек
	Для Каждого Элем Из Ванесса.Объект.КаталогиБиблиотек Цикл
		ПутьДляПоиска = Элем.Значение;
		ПолныйПуть = ИскатьСкриптSikuliXВКаталоге(ПутьДляПоиска,ИмяСкрипта);
		Если ПолныйПуть <> Неопределено Тогда
			Возврат ПолныйПуть; 
		КонецЕсли;	 
	КонецЦикла;	 
	
	ПутьДляПоиска = Ванесса.ДополнитьСлешВПуть(Ванесса.Объект.КаталогИнструментов) + "tools\Sikuli";
	ПолныйПуть = ИскатьСкриптSikuliXВКаталоге(ПутьДляПоиска,ИмяСкрипта);
	Если ПолныйПуть <> Неопределено Тогда
		Возврат ПолныйПуть; 
	КонецЕсли;	 
	
	Возврат Неопределено;
КонецФункции	 

&НаКлиенте
//И я выполняю скрипт SikuliX "ИмяСкрипта"
//@ЯВыполняюСкриптSikuliX(Парам01)
Процедура ЯВыполняюСкриптSikuliX(ИмяСкрипта) Экспорт
	ПолныйПутьКСкрипту = ПолныйПутьКСкрипту(ИмяСкрипта);
	
	Если ПолныйПутьКСкрипту = Неопределено Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не найден SikuliX скрипт <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",ИмяСкрипта);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	
	ОписаниеОшибки = "";
	Результат = Ванесса.ВыполнитьSikuliСкрипт(ПолныйПутьКСкрипту,-1,Истина,ОписаниеОшибки);
	Если Результат <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не удалось выполнить SukuliX скрипт <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",ИмяСкрипта);
		ТекстСообщения = ТекстСообщения + Символы.ПС + ОписаниеОшибки;
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 

	
КонецПроцедуры

&НаКлиенте
//Дано я открываю картинку в основном редакторе "ОтчетAllure2"
//@ЯОткрываюКартинкуВОсновномРедакторе(Парам01)
Процедура ЯОткрываюКартинкуВОсновномРедакторе(ИмяКартинки) Экспорт
	Файл = Ванесса.ПолучитьФайлКартинки(ИмяКартинки);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя,"\","/");
	Ванесса.ВыполнитьКомандуОСБезПоказаЧерногоОкна(ПутьКФайлу,0);
КонецПроцедуры

&НаКлиенте
//И я жду когда завершится процесс с ИД процесса "PID" в течение
//@ЯЖдуКогдаЗавершитсяПроцессСИДПроцесса(Парам01, Парам02)
Процедура ЯЖдуКогдаЗавершитсяПроцессСИДПроцессаВТечениеСекунд(Парам01, Парам02) Экспорт
	//Ванесса.ПосмотретьЗначение(Парам01,Истина);
	
	Если Ванесса.ЭтоLinux Тогда
		ВызватьИсключение "Не реализовано.";
	КонецЕсли;
	
	PID = XMLСтрока(Ванесса.ПолучитьЗначениеИзКонтекстаЕслиЭтоВозможно(Парам01));
	Секунд = Парам02;
	
	Ванесса.ЗапретитьВыполнениеШагов();
	
	ДатаНачалаВыполненияОбработчикаОжидания = ТекущаяДата();
	ПодключитьОбработчикОжидания("ЯЖдуКогдаЗавершитсяПроцессСИДПроцессаТик", 1, Ложь);
	
КонецПроцедуры

&НаКлиенте
//И я жду когда закроется TestClient "ПодготовкаОкружения" в течение 10 секунд
//@ЯЖдуКогдаЗакроетсяTestClientВТечениеСекунд(Парам01,Парам02)
Процедура ЯЖдуКогдаЗакроетсяTestClientВТечениеСекунд(ИмяTestClient,Парам02) Экспорт
	//Ванесса.ПосмотретьЗначение(Парам01,Истина);
	
	СвойстваTestClient = ПолучитьДанныеКлиентаТестирования(ИмяTestClient);
	
	Если Не СвойстваTestClient.Подключен Тогда
		Возврат;
	КонецЕсли;
	
	Если Ванесса.ЭтоLinux Тогда
		ВызватьИсключение "Не реализовано.";
	КонецЕсли;
	
	PID = ВычислитьTestClientPID(СвойстваTestClient.ПортЗапускаТестКлиента);
	
	Если PID = Неопределено Или ПустаяСтрока(PID) Тогда 
		ОбновитьДанныеКлиентовТестирования();
		Возврат;
	КонецЕсли;
	
	Секунд = Парам02;
	ОбновитьДанныеКлиентовТестирования = Истина;
	
	Ванесса.ЗапретитьВыполнениеШагов();
	
	ДатаНачалаВыполненияОбработчикаОжидания = ТекущаяДата();
	ПодключитьОбработчикОжидания("ЯЖдуКогдаЗавершитсяПроцессСИДПроцессаТик", 1, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯЖдуКогдаЗавершитсяПроцессСИДПроцессаТик()
	
	ИмяОбработчика = "ЯЖдуКогдаЗавершитсяПроцессСИДПроцессаТик";
	
	Команда = СтрШаблон("TaskList /FI ""PID EQ %1""", PID);
	ПотокВывода = ВыполнитьКомандуСПотокомВывода(Команда);
	
	Если СтрНайти(ПотокВывода, "INFO: No tasks are running which match the specified criteria.") Тогда
		Если ОбновитьДанныеКлиентовТестирования Тогда 
			ОбновитьДанныеКлиентовТестирования();
		КонецЕсли;
		
		ОтключитьОбработчикОжидания(ИмяОбработчика);
		Ванесса.ПродолжитьВыполнениеШагов();
		Возврат;
	КонецЕсли;
	
	Если ТекущаяДата() > (ДатаНачалаВыполненияОбработчикаОжидания + Секунд) Тогда
		ОтключитьОбработчикОжидания(ИмяОбработчика);
		Ванесса.ПродолжитьВыполнениеШагов(
			Истина,
			СтрШаблон("Ожидали, что процесс с ИД <%1> будет завершен в течение <%2> секунд.",
				PID,
				Секунд
			)
		);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВычислитьTestClientPID(TPort)
	
	Команда = "wmic path win32_process get ProcessID, CommandLine /format:csv";
	ПотокВывода = ВыполнитьКомандуСПотокомВывода(Команда, КодировкаТекста.UTF16);
	
	Для Счетчик = 1 По СтрЧислоСтрок(ПотокВывода) Цикл
		ТекСтрока = СтрПолучитьСтроку(ПотокВывода, Счетчик);
		Если СтрНайти(ТекСтрока, СтрШаблон("-TPort %1", XMLСтрока(TPort))) Тогда 
			Позиция = СтрНайти(ТекСтрока, ",", НаправлениеПоиска.СКонца);
			Возврат Сред(ТекСтрока, Позиция + 1);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Функция ВыполнитьКомандуСПотокомВывода(Команда, Кодировка = Неопределено)
	
	Если Кодировка = Неопределено Тогда 
		Кодировка = КодировкаТекста.OEM;
	КонецЕсли;
	
	ИмяФайлаПотокаВывода = ПолучитьИмяВременногоФайла("stdout.tmp");
	Команда = СтрШаблон(Команда + " > ""%1""", ИмяФайлаПотокаВывода);
	Ванесса.ВыполнитьКомандуОСБезПоказаЧерногоОкна(Команда);
	
	ЧтениеПотокаОшибок = Новый ЧтениеТекста(ИмяФайлаПотокаВывода, Кодировка);
	ПотокВывода = ЧтениеПотокаОшибок.Прочитать();
	ЧтениеПотокаОшибок.Закрыть();
	Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаПотокаВывода);
	
	Возврат ПотокВывода;
	
КонецФункции

&НаКлиенте
Функция ПолучитьДанныеКлиентаТестирования(ИмяTestClient)
	
	Для Каждого СтрокаДанныеКлиентовТестирования Из Ванесса.ДанныеКлиентовТестирования Цикл
		
		Если СтрокаДанныеКлиентовТестирования.Имя = ИмяTestClient Тогда
			Возврат СтрокаДанныеКлиентовТестирования;
		КонецЕсли;
		
		Если СтрокаДанныеКлиентовТестирования.Синоним = ИмяTestClient Тогда
			Возврат СтрокаДанныеКлиентовТестирования;
		КонецЕсли;
	КонецЦикла;
	
	ВызватьИсключение СтрШаблон("Не существует клиента тестирования. %1", ИмяTestClient);
КонецФункции

&НаКлиенте
Процедура ОбновитьДанныеКлиентовТестирования()
	
	КонтекстСохраняемый.Вставить("ТестовоеПриложение",Неопределено);
	
	Если КонтекстСохраняемый.Свойство("ПодключенныеTestClient") Тогда
		ПодключенныеTestClient = КонтекстСохраняемый.ПодключенныеTestClient;
		УдалитьЭлементПодключенныеTestClient(ПодключенныеTestClient,КонтекстСохраняемый.ТекущийПрофильTestClient);
	КонецЕсли;

	// снимем флаг подключения в таблице TestClient
	Для каждого СтрокаПодключения Из Ванесса.ДанныеКлиентовТестирования Цикл
		СтрокаПодключения.Подключен = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЭлементПодключенныеTestClient(ПодключенныеTestClient,ИмяTestClient,Синоним = Неопределено)
	Если ПодключенныеTestClient = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Ккк = 0 По ПодключенныеTestClient.Количество()-1 Цикл
		Если ПодключенныеTestClient[Ккк].Имя = ИмяTestClient Тогда
			Синоним = ПодключенныеTestClient[Ккк].Синоним;
			ПодключенныеTestClient.Удалить(Ккк);
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры