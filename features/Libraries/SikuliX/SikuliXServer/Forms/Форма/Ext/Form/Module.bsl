
///////////////////////////////////////////////////
// Служебные функции и процедуры
///////////////////////////////////////////////////

&НаКлиенте
// контекст фреймворка Vanessa-Behavior
Перем Ванесса;
 
&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов. Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт;
 
&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев. Существует, пока открыта форма Vanessa-Behavior.
Перем КонтекстСохраняемый Экспорт;

// Делает отключение модуля
&НаКлиенте
Функция ОтключениеМодуля() Экспорт

	Ванесса = Неопределено;
	Контекст = Неопределено;
	КонтекстСохраняемый = Неопределено;

КонецФункции

&НаКлиенте
// Функция экспортирует список шагов, которые реализованы в данной внешней обработке.
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;
	
	ВсеТесты = Новый Массив;

	// описание параметров
	// Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,ОписаниеШага,ТипШага,Транзакция,Параметр);

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "КликНаКартинку(Парам01)", "КликНаКартинку","Затем клик на картинку ""ИмяКартинки""","Эмулирует клик левой кнопкой мышки по картинке. Картинка, поиск которой будет происходить на экране, должна находится в каталоге проекта.", "Прочее.VanessaExt.Клик на картинку");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "КликНаТекст(Парам01)", "КликНаТекст","Затем клик на текст ""Текст""","Эмулирует клик левой кнопкой мышки по картинке. Картинка генерируется по переданному тексту.", "Прочее.SikuliX (устаревшее)");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "КликПравойКнопкойНаКартинку(Парам01)", "КликПравойКнопкойНаКартинку","Дано клик правой кнопкой на картинку ""ИмяКартинки""","Эмулирует клик правой кнопкой мышки по картинке. Картинка, поиск которой будет происходить на экране, должна находится в каталоге проекта.", "Прочее.VanessaExt.Клик на картинку");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "КликПравойКнопкойНаТекст(Парам01)", "КликПравойКнопкойНаТекст","Дано клик правой кнопкой на текст ""Текст""","Эмулирует клик правой кнопкой мышки по картинке. Картинка генерируется по переданному тексту.", "Прочее.SikuliX (устаревшее)");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "КурсорККартинке(Парам01)", "КурсорККартинке","Дано курсор к картинке ""ИмяКартинки""","Эмулирует перемещение курсора мышки к картинке. Картинка, поиск которой будет происходить на экране, должна находится в каталоге проекта.", "Прочее.VanessaExt.Курсор к картинке");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "КурсорКТексту(Парам01)", "КурсорКТексту","Дано курсор к тексту ""Текст""","Эмулирует перемещение курсора мышки к картинке. Картинка генерируется по переданному тексту.", "Прочее.SikuliX (устаревшее)");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ДвойнойКликНаКартинку(Парам01)", "ДвойнойКликНаКартинку","Дано двойной клик на картинку ""ИмяКартинки""","Делает двойной клик по картинке. Картинка, поиск которой будет происходить на экране, должна находится в каталоге проекта.", "Прочее.VanessaExt.Клик на картинку");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ДвойнойКликНаТекст(Парам01)", "ДвойнойКликНаТекст","Дано двойной клик на текст ""Текст""","Делает двойной клик по картинке. Картинка генерируется по переданному тексту.", "Прочее.SikuliX (устаревшее)");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯРисуюСтрелкуОтДо(Парам01,Парам02,ТабПарам)", "ЯРисуюСтрелкуОтДо","Затем я рисую стрелку от ""Картинка1"" до ""Картинка2"""
	+ Символы.ПС + "		|'Цвет'          | 'Зеленый'        |"
	+ Символы.ПС + "		|'Длительность'  | '2000'           |"
	+ Символы.ПС + "		|'ТочкаКуда'     | 'СерединаНиз'    |"
	+ Символы.ПС + "		|'Толщина'  	 | '10'           	|"
	,"Рисует стрелку от одной картинки до другой. Поиск картинок на экране делается с помощью SikuliX. Если первый параметр равен пустой сроке, тогда точкой ""от"" будет центр экрана. Дополнительные параметры задаются через таблицу Gherkin. "
	+ Символы.ПС + "- Цвет (Color) - строка, из перечисления WebЦвета, например ""Зеленый""."
	+ Символы.ПС + "- Длительность (Duration) - сколько милисекунд стрелка будет находится на экране, например - 2000."
	+ Символы.ПС + "- ТочкаКуда (PointTo), ТочкаОткуда (PointFrom) - место, в которое будет указывать/исходить стрелка. Возможные значения: СерединаНиз (MiddleDown), СерединаВерх (MiddleUp), СерединаПраво (MiddleRight), СерединаЛево (MiddleLeft), Центр (Center)."
	+ Символы.ПС + "- Толщина (Width) - толщина стрелки, например - 10.", "Прочее.VanessaExt.Стрелка к картинке");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯРисуюСтрелкуОтТекстаДоТекста(Парам01,Парам02,ТабПарам)", "ЯРисуюСтрелкуОтТекстаДоТекста","Затем я рисую стрелку от текста ""Текст1"" до текста ""Текст2"""
	+ Символы.ПС + "		|'Цвет'          | 'Зеленый'        |"
	+ Символы.ПС + "		|'Длительность'  | '2000'           |"
	+ Символы.ПС + "		|'ТочкаКуда'     | 'СерединаНиз'    |"
	+ Символы.ПС + "		|'Толщина'  	 | '10'           	|"
	,"Рисует стрелку от одной картинки до другой. Картинка генерируется по переданному тексту. Поиск картинок на экране делается с помощью SikuliX. Если первый параметр равен пустой сроке, тогда точкой ""от"" будет центр экрана. Дополнительные параметры задаются через таблицу Gherkin. "
	+ Символы.ПС + "- Цвет (Color) - строка, из перечисления WebЦвета, например ""Зеленый""."
	+ Символы.ПС + "- Длительность (Duration) - сколько милисекунд стрелка будет находится на экране, например - 2000."
	+ Символы.ПС + "- ТочкаКуда (PointTo), ТочкаОткуда (PointFrom) - место, в которое будет указывать/исходить стрелка. Возможные значения: СерединаНиз (MiddleDown), СерединаВерх (MiddleUp), СерединаПраво (MiddleRight), СерединаЛево (MiddleLeft), Центр (Center)."
	+ Символы.ПС + "- Толщина (Width) - толщина стрелки, например - 10.", "Прочее.SikuliX (устаревшее)");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯЗапускаюSikuliXСервер()", "ЯЗапускаюSikuliXСервер","И я запускаю SikuliX сервер","Запускает SikuliX сервер.", "Прочее.SikuliX (устаревшее)");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯНажимаюХоткей(Парам01,Парам02)", "ЯНажимаюХоткей","Дано я нажимаю хоткей ""ХотКей"" ""Модификатор""","Эмулирует нажатие хоткея с помощью SikuliX.", "Прочее.SikuliX (устаревшее)");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯНажимаюENTER()", "ЯНажимаюENTER","Дано я нажимаю ENTER","Эмулирует нажатие клавиши ENTER с помощью SikuliX.", "Прочее.SikuliX (устаревшее)");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯЖдуКартинкиВТечениеСекунд(Парам01,Парам02)", "ЯЖдуКартинкиВТечениеСекунд","И я жду картинки ""ИмяКартинки"" в течение 20 секунд","Ожидает появления картинки на экране в течение нужного числа секунд.", "Прочее.VanessaExt.Ожидание появления картинки");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯОпределяюКоординатыКартинкиКак(Парам01,Парам02)", "ЯОпределяюКоординатыКартинкиКак","И я определяю координаты картинки ""ИмяКартинки"" как ""ИмяПеременной""","Определяет коордианты картинки с помощью SikuliX и сохраняет структуру с координатами в переменную", "Прочее.VanessaExt.Определение координат картинки");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯРисуюРамкуВокругКартинки(Парам01, ТабПарам)", "ЯРисуюРамкуВокругКартинки","И я рисую рамку вокруг картинки ""ИмяКартинки"""
	+ Символы.ПС + "		|'Цвет'          | 'Зеленый'        |"
	+ Символы.ПС + "		|'Длительность'  | '2000'           |"
	+ Символы.ПС + "		|'Прозрачность'	 | '127'           	|"
	+ Символы.ПС + "		|'Толщина'  	 | '3'           	|"
	,"Рисует рамку вокруг картинки."
	+ Символы.ПС + "- Цвет (Color) - строка, из перечисления WebЦвета, например ""Зеленый""."
	+ Символы.ПС + "- Цвет (Color) - строка, из перечисления WebЦвета, например ""Зеленый""."
	+ Символы.ПС + "- Прозрачность (Transparency) - прозрачный эффект, изменяется от 0 до 255."
	+ Символы.ПС + "- Толщина (Width) - толщина рамки, например - 3.","Прочее.VanessaExt.Рамка вокруг картинки");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯВращаюКолесоМышкиРазСЗажатойКлавишейCtrl(Парам01,Парам02)", "ЯВращаюКолесоМышкиРазСЗажатойКлавишейCtrl","И я вращаю колесо мышки ""Направление"" 3 раз с зажатой клавишей ctrl","Эмулирует вращение колеса мышки с зажатой клавишей Ctrl. Нужно для эмуляции изменения масштаба. Параметр <Направление> может принимать значения ""Вверх"" или ""Вниз""", "Прочее.SikuliX (устаревшее)");

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЕстьКартинкаТогда(Парам01)", "ЕстьКартинкаТогда","Если есть картинка ""ИмяКартинки"" Тогда","Условие. Проверяет, есть ли на экране нужная картинка.", "Прочее.VanessaExt.Проверка существования картинки", "Условие");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯУстанавливаюОсновнойПрофильЦвета(ТабПарам)", "ЯУстанавливаюОсновнойПрофильЦвета","И я устанавливаю основной профиль цвета"
	+ Символы.ПС + "		| 'Шрифт'      | 'arial' |"
	+ Символы.ПС + "		| 'ЦветФона'   | 'white' |"
	+ Символы.ПС + "		| 'ЦветШрифта' | 'black' |"
	+ Символы.ПС + "		| 'Размер'     | '13'    |"
	
	,"Устанавливает параметры для генерации картинок по тексту. Можно передать таблицу параметров либо передать имя готового профиля.", "Прочее.SikuliX (устаревшее)");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯУстанавливаюПрофильЦвета(Парам01,ТабПарам)", "ЯУстанавливаюПрофильЦвета","И я устанавливаю профиль цвета ""ИмяПрофиля"""
	+ Символы.ПС + "		| 'Шрифт'      | 'arial' |"
	+ Символы.ПС + "		| 'ЦветФона'   | 'white' |"
	+ Символы.ПС + "		| 'ЦветШрифта' | 'black' |"
	+ Символы.ПС + "		| 'Размер'     | '13'    |"
	
	,"Устанавливает параметры для генерации картинок по тексту. Нужно передать имя профиля и значения параметров.", "Прочее.SikuliX (устаревшее)");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯПеретаскиваюКартинкуК(Парам01,Парам02)", "ЯПеретаскиваюКартинкуК","И я перетаскиваю картинку ""ИмяКартинки1"" к ""ИмяКартинки2""","Эмулирует Drag-and-drop от ""ИмяКартинки1"" к ""ИмяКартинки2"". Картинки, поиск которых будет происходить на экране, должны находится в каталоге проекта.", "Прочее.VanessaExt.Перетаскивание картинки");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯПоказываюПодсказкуEnjoyHintУКартинкиНаЭкране(Парам01,Парам02,ТабПарам)",
		"ЯПоказываюПодсказкуEnjoyHintУКартинкиНаЭкране",
		"И я показываю подсказку ""ТекстПодсказки"" EnjoyHint у картинки ""ИмяКартинки"" на экране
		|		|'Длительность'  | '2000'           |
		|		|'Прозрачность'	 | '127'           	|
		|		|'Толщина'  	 | '3'           	|",
		"Рисует фигуры на экране. Происходит затемнение экрана, кроме указанной картинки. Аналог эффекта EnjoyHint в браузере.",
		"Прочее.Автоинструкции.Универсальный EnjoyHint");
	
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, "ЯПоказываюНаЭкранеТекст(Парам01,ТабПарам)",
	"ЯПоказываюНаЭкранеТекст",
	"И я показываю на экране текст ""Произвольный текст""
	|		|'X'             | '800'          |
	|		|'Y'             | '600'          |
	|		|'Длительность'  | '2000'         |
	|		|'Шрифт'         | 'arial'        |
	|		|'Цвет'          | 'red'          |
	|		|'Размер'        | '13'           |",
	"Отображает произвольный текст на экране по указанным координатам.",
	"Прочее.Автоинструкции.Вывод текста");
	
	Возврат ВсеТесты;
КонецФункции
	
&НаСервере
// Служебная функция.
Функция ПолучитьМакетСервер(ИмяМакета)
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектСервер.ПолучитьМакет(ИмяМакета);
КонецФункции
	
&НаКлиенте
// Служебная функция для подключения библиотеки создания fixtures.
Функция ПолучитьМакетОбработки(ИмяМакета) Экспорт
	Возврат ПолучитьМакетСервер(ИмяМакета);
КонецФункции



///////////////////////////////////////////////////
// Работа со сценариями
///////////////////////////////////////////////////

&НаКлиенте
// Функция выполняется перед началом каждого сценария
Функция ПередНачаломСценария() Экспорт
	
КонецФункции

&НаКлиенте
// Функция выполняется перед окончанием каждого сценария
Функция ПередОкончаниемСценария() Экспорт
	
КонецФункции

///////////////////////////////////////////////////
// Реализация шагов
///////////////////////////////////////////////////

&НаКлиенте
Функция ПереданныеПараметры(ТабПарам)
	Структура = Новый Структура;
	Для Каждого СтрокаТабПарам Из ТабПарам Цикл
		Попытка
			Структура.Вставить(СтрокаТабПарам.Кол1, СтрокаТабПарам.Кол2);
		Исключение
			Ванесса.СообщитьПользователю(Ванесса.Локализовать(ОписаниеОшибки()));
		КонецПопытки;
	КонецЦикла;	 
	
	Возврат Структура;
КонецФункции	


&НаКлиенте
// Получает абсолютный цвет из параметров
//
Функция ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры)
	
	Цвет = WebЦвета.ЗеленыйЛес;
	Если ПереданныеПараметры.Свойство("Цвет") Тогда
		Попытка
			Цвет = WebЦвета[ПереданныеПараметры.Цвет];
		Исключение
			Ванесса.СообщитьПользователю(Ванесса.Локализовать(ОписаниеОшибки()));
		КонецПопытки;
	ИначеЕсли ПереданныеПараметры.Свойство("Color") Тогда
		Попытка
			Цвет = WebЦвета[ПереданныеПараметры.Color];
		Исключение
			Ванесса.СообщитьПользователю(Ванесса.Локализовать(ОписаниеОшибки()));
		КонецПопытки;
	КонецЕсли;	 
	АбсолютныйЦвет = Ванесса.АбсолютыйЦвет(Цвет);
	
	Возврат АбсолютныйЦвет;
	
КонецФункции

&НаКлиенте
Функция ЦветЧислом(Цвет)
	Возврат Ванесса.ЦветЧислом(Цвет); 
КонецФункции	 

&НаКлиенте
Функция Центр(Знач Точка1, Знач Точка2)
	Возврат Точка1 + Окр((Точка2 - Точка1) / 2);
КонецФункции	 

&НаКлиенте
Функция ПрочитатьФайлJson(ИмяФайла)
	ЧтениеJSON = Вычислить("Новый ЧтениеJSON");
	ЧтениеJSON.ОткрытьФайл(ИмяФайла);
	СтруктураПараметров = Вычислить("ПрочитатьJSON(ЧтениеJSON)");
	ЧтениеJSON.Закрыть();
	Возврат СтруктураПараметров; 
КонецФункции	 

&НаКлиенте
Функция ЗаписатьСтрокуJSON(ДанныеJSON)
	ЗаписьJSON = Вычислить("Новый ЗаписьJSON");
	ЗаписьJSON.УстановитьСтроку();
	Выполнить("ЗаписатьJSON(ЗаписьJSON, ДанныеJSON)");
	Возврат ЗаписьJSON.Закрыть(); 
КонецФункции	 

&НаКлиенте
Функция ПолучитьФайлКартинки(ИмяФайла)
	Возврат Ванесса.ПолучитьФайлКартинки(ИмяФайла); 
КонецФункции	 

&НаКлиенте
Функция КоординатыТочкиИзТекста(X, Y, Rect, Стр)
	Если Стр = "СерединаПраво" ИЛИ Стр = "MiddleRight" Тогда
		X = Rect.X2;
		Y = Rect.Y1 + Окр((Rect.Y2 - Rect.Y1) / 2);
	ИначеЕсли Стр = "СерединаЛево" ИЛИ Стр = "MiddleLeft" Тогда
		X = Rect.X1;
		Y = Rect.Y1 + Окр((Rect.Y2 - Rect.Y1) / 2);
	ИначеЕсли Стр = "СерединаВерх" ИЛИ Стр = "MiddleUp" Тогда
		X = Rect.X1 + Окр((Rect.X2 - Rect.X1) / 2);
		Y = Rect.Y1;
	ИначеЕсли Стр = "СерединаНиз" ИЛИ Стр = "MiddleDown" Тогда
		X = Rect.X1 + Окр((Rect.X2 - Rect.X1) / 2);
		Y = Rect.Y2;
	ИначеЕсли Стр = "Центр" ИЛИ Стр = "Center" Тогда
		X = Rect.X1 + Окр((Rect.X2 - Rect.X1) / 2);
		Y = Rect.Y1 + Окр((Rect.Y2 - Rect.Y1) / 2);
	КонецЕсли;	 
КонецФункции 

&НаКлиенте
// Затем клик на картинку "Тестовый Элемент 2"
//@КликНаКартинку(Парам01)
Функция КликНаКартинку(ИмяФайла) Экспорт
	
	Файл = ПолучитьФайлКартинки(ИмяФайла);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");

	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяФайла, "КликНаКартинку");
		Возврат Неопределено;
	КонецЕсли;

	СделатьКликНаКартинку(ПутьКФайлу, ИмяФайла);
	
КонецФункции

&НаКлиенте
Функция ПолучитьТекстСкрипта(ИмяСкрипта, ПараметрыСкрипта = Неопределено)
	
	ТекстСкрипта = Ванесса.ОбернутьВКавычки(Ванесса.Объект.КаталогИнструментов + "\tools\Sikuli\" + ИмяСкрипта + ".sikuli");
	
	Если ПараметрыСкрипта = Неопределено Тогда
		Возврат ТекстСкрипта;
	КонецЕсли;
	
	ТекстСкрипта = ТекстСкрипта + " --args ";
	
	Для Каждого ТекущийПараметр Из ПараметрыСкрипта Цикл
		ТекстСкрипта = ТекстСкрипта + Ванесса.ОбернутьВКавычки(ТекущийПараметр) + " ";
	КонецЦикла;
	
	Возврат ТекстСкрипта;
						
КонецФункции

&НаКлиенте
Функция СделатьКликНаКартинку(ПутьКФайлу, ИмяФайла)

	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ПутьКФайлу);
	ТекстСкрипта = ПолучитьТекстСкрипта("ClickOnPicture", ПараметрыСкрипта);
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось сделать клик мышкой по картинке <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяФайла);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 

КонецФункции

&НаКлиенте
Функция НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяФайла, Действие, ДопПараметры = Неопределено)

	Если НЕ Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить использование внешней компоненты VanessaExt.");
	КонецЕсли;	 

	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = Новый Структура;
	КонецЕсли;
	ДопПараметры.Вставить("ИмяФайла", ИмяФайла);
	ДопПараметры.Вставить("Действие", Действие);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваНайтиНаЭкране", ЭтаФорма, ДопПараметры);
	ФрагментКартинки = Новый ДвоичныеДанные(ПутьКФайлу);
	Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовНайтиНаЭкране(ОписаниеОповещения, ФрагментКартинки);
	Ванесса.ЗапретитьВыполнениеШагов();
	
КонецФункции

&НаКлиенте
Функция ОкончаниеВызоваНайтиНаЭкране(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	ДопустимыйПредел = Ванесса.ПорогПоискаКартинки();
	Координаты = Ванесса.ПрочитатьСтрокуJSON(Результат);

	Если ДополнительныеПараметры.Действие = "ЕстьКартинкаТогда" Тогда
		Ванесса.УстановитьРезультатУсловия(Координаты.Match >= ДопустимыйПредел);
		Ванесса.ПродолжитьВыполнениеШагов();
		Возврат Неопределено;
	КонецЕсли;

	Если Координаты.Match < ДопустимыйПредел Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось найти на экране картинку <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ДополнительныеПараметры.ИмяФайла);
		Ванесса.ПродолжитьВыполнениеШагов(Истина, ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;

	Если ДополнительныеПараметры.Действие = "КурсорККартинке"
		ИЛИ ДополнительныеПараметры.Действие = "КликНаКартинку"
		ИЛИ ДополнительныеПараметры.Действие = "ДвойнойКликНаКартинку"
		ИЛИ ДополнительныеПараметры.Действие = "КликПравойКнопкойНаКартинку"
		ИЛИ ДополнительныеПараметры.Действие = "ЯПеретаскиваюКартинкуК" Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваЭмуляцияДвиженияМыши", ЭтаФорма, ДополнительныеПараметры);
		
		КоличествоШаговПриДвиженииМышкиVanessaExt = Ванесса.Объект.КоличествоШаговПриДвиженииМышкиVanessaExt;
		ПаузаМеждуШагамиПриДвиженииМышкиVanessaExt = Ванесса.Объект.ПаузаМеждуШагамиПриДвиженииМышкиVanessaExt;
		
		Если НЕ ЗначениеЗаполнено(ПаузаМеждуШагамиПриДвиженииМышкиVanessaExt) Тогда
			ПаузаМеждуШагамиПриДвиженииМышкиVanessaExt = 2;
		КонецЕсли;	 
		Если НЕ ЗначениеЗаполнено(КоличествоШаговПриДвиженииМышкиVanessaExt) Тогда
			КоличествоШаговПриДвиженииМышкиVanessaExt = 800;
		КонецЕсли;	 
		
		ДополнительныеПараметры.Вставить("КоличествоШагов", КоличествоШаговПриДвиженииМышкиVanessaExt);
		ДополнительныеПараметры.Вставить("ПаузаМеждуШагами", ПаузаМеждуШагамиПриДвиженииМышкиVanessaExt);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовЭмуляцияДвиженияМыши(ОписаниеОповещения, Координаты.X, Координаты.Y,
			КоличествоШаговПриДвиженииМышкиVanessaExt, ПаузаМеждуШагамиПриДвиженииМышкиVanessaExt);
	ИначеЕсли ДополнительныеПараметры.Действие = "ЯОпределяюКоординатыКартинкиКак" Тогда
		Ванесса.СохранитьЗначениеПеременнойВКонтекст(ДополнительныеПараметры.ИмяПеременной, Координаты);
		Ванесса.ПродолжитьВыполнениеШагов();
	ИначеЕсли ДополнительныеПараметры.Действие = "ЯРисуюРамкуВокругКартинки" Тогда
		Толщина = ДополнительныеПараметры.thickness;
		НастройкиРисования = ЗаписатьСтрокуJSON(ДополнительныеПараметры);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваВнешнейКомпоненты", ЭтаФорма, ДополнительныеПараметры);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовНарисоватьПрямоугольник(ОписаниеОповещения, НастройкиРисования, 
			Координаты.Left - Толщина, Координаты.Top - Толщина, Координаты.Width + Толщина * 2, Координаты.Height + Толщина * 2);
	ИначеЕсли ДополнительныеПараметры.Действие = "ЯПоказываюПодсказкуEnjoyHintУКартинкиНаЭкране" Тогда
		Толщина = ДополнительныеПараметры.thickness;
		НастройкиРисования = ЗаписатьСтрокуJSON(ДополнительныеПараметры);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваВнешнейКомпоненты", ЭтаФорма, ДополнительныеПараметры);
		Ванесса.ПроверитьКорректностьПараметровДляВыполненияУроков(НастройкиРисования);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовНарисоватьТень(ОписаниеОповещения, НастройкиРисования, 
			Координаты.Left - Толщина, Координаты.Top - Толщина, Координаты.Width + Толщина * 2, Координаты.Height + Толщина * 2);
	ИначеЕсли ДополнительныеПараметры.Действие = "ЯРисуюСтрелкуОтДо" Тогда
		ДополнительныеПараметры.Вставить("Координаты", Координаты);
		ИмяФайла = ДополнительныеПараметры.КартинкаДо;
		ФайлКартинка = ПолучитьФайлКартинки(ИмяФайла);
		ПутьКФайлу = СтрЗаменить(ФайлКартинка.ПолноеИмя, "\", "/");
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяФайла, "ЯРисуюСтрелкуОтДо_КартинкаДо", ДополнительныеПараметры);
	ИначеЕсли ДополнительныеПараметры.Действие = "ЯРисуюСтрелкуОтДо_КартинкаДо" Тогда
		КоординатыОт = ДополнительныеПараметры.Координаты;
		НастройкиРисования = ЗаписатьСтрокуJSON(ДополнительныеПараметры);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваВнешнейКомпоненты", ЭтаФорма, ДополнительныеПараметры);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовНарисоватьСтрелку(ОписаниеОповещения, 
			НастройкиРисования, КоординатыОт.X, КоординатыОт.Y, Координаты.X, Координаты.Y);
	ИначеЕсли ДополнительныеПараметры.Действие = "ЯПеретаскиваюКартинкуК_КартинкаДо" Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваВнешнейКомпоненты", ЭтаФорма, ДополнительныеПараметры);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовЭмуляцияПеретаскивания(ОписаниеОповещения, 
			Координаты.X, Координаты.Y, ДополнительныеПараметры.КоличествоШагов, ДополнительныеПараметры.ПаузаМеждуШагами);
	Иначе
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Неправильно указано действие с картинкой <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ДополнительныеПараметры.Действие);
		Ванесса.ПродолжитьВыполнениеШагов(Истина, ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли

КонецФункции

&НаКлиенте
Функция ОкончаниеВызоваЭмуляцияДвиженияМыши(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваВнешнейКомпоненты", ЭтаФорма, ДополнительныеПараметры);
	Если ДополнительныеПараметры.Действие = "КликНаКартинку" Тогда
		Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовЭмуляцияНажатияМыши(ОписаниеОповещения, 0);
	ИначеЕсли ДополнительныеПараметры.Действие = "КликПравойКнопкойНаКартинку" Тогда
		Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовЭмуляцияНажатияМыши(ОписаниеОповещения, 1);
	ИначеЕсли ДополнительныеПараметры.Действие = "ДвойнойКликНаКартинку" Тогда
		Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовЭмуляцияДвойногоНажатия(ОписаниеОповещения);
	ИначеЕсли ДополнительныеПараметры.Действие = "ЯПеретаскиваюКартинкуК" Тогда
		ИмяФайла = ДополнительныеПараметры.КартинкаДо;
		ФайлКартинка = ПолучитьФайлКартинки(ИмяФайла);
		ПутьКФайлу = СтрЗаменить(ФайлКартинка.ПолноеИмя, "\", "/");
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяФайла, "ЯПеретаскиваюКартинкуК_КартинкаДо", ДополнительныеПараметры);
	Иначе
		Ванесса.ПродолжитьВыполнениеШагов();
	КонецЕсли
КонецФункции

&НаКлиенте
Функция ОкончаниеВызоваВнешнейКомпоненты(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	Ванесса.ПродолжитьВыполнениеШагов();
КонецФункции

&НаКлиенте
// Затем клик на текст "Текст"
//@КликНаТекст(Парам01)
Функция КликНаТекст(Текст) Экспорт
	Файл = Ванесса.ПолучитьФайлКартинкиИзТекста(Текст);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");
	
	СделатьКликНаКартинку(ПутьКФайлу, Текст);
	
КонецФункции

&НаКлиенте
Функция СделатьДвойнойКликНаКартинку(ПутьКФайлу, ИмяФайла)
	
	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ПутьКФайлу);
	ТекстСкрипта = ПолучитьТекстСкрипта("DoubleClickOnPicture", ПараметрыСкрипта);
	
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось сделать клик мышкой по картинке <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяФайла);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	
КонецФункции

&НаКлиенте
// Дано двойной клик на картинку "СтрокаУпавшегоШагаВСценарииПроверкиЭталонов"
//@ДвойнойКликНаКартинку(Парам01)
Функция ДвойнойКликНаКартинку(ИмяФайла) Экспорт
	Файл = ПолучитьФайлКартинки(ИмяФайла);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");

	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяФайла, "ДвойнойКликНаКартинку");
		Возврат Неопределено;
	КонецЕсли;

	СделатьДвойнойКликНаКартинку(ПутьКФайлу, ИмяФайла);
	
КонецФункции

&НаКлиенте
// Дано двойной клик на текст "СтрокаУпавшегоШагаВСценарииПроверкиЭталонов"
//@ДвойнойКликНаТекст(Парам01)
Функция ДвойнойКликНаТекст(ИмяФайла) Экспорт
	Файл = Ванесса.ПолучитьФайлКартинкиИзТекста(ИмяФайла);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");
	
	СделатьДвойнойКликНаКартинку(ПутьКФайлу, ИмяФайла);
	
КонецФункции

&НаКлиенте
Функция ПолучитьКоординатыКартинки(ПутьКФайлуКартинка, ИмяФайлаОтвета, Картинка)
	
	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ПутьКФайлуКартинка);
	ПараметрыСкрипта.Добавить(ИмяФайлаОтвета);
	ТекстСкрипта = ПолучитьТекстСкрипта("GetPictureCoordinats", ПараметрыСкрипта);
	
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось найти на экране картинку <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Картинка);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;		 
	
КонецФункции

&НаКлиенте
Функция ДополнитьСтруктуруДанныхКоординатКартинки(Данные)
	Если НЕ Данные.Свойство("X1") Тогда
		Данные.Вставить("X1", Данные.left);
		Данные.Вставить("X2", Данные.left + Данные.width);
		Данные.Вставить("Y1", Данные.top);
		Данные.Вставить("Y2", Данные.top + Данные.height);
	КонецЕсли;	 
КонецФункции 

&НаКлиенте
// Затем я рисую стрелку от "Vanessa_Automation" до "Запуск_сценариев"
//@ЯРисуюСтрелкуОтДо(Парам01,Парам02)
Функция ЯРисуюСтрелкуОтДо(КартинкаОт, КартинкаДо, ТабПарам) Экспорт
	
	ПереданныеПараметры = ПереданныеПараметры(ТабПарам);
	АбсолютныйЦвет = ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры);
	
	Если ПереданныеПараметры.Свойство("Толщина") Тогда
		Толщина = ПереданныеПараметры.Толщина;
	ИначеЕсли ПереданныеПараметры.Свойство("Width") Тогда
		Толщина = ПереданныеПараметры.Width;
	Иначе
		Толщина = "3";
	КонецЕсли;
	
	Если ПереданныеПараметры.Свойство("Длительность") Тогда
		Длительность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Duration") Тогда
		Длительность = ПереданныеПараметры.Duration;
	Иначе
		Длительность = "10000";
	КонецЕсли;	 
	
	Если ПереданныеПараметры.Свойство("Прозрачность") Тогда
		Прозрачность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Transparency") Тогда
		Прозрачность = ПереданныеПараметры.Transparency;
	Иначе
		Прозрачность = "255";
	КонецЕсли;	 
	
	Если ПереданныеПараметры.Свойство("Задержка") Тогда
		Задержка = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("frameDelay") Тогда
		Задержка = ПереданныеПараметры.Transparency;
	Иначе
		Задержка = "20";
	КонецЕсли;	 
	
	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ФайлКартинка = ПолучитьФайлКартинки(КартинкаОт);
		ПутьКФайлу = СтрЗаменить(ФайлКартинка.ПолноеИмя, "\", "/");
		ПараметрыРисования = Новый Структура;
		ПараметрыРисования.Вставить("color", ЦветЧислом(АбсолютныйЦвет));
		ПараметрыРисования.Вставить("transparency", Число(Прозрачность));
		ПараметрыРисования.Вставить("duration", Число(Длительность));
		ПараметрыРисования.Вставить("frameDelay", Число(Задержка));
		ПараметрыРисования.Вставить("thickness", Число(Толщина));
		ПараметрыРисования.Вставить("КартинкаОт", КартинкаОт);
		ПараметрыРисования.Вставить("КартинкаДо", КартинкаДо);
		НайтиКартинкуНаЭкране(ПутьКФайлу, КартинкаОт, "ЯРисуюСтрелкуОтДо", ПараметрыРисования);
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяФайлСтрелки = Ванесса.Объект.КаталогИнструментов + "/tools/VideoTools/Arrow.exe";
	Если НЕ Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайлСтрелки) Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не найден файл <%1>");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяФайлСтрелки);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	
	ДанныеОт = Неопределено;
	Если ТипЗнч(КартинкаОт) = Тип("Структура") Тогда
		ДанныеОт = КартинкаОт;
	КонецЕсли;	 
	Если ДанныеОт = Неопределено Тогда
		Если ЗначениеЗаполнено(КартинкаОт) Тогда
			ФайлКартинкаОт = ПолучитьФайлКартинки(КартинкаОт);
			ПутьКФайлуКартинкаОт = СтрЗаменить(ФайлКартинкаОт.ПолноеИмя, "\", "/");
			ИмяФайлаОтветаОт = ПолучитьИмяВременногоФайла("json");
			ПолучитьКоординатыКартинки(ПутьКФайлуКартинкаОт, ИмяФайлаОтветаОт, КартинкаОт);
			ДанныеОт = ПрочитатьФайлJson(ИмяФайлаОтветаОт);
			ТочкаОткудаX = Центр(ДанныеОт.X1, ДанныеОт.X2);
			ТочкаОткудаY = Центр(ДанныеОт.Y1, ДанныеОт.Y2);
		Иначе	
			ДанныеОт = ОпределитьКоординатыЦентраЭкрана(ТочкаОткудаX, ТочкаОткудаY);
		КонецЕсли;	 
	КонецЕсли;	 
	
	ДанныеДо = Неопределено;
	Если ТипЗнч(КартинкаДо) = Тип("Структура") Тогда
		ДанныеДо = КартинкаДо;
	КонецЕсли;	 
	Если ДанныеДо = Неопределено Тогда
		ФайлКартинкаДо = ПолучитьФайлКартинки(КартинкаДо);
		ПутьКФайлуКартинкаДо = СтрЗаменить(ФайлКартинкаДо.ПолноеИмя, "\", "/");
		ИмяФайлаОтветаДо = ПолучитьИмяВременногоФайла("json");
		ПолучитьКоординатыКартинки(ПутьКФайлуКартинкаДо, ИмяФайлаОтветаДо, КартинкаДо);
		ДанныеДо = ПрочитатьФайлJson(ИмяФайлаОтветаДо);
	КонецЕсли;	 
	
	ПереданныеПараметры = ПереданныеПараметры(ТабПарам);
	АбсолютныйЦвет = ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры);
	
	ДополнитьСтруктуруДанныхКоординатКартинки(ДанныеДо);
	ДополнитьСтруктуруДанныхКоординатКартинки(ДанныеОт);
	
	ТочкаКудаX   = Центр(ДанныеДо.X1, ДанныеДо.X2);
	ТочкаКудаY   = Центр(ДанныеДо.Y1, ДанныеДо.Y2);
	
	Если ПереданныеПараметры.Свойство("ТочкаОткуда") Тогда
		КоординатыТочкиИзТекста(ТочкаОткудаX, ТочкаОткудаY, ДанныеОт, ПереданныеПараметры.ТочкаОткуда);
	ИначеЕсли ПереданныеПараметры.Свойство("PointFrom") Тогда
		КоординатыТочкиИзТекста(ТочкаОткудаX, ТочкаОткудаY, ДанныеОт, ПереданныеПараметры.PointFrom);
	КонецЕсли;	 
	Если ПереданныеПараметры.Свойство("ТочкаКуда") Тогда
		КоординатыТочкиИзТекста(ТочкаКудаX, ТочкаКудаY, ДанныеДо, ПереданныеПараметры.ТочкаКуда);
	ИначеЕсли ПереданныеПараметры.Свойство("PointTo") Тогда
		КоординатыТочкиИзТекста(ТочкаКудаX, ТочкаКудаY, ДанныеДо, ПереданныеПараметры.PointTo);
	КонецЕсли;	 
	
	Длительность = ОпределитьДлительность(ПереданныеПараметры);
	Толщина = ОпределитьТолщину(ПереданныеПараметры);
	
	СтрокаКоманды = Ванесса.ОбернутьВКавычки(ИмяФайлСтрелки) + " %1 %2 %3 %4 %5 %6 %7 %8 %9 %10";	
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%1", Формат(ТочкаОткудаX, "ЧГ=; ЧН=0"));
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%2", Формат(ТочкаОткудаY, "ЧГ=; ЧН=0"));
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%3", Формат(ТочкаКудаX, "ЧГ=; ЧН=0"));
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%4", Формат(ТочкаКудаY, "ЧГ=; ЧН=0"));
	Попытка
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%5", Формат(АбсолютныйЦвет.Красный, "ЧГ=; ЧН=0"));// R
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%6", Формат(АбсолютныйЦвет.Зеленый, "ЧГ=; ЧН=0"));// G
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%7", Формат(АбсолютныйЦвет.Синий, "ЧГ=; ЧН=0"));// B
	Исключение
		Ванесса.СообщитьПользователю(Ванесса.Локализовать(ОписаниеОшибки()));
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%5", Формат(10, "ЧГ=; ЧН=0"));
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%6", Формат(10, "ЧГ=; ЧН=0"));
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%7", Формат(10, "ЧГ=; ЧН=0"));
	КонецПопытки;
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%8", Формат(Длительность, "ЧГ=; ЧН=0"));// длительность в мс
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%9", Формат(Толщина, "ЧГ=; ЧН=0"));// толщина линии
	СтопФайл = ПолучитьИмяВременногоФайла("txt");
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%10", "stopfilename=" + СтопФайл);
	Ванесса.ВыполнитьКомандуОСБезПоказаЧерногоОкна(СтрокаКоманды, 0);
КонецФункции

&НаКлиенте
// Затем я рисую стрелку от текста "Vanessa_Automation" до текста "Запуск_сценариев"
//@ЯРисуюСтрелкуОтТекстаДоТекста(Парам01,Парам02)
Функция ЯРисуюСтрелкуОтТекстаДоТекста(ТекстОт, ТекстДо, ТабПарам) Экспорт
	ИмяФайлСтрелки = Ванесса.Объект.КаталогИнструментов + "/tools/VideoTools/Arrow.exe";
	Если НЕ Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайлСтрелки) Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не найден файл <%1>");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяФайлСтрелки);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	
	Если ЗначениеЗаполнено(ТекстОт) Тогда 
		ФайлКартинкаОт = Ванесса.ПолучитьФайлКартинкиИзТекста(ТекстОт);
		ПутьКФайлуКартинкаОт = СтрЗаменить(ФайлКартинкаОт.ПолноеИмя, "\", "/");
		ИмяФайлаОтветаОт = ПолучитьИмяВременногоФайла("json");
		ПолучитьКоординатыКартинки(ПутьКФайлуКартинкаОт, ИмяФайлаОтветаОт, ТекстОт);
		ДанныеОт = ПрочитатьФайлJson(ИмяФайлаОтветаОт);
		ТочкаОткудаX = Центр(ДанныеОт.X1, ДанныеОт.X2);
		ТочкаОткудаY = Центр(ДанныеОт.Y1, ДанныеОт.Y2);
	Иначе	
		ДанныеОт = ОпределитьКоординатыЦентраЭкрана(ТочкаОткудаX, ТочкаОткудаY);
	КонецЕсли;	 
	
	ДанныеДо = Неопределено;
	Если ТипЗнч(ТекстДо) = Тип("Структура") Тогда
		ДанныеДо = ТекстДо;
	КонецЕсли;	 
	
	Если ДанныеДо = Неопределено Тогда
		ФайлКартинкаДо = Ванесса.ПолучитьФайлКартинкиИзТекста(ТекстДо);
		ПутьКФайлуКартинкаДо = СтрЗаменить(ФайлКартинкаДо.ПолноеИмя, "\", "/");
		ИмяФайлаОтветаДо = ПолучитьИмяВременногоФайла("json");
		ПолучитьКоординатыКартинки(ПутьКФайлуКартинкаДо, ИмяФайлаОтветаДо, ТекстДо);
		ДанныеДо = ПрочитатьФайлJson(ИмяФайлаОтветаДо);
	КонецЕсли;	 
	
	ПереданныеПараметры = ПереданныеПараметры(ТабПарам);
	АбсолютныйЦвет = ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры);
	
	ТочкаКудаX   = Центр(ДанныеДо.X1, ДанныеДо.X2);
	ТочкаКудаY   = Центр(ДанныеДо.Y1, ДанныеДо.Y2);
	
	Если ПереданныеПараметры.Свойство("ТочкаОткуда") Тогда
		КоординатыТочкиИзТекста(ТочкаОткудаX, ТочкаОткудаY, ДанныеОт, ПереданныеПараметры.ТочкаОткуда);
	ИначеЕсли ПереданныеПараметры.Свойство("PointFrom") Тогда
		КоординатыТочкиИзТекста(ТочкаОткудаX, ТочкаОткудаY, ДанныеОт, ПереданныеПараметры.PointFrom);
	КонецЕсли;	 
	Если ПереданныеПараметры.Свойство("ТочкаКуда") Тогда
		КоординатыТочкиИзТекста(ТочкаКудаX, ТочкаКудаY, ДанныеДо, ПереданныеПараметры.ТочкаКуда);
	ИначеЕсли ПереданныеПараметры.Свойство("PointTo") Тогда
		КоординатыТочкиИзТекста(ТочкаКудаX, ТочкаКудаY, ДанныеДо, ПереданныеПараметры.PointTo);
	КонецЕсли;	 
	
	Длительность = ОпределитьДлительность(ПереданныеПараметры);
	
	Толщина = ОпределитьТолщину(ПереданныеПараметры);
	
	СтрокаКоманды = Ванесса.ОбернутьВКавычки(ИмяФайлСтрелки) + " %1 %2 %3 %4 %5 %6 %7 %8 %9 %10";	
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%1", Формат(ТочкаОткудаX, "ЧГ=; ЧН=0"));
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%2", Формат(ТочкаОткудаY, "ЧГ=; ЧН=0"));
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%3", Формат(ТочкаКудаX, "ЧГ=; ЧН=0"));
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%4", Формат(ТочкаКудаY, "ЧГ=; ЧН=0"));
	Попытка
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%5", Формат(АбсолютныйЦвет.Красный, "ЧГ=; ЧН=0"));// R
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%6", Формат(АбсолютныйЦвет.Зеленый, "ЧГ=; ЧН=0"));// G
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%7", Формат(АбсолютныйЦвет.Синий, "ЧГ=; ЧН=0"));// B
	Исключение
		Ванесса.СообщитьПользователю(Ванесса.Локализовать(ОписаниеОшибки()));
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%5", Формат(10, "ЧГ=; ЧН=0"));
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%6", Формат(10, "ЧГ=; ЧН=0"));
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%7", Формат(10, "ЧГ=; ЧН=0"));
	КонецПопытки;
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%8", Формат(Длительность, "ЧГ=; ЧН=0"));// длительность в мс
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%9", Формат(Толщина, "ЧГ=; ЧН=0"));// толщина линии
	СтопФайл = ПолучитьИмяВременногоФайла("txt");
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%10", "stopfilename=" + СтопФайл);
	Ванесса.ВыполнитьКомандуОСБезПоказаЧерногоОкна(СтрокаКоманды, 0);
КонецФункции

&НаКлиенте
Функция ОпределитьКоординатыЦентраЭкрана(ТочкаОткудаX, ТочкаОткудаY)
	
	Перем ИнформацияЭкранаКлиента;
	
	ИнформацияЭкранаКлиента = Вычислить("ПолучитьИнформациюЭкрановКлиента()[0]");
	Ширина = ИнформацияЭкранаКлиента.Ширина;
	Высота = ИнформацияЭкранаКлиента.Высота;
	
	ТочкаОткудаX = Окр(Ширина / 2);
	ТочкаОткудаY = Окр(Высота / 2);
	
	ДанныеОт = Новый Структура;
	ДанныеОт.Вставить("X1", ТочкаОткудаX);
	ДанныеОт.Вставить("X2", ТочкаОткудаX);
	ДанныеОт.Вставить("Y1", ТочкаОткудаY);
	ДанныеОт.Вставить("Y2", ТочкаОткудаY);
	
	Возврат ДанныеОт;

КонецФункции

&НаКлиенте
Функция ОпределитьТолщину(Знач ПереданныеПараметры)
	
	Перем Толщина;
	
	Если ПереданныеПараметры.Свойство("Толщина") Тогда
		Толщина = ПереданныеПараметры.Толщина;
	ИначеЕсли ПереданныеПараметры.Свойство("Width") Тогда
		Толщина = ПереданныеПараметры.Width;
	Иначе
		Толщина = "3";
	КонецЕсли;
	Возврат Толщина;

КонецФункции

&НаКлиенте
Функция ОпределитьДлительность(Знач ПереданныеПараметры)
	
	Перем Длительность;
	
	Если ПереданныеПараметры.Свойство("Длительность") Тогда
		Длительность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Duration") Тогда
		Длительность = ПереданныеПараметры.Duration;
	Иначе
		Длительность = "10000";
	КонецЕсли;
	Возврат Длительность;

КонецФункции


&НаКлиенте
// И я запускаю SikuliX сервер
//@ЯЗапускаюSikuliXСервер()
Функция ЯЗапускаюSikuliXСервер() Экспорт
	Ванесса.ЗапуститьSikuliXСевер();
КонецФункции


&НаКлиенте
// Дано я нажимаю хоткей "r" "win"
//@ЯНажимаюХоткей(Парам01,Парам02)
Функция ЯНажимаюХоткей(ХотКей, Модификатор = "no") Экспорт
	
	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ХотКей);
	ПараметрыСкрипта.Добавить(НРег(Модификатор));
	ТекстСкрипта = ПолучитьТекстСкрипта("HotKey", ПараметрыСкрипта);
	
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось выполнить эмуляцию хоткея <%1> <%2>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ХотКей);
		Если Модификатор = "no" Тогда
			ТекстСообщения = СтрЗаменить(ТекстСообщения," <%2>", "");
		Иначе	
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%2", Модификатор);
		КонецЕсли;	 
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
КонецФункции

&НаКлиенте
// Дано я нажимаю ENTER
//@ЯНажимаюENTER()
Функция ЯНажимаюENTER() Экспорт
	ТекстСкрипта = ПолучитьТекстСкрипта("TypeENTER");
	
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось выполнить эмуляцию ENTER.");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
КонецФункции

&НаКлиенте
Функция ОбработчикЯЖдуКартинкиВТечениеСекунд()
	
	ИмяОбработчика = "ОбработчикЯЖдуКартинкиВТечениеСекунд";
	
	Если НЕ Ванесса.ИдетВыполнениеСценариев() Тогда
		ОтключитьОбработчикОжидания(ИмяОбработчика);
		Возврат Неопределено;
	КонецЕсли;
	
	Если (ТекущаяДата() - ДатаНачалаОбработкиОжидания) > КоличествоСекундОбработкаОжидания Тогда
		ОтключитьОбработчикОжидания(ИмяОбработчика);
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Ожидалось в течение <%1> секунд появления картинки <%2>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",КоличествоСекундОбработкаОжидания);
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%2",ИмяФайлаОбработчикОжидания);
		Ванесса.ПродолжитьВыполнениеШагов(Истина, ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;	 
	
	ДопПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВызоваНайтиНаЭкранеОбработчикЯЖдуКартинкиВТечениеСекунд", ЭтаФорма, ДопПараметры);
	ФрагментКартинки = Новый ДвоичныеДанные(ИмяФайлаОбработчикОжидания);
	Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовНайтиНаЭкране(ОписаниеОповещения, ФрагментКартинки);
	
КонецФункции 

&НаКлиенте
Функция ОкончаниеВызоваНайтиНаЭкранеОбработчикЯЖдуКартинкиВТечениеСекунд(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	ДопустимыйПредел = Ванесса.ПорогПоискаКартинки();
	Координаты = Ванесса.ПрочитатьСтрокуJSON(Результат);
	
	Если Координаты.Match < ДопустимыйПредел Тогда
		ПодключитьОбработчикОжидания("ОбработчикЯЖдуКартинкиВТечениеСекунд", 1, Истина);
		Возврат Неопределено;
	КонецЕсли;

	//Значит картинка была найдена.
	Ванесса.ПродолжитьВыполнениеШагов();

КонецФункции

&НаКлиенте
// И я жду картинки "ИмяКартинки" в течение 20 секунд
//@ЯЖдуКартинкиВТечениеСекунд(Парам01,Парам02)
Функция ЯЖдуКартинкиВТечениеСекунд(ИмяКартинки, КоличествоСекунд) Экспорт
	Файл = ПолучитьФайлКартинки(ИмяКартинки);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");
	
	Если НЕ Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить использование внешней компоненты VanessaExt.");
	КонецЕсли;	 

	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		Ванесса.ЗапретитьВыполнениеШагов();
		ИмяФайлаОбработчикОжидания = ПутьКФайлу;
		КоличествоСекундОбработкаОжидания = Ванесса.ЗначениеТаймаутаДляАсинхронногоШага(КоличествоСекунд);
		ДатаНачалаОбработкиОжидания = ТекущаяДата();
		ПодключитьОбработчикОжидания("ОбработчикЯЖдуКартинкиВТечениеСекунд", 1, Истина);
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ПутьКФайлу);
	ПараметрыСкрипта.Добавить(Формат(КоличествоСекунд, "ЧГ=; ЧН=0"));
	ТекстСкрипта = ПолучитьТекстСкрипта("WaitPict", ПараметрыСкрипта);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Таймаут", Число(КоличествоСекунд));
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина,, ДопПараметры);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось дождаться картинки <%1> в течение <%2> секунд.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяКартинки);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%2", КоличествоСекунд);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
КонецФункции

&НаКлиенте
Функция ПолучитьДанныеКартинкиНаЭкране(ИмяКартинки)
	ФайлКартинка = ПолучитьФайлКартинки(ИмяКартинки);
	ПутьКФайлуКартинки = СтрЗаменить(ФайлКартинка.ПолноеИмя, "\", "/");
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла("json");
	
	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ПутьКФайлуКартинки);
	ПараметрыСкрипта.Добавить(ИмяФайлаОтвета);
	ТекстСкрипта = ПолучитьТекстСкрипта("GetPictureCoordinats", ПараметрыСкрипта);
	
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось найти на экране картинку <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяКартинки);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	Данные = ПрочитатьФайлJson(ИмяФайлаОтвета);
	Данные.Вставить("left", Данные.X1);
	Данные.Вставить("top", Данные.Y1);
	Данные.Вставить("width", Данные.X2 - Данные.X1);
	Данные.Вставить("height", Данные.Y2 - Данные.Y1);
	Возврат Данные;
КонецФункции	 

&НаКлиенте
// И я определяю координаты картинки "ИмяКартинки" как "ИмяПеременной"
//@ЯОпределяюКоординатыКартинкиКак(Парам01,Парам02)
Функция ЯОпределяюКоординатыКартинкиКак(ИмяКартинки, ИмяПеременной) Экспорт
	
	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ФайлКартинка = ПолучитьФайлКартинки(ИмяКартинки);
		ПутьКФайлу = СтрЗаменить(ФайлКартинка.ПолноеИмя, "\", "/");
		ДопПараметры = Новый Структура("ИмяПеременной", ИмяПеременной);
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяКартинки, "ЯОпределяюКоординатыКартинкиКак", ДопПараметры);
		Возврат Неопределено;
	КонецЕсли;
	
	Данные = ПолучитьДанныеКартинкиНаЭкране(ИмяКартинки);
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Данные);
КонецФункции

&НаКлиенте
// И я рисую рамку вокруг картинки "ИмяКартинки"
//@ЯРисуюРамкуВокругКартинки(Парам01)
Функция ЯРисуюРамкуВокругКартинки(ИмяКартинки, ТабПарам) Экспорт
	
	ПереданныеПараметры = ПереданныеПараметры(ТабПарам);
	АбсолютныйЦвет = ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры);
	
	Если ПереданныеПараметры.Свойство("Толщина") Тогда
		Толщина = ПереданныеПараметры.Толщина;
	ИначеЕсли ПереданныеПараметры.Свойство("Width") Тогда
		Толщина = ПереданныеПараметры.Width;
	Иначе
		Толщина = "3";
	КонецЕсли;
	
	Если ПереданныеПараметры.Свойство("Длительность") Тогда
		Длительность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Duration") Тогда
		Длительность = ПереданныеПараметры.Duration;
	Иначе
		Длительность = "10000";
	КонецЕсли;	 
	
	Если ПереданныеПараметры.Свойство("Прозрачность") Тогда
		Прозрачность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Transparency") Тогда
		Прозрачность = ПереданныеПараметры.Transparency;
	Иначе
		Прозрачность = "255";
	КонецЕсли;	 
	
	
	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ФайлКартинка = ПолучитьФайлКартинки(ИмяКартинки);
		ПутьКФайлу = СтрЗаменить(ФайлКартинка.ПолноеИмя, "\", "/");
		ПараметрыРисования = Новый Структура;
		ПараметрыРисования.Вставить("color", ЦветЧислом(АбсолютныйЦвет));
		ПараметрыРисования.Вставить("transparency", Число(Прозрачность));
		ПараметрыРисования.Вставить("duration", Число(Длительность));
		ПараметрыРисования.Вставить("thickness", Число(Толщина));
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяКартинки, "ЯРисуюРамкуВокругКартинки", ПараметрыРисования);
		Возврат Неопределено;
	КонецЕсли;
	
	Данные = ПолучитьДанныеКартинкиНаЭкране(ИмяКартинки);
	
	ИмяФайлРамки = Ванесса.Объект.КаталогИнструментов + "/tools/VideoTools/Rectangle.exe";
	Если НЕ Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайлРамки) Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не найден файл <%1>");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяФайлРамки);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	 
	СтрокаКоманды = Ванесса.ОбернутьВКавычки(ИмяФайлРамки) + " %1 %2 %3 %4 %5 %6 %7 %8 %9 %10";	
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%1", Формат(Данные.X1, "ЧГ=; ЧН=0"));
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%2", Формат(Данные.Y1, "ЧГ=; ЧН=0"));
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%3", Формат(Данные.X2, "ЧГ=; ЧН=0"));
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%4", Формат(Данные.Y2, "ЧГ=; ЧН=0"));
	Попытка
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%5", Формат(АбсолютныйЦвет.Красный, "ЧГ=; ЧН=0"));// R
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%6", Формат(АбсолютныйЦвет.Зеленый, "ЧГ=; ЧН=0"));// G
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%7", Формат(АбсолютныйЦвет.Синий, "ЧГ=; ЧН=0"));// B
	Исключение
		Ванесса.СообщитьПользователю(Ванесса.Локализовать(ОписаниеОшибки()));
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%5", Формат(10, "ЧГ=; ЧН=0"));
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%6", Формат(10, "ЧГ=; ЧН=0"));
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%7", Формат(10, "ЧГ=; ЧН=0"));
	КонецПопытки;
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%8", Формат(Длительность, "ЧГ=; ЧН=0"));// длительность в мс
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%9", Формат(Толщина, "ЧГ=; ЧН=0"));// толщина линии
	СтопФайл = ПолучитьИмяВременногоФайла("txt");
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%10", "stopfilename=" + СтопФайл);
	Ванесса.ВыполнитьКомандуОСБезПоказаЧерногоОкна(СтрокаКоманды, 0);
	
КонецФункции

&НаКлиенте
// И я вращаю колесо мышки "Вверх" 3 раз с зажатой клавишей ctrl
//@ЯВращаюКолесоМышкиРазСЗажатойКлавишейCtrl(Парам01,Парам02)
Функция ЯВращаюКолесоМышкиРазСЗажатойКлавишейCtrl(Направление, КоличествоРаз) Экспорт
	Если Нрег(Направление) = "вверх" Тогда
		ПараметрНаправление = "up";
	ИначеЕсли Нрег(Направление) = "up" Тогда
		ПараметрНаправление = "up";
	ИначеЕсли Нрег(Направление) = "вниз" Тогда
		ПараметрНаправление = "down";
	ИначеЕсли Нрег(Направление) = "down" Тогда
		ПараметрНаправление = "down";
	Иначе
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Неверно указано значение параметра направления вращения колеса мышки: <%1>. Возможные значения: Вверх или Вниз.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Направление);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	
	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(Направление);
	ПараметрыСкрипта.Добавить(Формат(КоличествоРаз, "ЧГ=; ЧН=0"));
	ТекстСкрипта = ПолучитьТекстСкрипта("WheelCtrl", ПараметрыСкрипта);
	
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось выполнить эмуляцию вращения колеса мышки в направлении <%1>, количество итераций <%2>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Направление);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%2", Формат(КоличествоРаз, "ЧГ=; ЧН=0"));
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
КонецФункции

&НаКлиенте
// Если есть картинка "ИмяКартинки" Тогда
//@ЕстьКартинкаТогда(Парам01)
Функция ЕстьКартинкаТогда(ИмяКартинки) Экспорт
	Файл = ПолучитьФайлКартинки(ИмяКартинки);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");

	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяКартинки, "ЕстьКартинкаТогда");
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла("json");

	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ПутьКФайлу);
	ПараметрыСкрипта.Добавить(ИмяФайлаОтвета);
	ТекстСкрипта = ПолучитьТекстСкрипта("PictExists", ПараметрыСкрипта);
	
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось выполнить SikuliX скрипт <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", "PictExists");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	
	Данные = ПрочитатьФайлJson(ИмяФайлаОтвета);
	Если Число(Данные.exists) = 1 Тогда
		Ванесса.УстановитьРезультатУсловия(Истина);
	Иначе	
		Ванесса.УстановитьРезультатУсловия(Ложь);
	КонецЕсли;	 
КонецФункции

&НаКлиенте
Функция СделатьКликПравойКнопкойМышиНаКартинку(ПутьКФайлу, ИмяФайла)
	
	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ПутьКФайлу);
	ТекстСкрипта = ПолучитьТекстСкрипта("ClickRightOnPicture", ПараметрыСкрипта);
	
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось сделать клик правой кнопкой мышки по картинке <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяФайла);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 

КонецФункции

&НаКлиенте
// Дано клик правой кнопкой на картинку "Тогда открылось окно"
//@КликПравойКнопкойНаКартинку(Парам01)
Функция КликПравойКнопкойНаКартинку(ИмяФайла) Экспорт
	Файл = ПолучитьФайлКартинки(ИмяФайла);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");

	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяФайла, "КликПравойКнопкойНаКартинку");
		Возврат Неопределено;
	КонецЕсли;
	
	СделатьКликПравойКнопкойМышиНаКартинку(ПутьКФайлу, ИмяФайла);
	
КонецФункции

&НаКлиенте
// Дано клик правой кнопкой на текст "Текст"
//@КликПравойКнопкойНаТекст(Парам01)
Функция КликПравойКнопкойНаТекст(Текст) Экспорт
	Файл = Ванесса.ПолучитьФайлКартинкиИзТекста(Текст);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");
	
	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ПутьКФайлу);
	ТекстСкрипта = ПолучитьТекстСкрипта("ClickRightOnPicture", ПараметрыСкрипта);
	
	СделатьКликПравойКнопкойМышиНаКартинку(ПутьКФайлу, ПутьКФайлу);
	 
КонецФункции

&НаКлиенте
// Дано курсор к картинке "Свернуть до фич"
//@КурсорККартинке(Парам01)
Функция КурсорККартинке(ИмяФайла) Экспорт
	Файл = ПолучитьФайлКартинки(ИмяФайла);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");

	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяФайла, "КурсорККартинке");
		Возврат Неопределено;
	КонецЕсли;
	
	Рез = Ванесса.ПереместитьКурсорМышкиККартинке(ПутьКФайлу);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось переместить курсор мышки к картинке <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяФайла);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
КонецФункции

&НаКлиенте
// Дано курсор к тексту "Текст"
//@КурсорКТексту(Парам01)
Функция КурсорКТексту(ИмяФайла) Экспорт
	Файл = Ванесса.ПолучитьФайлКартинкиИзТекста(ИмяФайла);
	ПутьКФайлу = СтрЗаменить(Файл.ПолноеИмя, "\", "/");
	
	Рез = Ванесса.ПереместитьКурсорМышкиККартинке(ПутьКФайлу);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось переместить курсор мышки к картинке <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ПутьКФайлу);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
КонецФункции



&НаКлиенте
// И я устанавливаю основной профиль цвета
//@ЯУстанавливаюОсновнойПрофильЦвета(ТабПарам)
Функция ЯУстанавливаюОсновнойПрофильЦвета(ТабПарам) Экспорт
	Если ТипЗнч(ТабПарам) = Тип("Строка") Тогда
		ИмяПрофиля = ТабПарам;
		ДанныеПрофиля = Ванесса.ДанныеПрофиляПоискаКартинкиСТекстом(ИмяПрофиля);
		КонтекстСохраняемый.Вставить("ОсновнойПрофильПоискаКартинкиСТекстом", ДанныеПрофиля);
	Иначе	
		ДанныеПрофиля = Новый Структура;
		Для Каждого СтрокаТабПарам Из ТабПарам Цикл
			ДанныеПрофиля.Вставить(СтрокаТабПарам.Кол1, СтрокаТабПарам.Кол2);
		КонецЦикла;	 
		
		КонтекстСохраняемый.Вставить("ОсновнойПрофильПоискаКартинкиСТекстом", ДанныеПрофиля);
	КонецЕсли;	 
	
КонецФункции

&НаКлиенте
// И я устанавливаю профиль цвета "ИмяПрофиля"
//@ЯУстанавливаюПрофильЦвета(Парам01,ТабПарам)
Функция ЯУстанавливаюПрофильЦвета(ИмяПрофиля, ТабПарам) Экспорт
	ДанныеПрофиля = Новый Структура;
	Для Каждого СтрокаТабПарам Из ТабПарам Цикл
		ДанныеПрофиля.Вставить(СтрокаТабПарам.Кол1, СтрокаТабПарам.Кол2);
	КонецЦикла;	 
	Ванесса.УстановитьПрофильПоискаКартинкиСТекстом(ИмяПрофиля, ДанныеПрофиля);
КонецФункции

&НаКлиенте
// И я перетаскиваю картинку "ИмяФайла1" к "ИмяФайла2"
//@ЯПеретаскиваюКартинкуК(Парам01,Парам02)
Функция ЯПеретаскиваюКартинкуК(ИмяФайла1, ИмяФайла2) Экспорт
	
	Файл = ПолучитьФайлКартинки(ИмяФайла1);
	ПутьКФайлу1 = СтрЗаменить(Файл.ПолноеИмя, "\", "/");
	
	Файл = ПолучитьФайлКартинки(ИмяФайла2);
	ПутьКФайлу2 = СтрЗаменить(Файл.ПолноеИмя, "\", "/");

	Если Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt И Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ПараметрыРисования = Новый Структура;
		ПараметрыРисования.Вставить("КартинкаОт", ИмяФайла1);
		ПараметрыРисования.Вставить("КартинкаДо", ИмяФайла2);
		НайтиКартинкуНаЭкране(ПутьКФайлу1, ИмяФайла1, "ЯПеретаскиваюКартинкуК", ПараметрыРисования);
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыСкрипта = Новый Массив;
	ПараметрыСкрипта.Добавить(ПутьКФайлу1);
	ПараметрыСкрипта.Добавить(ПутьКФайлу2);
	ТекстСкрипта = ПолучитьТекстСкрипта("DragAndDrop", ПараметрыСкрипта);
	Рез = Ванесса.ВыполнитьSikuliСкрипт(ТекстСкрипта, -1, Истина);
	Если Рез <> 0 Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось сделать DragAndDrop от <%1> к <%2>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ИмяФайла1);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%2", ИмяФайла2);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	
КонецФункции

&НаКлиенте
Функция ЯПоказываюПодсказкуEnjoyHintУКартинкиНаЭкране(ТекстПодсказки, ИмяКартинки, ТабПарам) Экспорт
	
	ПереданныеПараметры = ПереданныеПараметры(ТабПарам);
	АбсолютныйЦвет = ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры);
	
	Если ПереданныеПараметры.Свойство("Толщина") Тогда
		Толщина = ПереданныеПараметры.Толщина;
	ИначеЕсли ПереданныеПараметры.Свойство("Width") Тогда
		Толщина = ПереданныеПараметры.Width;
	Иначе
		Толщина = "3";
	КонецЕсли;
	
	Если ПереданныеПараметры.Свойство("Длительность") Тогда
		Длительность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Duration") Тогда
		Длительность = ПереданныеПараметры.Duration;
	Иначе
		Длительность = "10000";
	КонецЕсли;	 
	
	Если ПереданныеПараметры.Свойство("Прозрачность") Тогда
		Прозрачность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Transparency") Тогда
		Прозрачность = ПереданныеПараметры.Transparency;
	Иначе
		Прозрачность = "255";
	КонецЕсли;	 
	
	Если НЕ Ванесса.Объект.ПоискКартинокСПомощьюКомпонентыVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить поиск картинок с помощью внешней компоненты VanessaExt.");
	ИначеЕсли НЕ Ванесса.Объект.ИспользоватьКомпонентуVanessaExt Тогда
		ВызватьИсключение Ванесса.Локализовать("Необходимо включить использование внешней компоненты VanessaExt.");
	Иначе
		ФайлКартинка = ПолучитьФайлКартинки(ИмяКартинки);
		ПутьКФайлу = СтрЗаменить(ФайлКартинка.ПолноеИмя, "\", "/");
		ПараметрыРисования = Новый Структура;
		ПараметрыРисования.Вставить("color", ЦветЧислом(АбсолютныйЦвет));
		ПараметрыРисования.Вставить("transparency", Число(Прозрачность));
		ПараметрыРисования.Вставить("duration", Число(Длительность));
		ПараметрыРисования.Вставить("thickness", Число(Толщина));
		ПараметрыРисования.Вставить("text", ТекстПодсказки);
		НайтиКартинкуНаЭкране(ПутьКФайлу, ИмяКартинки, "ЯПоказываюПодсказкуEnjoyHintУКартинкиНаЭкране", ПараметрыРисования);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ЯПоказываюНаЭкранеТекст(ТекстПодсказки, ТабПарам) Экспорт
	
	ПереданныеПараметры = ПереданныеПараметры(ТабПарам);
	АбсолютныйЦвет = ПолучитьАбсолютныйЦветИзПараметров(ПереданныеПараметры);

	Если ПереданныеПараметры.Свойство("Шрифт") Тогда
		Шрифт = ПереданныеПараметры.Шрифт;
	ИначеЕсли ПереданныеПараметры.Свойство("Font") Тогда
		Шрифт = ПереданныеПараметры.Font;
	Иначе
		Шрифт = "Arial";
	КонецЕсли;
	
	Если ПереданныеПараметры.Свойство("Размер") Тогда
		Размер = ПереданныеПараметры.Размер;
	ИначеЕсли ПереданныеПараметры.Свойство("Size") Тогда
		Размер = ПереданныеПараметры.Size;
	Иначе
		Размер = "20";
	КонецЕсли;
	
	Если ПереданныеПараметры.Свойство("X") Тогда
		X = ПереданныеПараметры.X;
	Иначе
		X = "800";
	КонецЕсли;
	
	Если ПереданныеПараметры.Свойство("Y") Тогда
		Y = ПереданныеПараметры.Y;
	Иначе
		Y = "600";
	КонецЕсли;
	
	Если ПереданныеПараметры.Свойство("Длительность") Тогда
		Длительность = ПереданныеПараметры.Длительность;
	ИначеЕсли ПереданныеПараметры.Свойство("Duration") Тогда
		Длительность = ПереданныеПараметры.Duration;
	Иначе
		Длительность = "10000";
	КонецЕсли;	 
	
	X = Число(X);
	Y = Число(Y); 
	
	ПараметрыРисования = Новый Структура();
	ПараметрыРисования.Вставить("duration", Число(Длительность));
	ПараметрыРисования.Вставить("fontColor", ЦветЧислом(АбсолютныйЦвет));
	ПараметрыРисования.Вставить("fontSize", Число(Размер));
	ПараметрыРисования.Вставить("fontName", Шрифт);
	НастройкиРисования = ЗаписатьСтрокуJSON(ПараметрыРисования);

	Ванесса.ЗапретитьВыполнениеШагов();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеНачатьВызовПоказатьТекст", ЭтаФорма);
	Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовПоказатьТекст(
		ОписаниеОповещения, НастройкиРисования, X, Y, ТекстПодсказки);
	
КонецФункции

&НаКлиенте
Функция ОкончаниеНачатьВызовПоказатьТекст(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Ванесса.ПродолжитьВыполнениеШагов();
	
КонецФункции
