//начало текста модуля

///////////////////////////////////////////////////
//Служебная часть
///////////////////////////////////////////////////

&НаКлиенте
// контекст фреймворка Vanessa-Behavior
Перем Ванесса;
 
&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов. Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт;
 
&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев. Существует, пока открыта форма Vanessa-Behavior.
Перем КонтекстСохраняемый Экспорт;

// Делает отключение модуля
&НаКлиенте
Функция ОтключениеМодуля() Экспорт

	Ванесса = Неопределено;
	Контекст = Неопределено;
	КонтекстСохраняемый = Неопределено;

КонецФункции

&НаКлиенте
// Функция экспортирует список шагов, которые реализованы в данной внешней обработке.
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;
	
	ВсеТесты = Новый Массив;

	//описание параметров
	//ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,Транзакция,Параметр);

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯСоздаюFixturesПоМакету(Парам01)","ЯСоздаюFixturesПоМакету","И я создаю fixtures по макету ""Макет""","Создает данные из макета MXL","Прочее.Fixtures");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВБазеОтсутствуетПользовательИБ(Парам01)","ВБазеОтсутствуетПользовательИБ","Когда В базе отсутствует пользователь ИБ ""ТестовыйПользователь""");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВБазеСуществуетПользовательИБ(Парам01)","ВБазеСуществуетПользовательИБ","Тогда В базе существует пользователь ИБ ""ТестовыйПользователь""");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯУдаляюПользователяИБ(Парам01)","ЯУдаляюПользователяИБ","И Я удаляю пользователя ИБ ""ТестовыйПользователь""");

	Возврат ВсеТесты;
КонецФункции
	
&НаСервере
// Служебная функция.
Функция ПолучитьМакетСервер(ИмяМакета)
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектСервер.ПолучитьМакет(ИмяМакета);
КонецФункции
	
&НаКлиенте
// Служебная функция для подключения библиотеки создания fixtures.
Функция ПолучитьМакетОбработки(ИмяМакета) Экспорт
	Возврат ПолучитьМакетСервер(ИмяМакета);
КонецФункции



///////////////////////////////////////////////////
//Работа со сценариями
///////////////////////////////////////////////////

&НаКлиенте
// Функция выполняется перед началом каждого сценария
Функция ПередНачаломСценария() Экспорт
	
КонецФункции

&НаКлиенте
// Функция выполняется перед окончанием каждого сценария
Функция ПередОкончаниемСценария() Экспорт
	
КонецФункции



///////////////////////////////////////////////////
//Реализация шагов
///////////////////////////////////////////////////

&НаКлиенте
//И я создаю fixtures по макету "Макет1"
//@ЯСоздаюFixturesПоМакету(Парам01)
Функция ЯСоздаюFixturesПоМакету(Знач ИмяМакета) Экспорт
	Макет = Ванесса.ПолучитьМакетОбработки(ИмяМакета);
	Если Макет = Неопределено И Ванесса.Объект.ВерсияПоставки = "single" Тогда
		ИмяМакета = "МакетОбработки_СозданиеFixturesПоМакетуОбработкиФичи_" + ИмяМакета;
		Макет = Ванесса.ПолучитьМакетОбработки(ИмяМакета);
	КонецЕсли;	 
	
	Если Макет = Неопределено Тогда
		ТекстСообщения = Ванесса.ПолучитьТекстСообщенияПользователю("Не получилось найти данные макета <%1>.");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1",ИмяМакета); 
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	 
	ВозвращатьДанные = Истина;
	СтруктураДанных = Ванесса.СоздатьДанныеПоТабличномуДокументу(Макет, Неопределено, Неопределено, ВозвращатьДанные);
	// не сериализуемые типы не возвращаем
	Если ВозвращатьДанные Тогда
		Ванесса.ПроверитьНеРавенство(СтруктураДанных,Неопределено,"Получили структуру данных.");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
//Когда В базе отсутствует пользователь ИБ "ТестовыйПользователь"
//@ВБазеОтсутствуетПользовательИБ(Парам01)
Функция ВБазеОтсутствуетПользовательИБ(ИмяПользователя) Экспорт
	//Ванесса.ПосмотретьЗначение(Парам01,Истина);
	ДопСообщениеОшибки = "Ожидали, что пользователь " + ИмяПользователя + " отсутствует в базе";
	Ванесса.ПроверитьИстину(ВБазеОтсутствуетПользовательИБСервер(ИмяПользователя), ДопСообщениеОшибки);
КонецФункции

&НаСервереБезКонтекста
Функция ВБазеОтсутствуетПользовательИБСервер(ИмяПользователя)
	Возврат ПользователиИнформационнойБазы.НайтиПоИмени(ИмяПользователя) = Неопределено;	
КонецФункции

&НаКлиенте
//Тогда В базе существует пользователь ИБ "ТестовыйПользователь"
//@ВБазеСуществуетПользовательИБ(Парам01)
Функция ВБазеСуществуетПользовательИБ(ИмяПользователя) Экспорт
	//Ванесса.ПосмотретьЗначение(Парам01,Истина);
	ДопСообщениеОшибки = "Ожидали, что пользователь " + ИмяПользователя + " существует в базе";
	Ванесса.ПроверитьЛожь(ВБазеОтсутствуетПользовательИБСервер(ИмяПользователя), ДопСообщениеОшибки);
КонецФункции

&НаКлиенте
//И Я удаляю пользователя ИБ "ТестовыйПользователь"
//@ЯУдаляюПользователяИБ(Парам01)
Функция ЯУдаляюПользователяИБ(ИмяПользователя) Экспорт
	//Ванесса.ПосмотретьЗначение(Парам01,Истина);
	ЯУдаляюПользователяИБСервер(ИмяПользователя);
КонецФункции

&НаСервереБезКонтекста
Функция ЯУдаляюПользователяИБСервер(ИмяПользователя)
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(ИмяПользователя);
	ПользовательИБ.Удалить();
КонецФункции

//окончание текста модуля

