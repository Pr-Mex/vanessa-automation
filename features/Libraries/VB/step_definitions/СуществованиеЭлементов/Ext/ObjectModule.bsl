Перем Ванесса;

Функция ДобавитьШагВМассивТестов(МассивТестов,Снипет,ИмяПроцедуры,ПредставлениеТеста = Неопределено,Транзакция = Неопределено,Параметр = Неопределено)
	Структура = Новый Структура;
	Структура.Вставить("Снипет",Снипет);
	Структура.Вставить("ИмяПроцедуры",ИмяПроцедуры);
	Структура.Вставить("ИмяПроцедуры",ИмяПроцедуры);
	Структура.Вставить("ПредставлениеТеста",ПредставлениеТеста);
	Структура.Вставить("Транзакция",Транзакция);
	Структура.Вставить("Параметр",Параметр);
	МассивТестов.Добавить(Структура);
КонецФункции

Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;
	
	ВсеТесты = Новый Массив;

	//описание параметров
	//ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,Транзакция,Параметр);

	ДобавитьШагВМассивТестов(ВсеТесты,"ВМетаданныхЕстьСправочник(Парам01Строка)","ВМетаданныхЕстьСправочник","в метаданных есть Справочник ""Справочник1""");
	ДобавитьШагВМассивТестов(ВсеТесты,"ЯУдаляюВсеЭлементыСправочника(Парам01Строка)","ЯУдаляюВсеЭлементыСправочника","я удаляю все элементы Справочника ""Справочник1""");
	ДобавитьШагВМассивТестов(ВсеТесты,"ВБазеНетЭлементовСправочника(Парам01Строка)","ВБазеНетЭлементовСправочника","в базе нет элементов Справочника ""Справочник1""");
	ДобавитьШагВМассивТестов(ВсеТесты,"ЯСоздаюСамОдинЭлементСправочника(Парам01Строка)","ЯСоздаюСамОдинЭлементСправочника","я создаю сам один элемент справочника ""Справочник1""");
	ДобавитьШагВМассивТестов(ВсеТесты,"ВБазеПоявилсяХотяБыОдинЭлементСправочника(Парам01Строка)","ВБазеПоявилсяХотяБыОдинЭлементСправочника","В базе появился хотя бы один элемент справочника ""Справочник1""");

	Возврат ВсеТесты;
КонецФункции

Процедура ПередНачаломСценария() Экспорт
	
КонецПроцедуры

Процедура ПередОкончаниемСценария() Экспорт
	
КонецПроцедуры


//в метаданных есть Справочник "Справочник1"
//@ВМетаданныхЕстьСправочник(Парам01Строка)
Процедура ВМетаданныхЕстьСправочник(ВидСправочника) Экспорт
	Нашел = Ложь;
	Для каждого Элем Из Метаданные.Справочники Цикл
		Имя = Элем.Имя;
		Если НРег(Имя) = НРег(ВидСправочника) Тогда
			Нашел = Истина;
			Прервать;
		КонецЕсли;	 
	КонецЦикла;
	
	Ванесса.ПроверитьРавенство(Нашел,Истина,"В метаданных есть вид справочника " + ВидСправочника);
КонецПроцедуры

//я удаляю все элементы Справочника "Справочник1"
//@ЯУдаляюВсеЭлементыСправочника(Парам01Строка)
Процедура ЯУдаляюВсеЭлементыСправочника(ВидСправочника) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Справочник1.Ссылка
		|ИЗ
		|	Справочник.Справочник1 КАК Справочник1";

	Запрос.Текст = СтрЗаменить(Запрос.Текст,"Справочник1",ВидСправочника);	
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СпрОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		СпрОбъект.Удалить();
	КонецЦикла;

КонецПроцедуры

//в базе нет элементов Справочника "Справочник1"
//@ВБазеНетЭлементовСправочника(Парам01Строка)
Процедура ВБазеНетЭлементовСправочника(ВидСправочника) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Справочник1.Ссылка) КАК КолЭлементов
		|ИЗ
		|	Справочник.Справочник1 КАК Справочник1";

	Запрос.Текст = СтрЗаменить(Запрос.Текст,"Справочник1",ВидСправочника);	
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Ванесса.ПроверитьРавенство(ВыборкаДетальныеЗаписи.КолЭлементов,0,"Не должно быть элементов");
	КонецЦикла;
КонецПроцедуры

//я создаю сам один элемент справочника "Справочник1"
//@ЯСоздаюСамОдинЭлементСправочника(Парам01Строка)
Процедура ЯСоздаюСамОдинЭлементСправочника(ВидСправочника) Экспорт
	СпрОбъект = Справочники[ВидСправочника].СоздатьЭлемент();
	СпрОбъект.Наименование = "Созданный элемент 1";
	СпрОбъект.Записать();
КонецПроцедуры

//В базе появился хотя бы один элемент справочника "Справочник1"
//@ВБазеПоявилсяХотяБыОдинЭлементСправочника(Парам01Строка)
Процедура ВБазеПоявилсяХотяБыОдинЭлементСправочника(ВидСправочника) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Справочник1.Ссылка) КАК КолЭлементов
		|ИЗ
		|	Справочник.Справочник1 КАК Справочник1";

	Запрос.Текст = СтрЗаменить(Запрос.Текст,"Справочник1",ВидСправочника);	
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Ванесса.ПроверитьНеРавенство(ВыборкаДетальныеЗаписи.КолЭлементов,0,"В базе должны быть элементы");
	КонецЦикла;
КонецПроцедуры
  