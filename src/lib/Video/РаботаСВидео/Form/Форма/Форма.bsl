&НаКлиенте
Перем ИмяФайлаВидеоТекущегоШага;

&НаКлиенте
Перем ВерсияСкриптаСборкиВидео;


&НаКлиенте
Процедура  ЗаменитьПутиКФайламНаИмяФайла(ПараметрыВидео,Текст)
	Массив = ПараметрыВидео.Ванесса.РазложитьСтрокуВМассивПодстрок(Текст," ");
	НовыйМассив = Новый Массив;
	Для Каждого Стр Из Массив Цикл
		МассивСлешей = ПараметрыВидео.Ванесса.РазложитьСтрокуВМассивПодстрок(Стр,"\");
		Если МассивСлешей.Количество() > 2 Тогда //длинный путь к файлу
			Если Найти(МассивСлешей[МассивСлешей.Количество()-1],".") > 0 Тогда //есть точка в имени файла, т.е. есть расширение
				Стр = МассивСлешей[МассивСлешей.Количество()-1];
				
				Если Прав(Стр,1) = """" Тогда
					Если Лев(Стр,1) <> """" Тогда
						Стр = """" + Стр;//т.к. могло получиться так что мы потеряли кавычку
					КонецЕсли;	 
				КонецЕсли;	 
				
				Если Прав(Стр,1) = "'" Тогда
					Если Лев(Стр,1) <> "'" Тогда
						Стр = "'" + Стр;//т.к. могло получиться так что мы потеряли апостроф
					КонецЕсли;	 
				КонецЕсли;	 
				
				НовыйМассив.Добавить(Стр);
				Продолжить;
			КонецЕсли;	 
		КонецЕсли;	 
		
		НовыйМассив.Добавить(Стр);
	КонецЦикла;	
	
	Текст = "";
	Ном = 0;
	Для Каждого Стр Из НовыйМассив Цикл
		Ном = Ном + 1;
		Текст = Текст + Стр;
		Если Ном < НовыйМассив.Количество() Тогда
			Текст = Текст + " ";
		КонецЕсли;	 
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Функция СоздатьКартинкуИзТекста(ПараметрыВидео,Знач Текст,ТипВыводаТекста,pointsize)
	
	УбратьЛишниеСимволыИзТекста(Текст);
	ЗаменитьПутиКФайламНаИмяФайла(ПараметрыВидео,Текст);
	
	КартинкаТекст     = ПолучитьИмявременногоФайла("png");
	ФайлКартинкаТекст = Новый Файл(КартинкаТекст);
	КартинкаТекст     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлКартинкаТекст.Имя;
	
	ЛогФайл       = ПолучитьИмяВременногоФайла("txt");
	
	Команда = """" + ПараметрыВидео.ЗаписьВидеоКомандаConvert + """ -gravity Center  -background black  -fill white   -pointsize " + pointsize + " -size <screenwidth>x<screenheight>   " + ТипВыводаТекста + ":""<Text>"" " + КартинкаТекст;
	Команда = СтрЗаменить(Команда,"<screenwidth>",Формат(ПараметрыВидео.ЗаписьВидеоЭкранШирина,"ND=10; NG=0"));
	Команда = СтрЗаменить(Команда,"<screenheight>",Формат(ПараметрыВидео.ЗаписьВидеоЭкранВысота,"ND=10; NG=0"));
	Команда = СтрЗаменить(Команда,"<Text>",Текст);
	
	Команда = СтрЗаменить(Команда,Символы.НПП,"");
	
	ПараметрыВидео.Ванесса.Отладка("СоздатьКартинкуИзТекста");
	ПараметрыВидео.Ванесса.Отладка(Команда);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	ЗТ.ЗаписатьСтроку("CHCP 65001"); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();

	КомандаСистемы(ИмяВременногоФайла);
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяВременногоФайла);
	
	Возврат КартинкаТекст;
КонецФункции	

&НаКлиенте
Функция СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,ДлительностьВидео,ДелатьFadeIn = Истина,ДелатьFadeOut = Истина)
	КартинкаВидео     = ПолучитьИмявременногоФайла("mpg");
	ФайлКартинкаВидео = Новый Файл(КартинкаВидео);
	КартинкаВидео     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлКартинкаВидео.Имя;
	
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -loop 1 -i """ + КартинкаТекст + """ -qscale 1 -r 29.97 -t " + ДлительностьВидео + " -y -f mpegts """ + КартинкаВидео + """";
	ПараметрыВидео.Ванесса.Отладка(Команда);
	КомандаСистемы(Команда);
	
	
	
	Если ДелатьFadeIn Тогда
		КартинкаВидеоПереход1     = ПолучитьИмявременногоФайла("mpg");
		ФайлКартинкаВидеоПереход1 = Новый Файл(КартинкаВидеоПереход1);
		КартинкаВидеоПереход1     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлКартинкаВидеоПереход1.Имя;
		Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + КартинкаВидео + " -y -qscale 1 -vf fade=in:0:30 " + КартинкаВидеоПереход1;
		ПараметрыВидео.Ванесса.Отладка(Команда);
		КомандаСистемы(Команда);
	Иначе	
		КартинкаВидеоПереход1 = КартинкаВидео;
	КонецЕсли;	 
	
	
	
	Если ДелатьFadeOut Тогда
		КартинкаВидеоПереход2     = ПолучитьИмявременногоФайла("mpg");
		ФайлКартинкаВидеоПереход2 = Новый Файл(КартинкаВидеоПереход2);
		КартинкаВидеоПереход2     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлКартинкаВидеоПереход2.Имя;
		Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + КартинкаВидеоПереход1 + " -y -qscale 1 -vf fade=out:" + ((ДлительностьВидео-1)*30) +":30 " + КартинкаВидеоПереход2;
		ПараметрыВидео.Ванесса.Отладка(Команда);
		КомандаСистемы(Команда);
		ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаВидеоПереход1);
	Иначе	
		КартинкаВидеоПереход2 = КартинкаВидеоПереход1;
	КонецЕсли;	 
	
	
	
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаВидео);
	
	
	Возврат КартинкаВидеоПереход2;
	
КонецФункции	

&НаКлиенте
Функция ОпределитьДлительностьФайла(ПараметрыВидео,ИмяИсходногоФайла)
	ИмяВременногоФайлаЛог = ПолучитьИмяВременногоФайла("txt");
	Команда = """" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + """ -i " + ИмяИсходногоФайла + " > """ + ИмяВременногоФайлаЛог + """ 2>&1";
	
	КомандаСистемыЧерезBat(ПараметрыВидео,Команда);
	
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяВременногоФайлаЛог,"UTF-8");
	
	Зн = Неопределено;
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Если Найти(Стр,"Duration:") = 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		
		
		Стр = СокрЛП(СтрЗаменить(Стр,"Duration:",""));
		Поз = Найти(Стр,",");
		Стр = Лев(Стр,Поз-1);
		
		Зн = Стр;
		Прервать;
	КонецЦикла;	
	
	Текст.Закрыть();
	
	
	Если Зн = Неопределено Тогда
		ВызватьИсключение "Не смог определить длительность у файла: " + ИмяИсходногоФайла;
	КонецЕсли;	 
	
	КолМилиСек = Сред(Зн,10);
	Зн = Лев(Зн,8);
	МассивСтрок = РазложитьСтрокуВМассивПодстрок(Зн,":");
	КолСек = 0;
	КолСек = КолСек + Число(МассивСтрок[1])*60 + Число(МассивСтрок[2]) + Число(КолМилиСек)/1000;
	
	КолСек = Окр(КолСек,2);
	Возврат КолСек;
КонецФункции	


&НаКлиенте
Процедура ДобавитьMp3КВидео(ПараметрыВидео,ИмяФайлаВидео,Mp3)
	
	ФайлИмяФайлаВидео = Новый Файл(ИмяФайлаВидео);
	Расширение        = ФайлИмяФайлаВидео.Расширение;
	Если Лев(Расширение,1) = "." Тогда
		Расширение = Сред(Расширение,2);
	КонецЕсли;	 
	
	НовоеИмяФайлаВидео     = ПолучитьИмявременногоФайла(Расширение);
	ФайлНовоеИмяФайлаВидео = Новый Файл(НовоеИмяФайлаВидео);
	НовоеИмяФайлаВидео     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлНовоеИмяФайлаВидео.Имя;
	
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + ИмяФайлаВидео + " -i " + Mp3 + " -codec copy -shortest " + НовоеИмяФайлаВидео;
	
	ПараметрыВидео.Ванесса.Отладка(Команда);
	КомандаСистемы(Команда);
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаВидео);
	ИмяФайлаВидео = НовоеИмяФайлаВидео; 
	
КонецПроцедуры

&НаКлиенте
Функция СоздатьИмяФайлаТишины(ПараметрыВидео,Длительность,ПереданноеИмяФайла)
	КаталогДляФайловTTS = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS";
	ИмяВременногоФайлаMp3 = КаталогДляФайловTTS + "\" + ПереданноеИмяФайла + ".mp3";
	
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -f lavfi -i anullsrc=r=44100:cl=stereo -t " + СтрЗаменить(XMLСтрока(Длительность),",",".") + " -ab 192k -acodec libmp3lame " + ИмяВременногоФайлаMp3;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	КомандаСистемы(Команда);
	
	Возврат ИмяВременногоФайлаMp3;
КонецФункции	

&НаКлиенте
Функция ОбъединитьФайлыMP3(ПараметрыВидео,КаталогРаботы,Файл1,Файл2,Файл3)
	ИмяФайлаСписок = КаталогРаботы + "\listmp3.txt";
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаСписок);
	ЗТ = Новый ЗаписьТекста(ИмяФайлаСписок,"windows-1251",,Ложь); 
	
	
	ПромФайл1 = Новый Файл(Файл1);
	ПромФайл2 = Новый Файл(Файл2);
	ЗТ.ЗаписатьСтроку("file '" + ПромФайл1.Имя + "'"); 
	ЗТ.ЗаписатьСтроку("file '" + ПромФайл2.Имя + "'"); 
	Если Файл3 <> Неопределено Тогда
		ПромФайл3 = Новый Файл(Файл3);
		ЗТ.ЗаписатьСтроку("file '" + ПромФайл3.Имя + "'"); 
	КонецЕсли;	 
	
	ЗТ.Закрыть();
	
	
	
	ВременныйMp3 = ПолучитьИмяВременногоФайла("mp3");
	ФайлВременныйMp3 = Новый Файл(ВременныйMp3);
	ВременныйMp3 = КаталогРаботы + "\" + ФайлВременныйMp3.Имя;
	ФайлВременныйMp3 = Новый Файл(ВременныйMp3);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -f concat -i listmp3.txt -c copy " + ФайлВременныйMp3.Имя;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	
	ЗТ.ЗаписатьСтроку(""+ Лев(КаталогРаботы,2)); 
	ЗТ.ЗаписатьСтроку("CD " + КаталогРаботы); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();
	
	КомандаСистемы(ИмяВременногоФайла);
	Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ФайлВременныйMp3.ПолноеИмя) Тогда
		ВызватьИсключение("Команда по объединению mp3 не выполнена! Файл1=" + Файл1 + ", Файл2=" + Файл2 + ", Файл3=" + Файл3);
	КонецЕсли;	 
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(Файл1);
	ПараметрыВидео.Ванесса.КопироватьФайлКомандаСистемы(ВременныйMp3,Файл1);
	
	Возврат Файл1;
КонецФункции	

&НаКлиенте
Функция СоздатьФайлВидеоИзТекста(ПараметрыВидео,Текст,MP3,ФинальноеИмяФайла)
	КартинкаТекст = СоздатьКартинкуИзТекста(ПараметрыВидео,Текст,"caption",80);
	
	ДлительностьВидео = 5;
	Если MP3 <> Неопределено Тогда
		ДлительностьMp3 = Окр(ОпределитьДлительностьФайла(ПараметрыВидео,MP3));
		Если  ДлительностьВидео < (ДлительностьMp3+2) Тогда
			ДлительностьВидео = ДлительностьMp3+2;
		КонецЕсли;	 
	КонецЕсли;	 
	
	ФайлВидео     = СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,ДлительностьВидео);
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаТекст);
	
	
	Если Mp3 <> Неопределено Тогда
		ФайлТишины1 = СоздатьИмяФайлаТишины(ПараметрыВидео,1,ФинальноеИмяФайла + "_silence_1");
		ФайлТишины2 = СоздатьИмяФайлаТишины(ПараметрыВидео,10,ФинальноеИмяФайла +"_silence_2");
		ФайлMp3     = ОбъединитьФайлыMP3(ПараметрыВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS",ФайлТишины1,Mp3,ФайлТишины2);
		ДобавитьMp3КВидео(ПараметрыВидео,ФайлВидео,ФайлMp3)
	Иначе
		ФайлMp3 = СоздатьИмяФайлаТишины(ПараметрыВидео,ДлительностьВидео,ФинальноеИмяФайла + "_silence_0");
		ДобавитьMp3КВидео(ПараметрыВидео,ФайлВидео,ФайлMp3)
	КонецЕсли;	 
	
	
	ФайлФайлВидео = Новый Файл(ФайлВидео);
	ФинальноеИмя  = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФинальноеИмяФайла + ФайлФайлВидео.Расширение;
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ФинальноеИмя);
	
	
	Возврат ФинальноеИмя;
КонецФункции	

&НаКлиенте
Функция ОпределитьЯзыкФичи(ИмяФайла)
	Язык = "ru";
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяФайла,"UTF-8");
	
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Если Лев(СокрЛП(Стр),1) <> "#" Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если Найти(НРег(стр),"language:") = 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		Поз = Найти(Стр,":");
		Язык = СокрЛП(Сред(Стр,Поз+1));
	КонецЦикла;	
	Текст.Закрыть();
	
	Возврат НРег(Язык);
КонецФункции	


&НаКлиенте
Функция ПолучитьСолвоСценарий(Язык)
	Если Язык = "en" Тогда
		Возврат "Scenario";
	КонецЕсли;	 
	
	Возврат "Сценарий";
КонецФункции	

&НаКлиенте
Функция СоздатьЗаголовокСценария(ПараметрыВидео,ИмяСценария,НомерСценария)
	Текст   = ИмяСценария;
	Текст   = СтрЗаменить(Текст,"<","");
	Текст   = СтрЗаменить(Текст,">","");
	
	
	ПолныйПутьКФиче = ПараметрыВидео.МассивСценариевДляВыполнения[0].ПолныйПутьКФиче;
	ЯзыкФичи        = ОпределитьЯзыкФичи(ПолныйПутьКФиче);
	
	СловоСценарий = ПолучитьСолвоСценарий(ЯзыкФичи);
	
	СценарийMP3 = ПолучитьФайлMP3ИзТекста(ПараметрыВидео,"" + СловоСценарий + ". " + Текст,"scen_" + ДобавитьНулей(НомерСценария,3) + "_begin");
	
	Текст   = "" + СловоСценарий + "\n\n " + ИмяСценария;
	
	ФайлВидео   = СоздатьФайлВидеоИзТекста(ПараметрыВидео,Текст,СценарийMP3,"scen_" + ДобавитьНулей(НомерСценария,3) + "_begin");
	
	Возврат ФайлВидео;
КонецФункции

&НаКлиенте
Процедура ДобавитьКрасивыйТег(ЧтоОборачиваем,Стр)
	ЧтоОборачиваемОриг = ЧтоОборачиваем;
	
	ЧтоОборачиваем = "<span underline='single'><b>" + ЧтоОборачиваем + "</b></span>";
	
	Стр = ЧтоОборачиваем + Сред(Стр,СтрДлина(ЧтоОборачиваемОриг)+1);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтрокаВыводаФичи(ПараметрыВидео,ФайлФичи,СтрокаMP3)
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ФайлФичи,"UTF-8");
	
	СтрокаВыводаФичи = "";
	СтрокаMP3 = "";
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Стр = СокрЛП(Стр);
		
		Если Лев(Стр,1) = "#" Тогда
			Продолжить;
		КонецЕсли;	 
		Если Лев(Стр,1) = "@" Тогда
			Продолжить;
		КонецЕсли;	 
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		НРегСтр = НРег(Стр);
		
		Если Найти(НРегСтр,"контекст:") > 0 Тогда
			Прервать;
		КонецЕсли;	 
		Если Найти(НРегСтр,"сценарий:") > 0 Тогда
			Прервать;
		КонецЕсли;	 
		Если Найти(НРегСтр,"scenario:") > 0 Тогда
			Прервать;
		КонецЕсли;	 
		Если Найти(НРегСтр,"background:") > 0 Тогда
			Прервать;
		КонецЕсли;	 
		
		
		Стр = СтрЗаменить(Стр,">","");
		Стр = СтрЗаменить(Стр,"<","");
		
		СтрокаMP3 = СтрокаMP3 + Стр + Символы.ПС;
		
		Если Лев(НРегСтр,11) = "функционал:" Тогда
			ДобавитьКрасивыйТег(Лев(Стр,11),Стр);
		КонецЕсли;	 
		Если Лев(НРегСтр,3) = "как" Тогда
			ДобавитьКрасивыйТег(Лев(Стр,3),Стр);
		КонецЕсли;	 
		Если Лев(НРегСтр,6) = "я хочу" Тогда
			ДобавитьКрасивыйТег(Лев(Стр,6),Стр);
		КонецЕсли;	 
		Если Лев(НРегСтр,5) = "чтобы" Тогда
			ДобавитьКрасивыйТег(Лев(Стр,5),Стр);
		КонецЕсли;	 
		
		
		Если Лев(НРегСтр,3) = "как" Тогда
			СтрокаВыводаФичи = СтрокаВыводаФичи + Символы.ПС;
		КонецЕсли;	 
		
		Если СтрокаВыводаФичи <> "" Тогда
			СтрокаВыводаФичи = СтрокаВыводаФичи + Символы.ПС;
		КонецЕсли;	 
		СтрокаВыводаФичи = СтрокаВыводаФичи + Стр;
	КонецЦикла;	
	
	Текст.Закрыть();
	
	СтрокаВыводаФичи = СтрЗаменить(СтрокаВыводаФичи,Символы.ПС,"\n");
	
	
	
	СтрокаВыводаФичи = "\n\n\n\n\n" + СтрокаВыводаФичи;
	
	ПараметрыВидео.Ванесса.Отладка("СтрокаВыводаФичи:");
	ПараметрыВидео.Ванесса.Отладка(СтрокаВыводаФичи);
	
	Возврат СтрокаВыводаФичи;
КонецФункции	

&НаКлиенте
Процедура СоздатьIntro(ПараметрыВидео)
	Попытка
		ПолныйПутьКФиче = ПараметрыВидео.МассивСценариевДляВыполнения[0].ПолныйПутьКФиче;
	Исключение
		ВызватьИсключение "Не найдено ни одного сценария! Запись видео невозможна!";
	КонецПопытки;	
		
	
	СтрокаMP3 = "";
	СтрокаВыводаФичи = ПолучитьСтрокаВыводаФичи(ПараметрыВидео,ПолныйПутьКФиче,СтрокаMP3);
	
	
	ДлительностьВидео = 6;
	Если ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
		ФайлMp3         = ПолучитьФайлMP3ИзТекста(ПараметрыВидео,СтрокаMP3,"intro");
		ДлительностьMp3 = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ФайлMp3)) + 1;
		
		Если ДлительностьВидео < (ДлительностьMp3+2) Тогда
			ДлительностьВидео = ДлительностьMp3 + 2;
		КонецЕсли;	 
	Иначе	
		ФайлMp3         = СоздатьИмяФайлаТишины(ПараметрыВидео,10,"intro_silence_0");
	КонецЕсли;	 
	
	
	
	
	КартинкаТекст = СоздатьКартинкуИзТекста(ПараметрыВидео,СтрокаВыводаФичи,"pango",35);
	ФайлВидео     = СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,ДлительностьВидео);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаТекст);
	
	
	
	ФайлТишины1 = СоздатьИмяФайлаТишины(ПараметрыВидео,1,"intro_silence_1");
	ФайлТишины2 = СоздатьИмяФайлаТишины(ПараметрыВидео,20,"intro_silence_2");
	ФайлMp3     = ОбъединитьФайлыMP3(ПараметрыВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS",ФайлТишины1,ФайлMp3,ФайлТишины2);
	
	ДобавитьMp3КВидео(ПараметрыВидео,ФайлВидео,ФайлMp3);
	ФайлФайлВидео = Новый Файл(ФайлВидео);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + "intro" + ФайлФайлВидео.Расширение);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКоличествоШагов(МассивСценариевДляВыполнения)
	Кол = 0;
	Для каждого Сценарий Из МассивСценариевДляВыполнения Цикл
		Шаги = Сценарий.Шаги;
		Кол  = Кол + Шаги.Количество();
	КонецЦикла;
	
	Возврат Кол;
КонецФункции	

&НаКлиенте
Процедура СоздатьOutro(ПараметрыВидео)
	
	ПараметрыВидео.Вставить("ФайлМузыки",Неопределено);
	КаталогМузыки = ПараметрыВидео.ЗаписьВидеоКаталогМузыки;
	Если СокрЛП(КаталогМузыки) <> "" Тогда
		Если ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(КаталогМузыки) Тогда
			СписокФайловКаталога = ПолучитьСписокФайловКаталога(ПараметрыВидео,КаталогМузыки);
			Если СписокФайловКаталога.Количество() > 0 Тогда
				ТекДата = Формат(ТекущаяДата(),"DF=dd.MM.yyyy");
				ТекДата = СтрЗаменить(ТекДата," ","");
				ТекДата = СтрЗаменить(ТекДата,":","");
				ТекДата = СтрЗаменить(ТекДата,".","");
				ТекДата = Прав(ТекДата,2);//секунды
				ТекДата = Число(ТекДата);
				ГенераторСлучайныхЧисел = Новый ГенераторСлучайныхЧисел(ТекДата);
				
				ИД_mp3 = ГенераторСлучайныхЧисел.СлучайноеЧисло(0,СписокФайловКаталога.Количество()-1);
				mp3файл = СписокФайловКаталога[ИД_mp3].ПолноеИмя;
				
				
				ПараметрыВидео.Ванесса.Отладка("Был найден файл mp3 <" + mp3файл + ">");
				
				ПараметрыВидео.Вставить("ФайлМузыки",mp3файл);
			КонецЕсли;	 
		КонецЕсли;	 
	КонецЕсли;	 
	
	
	
	
	ПолныйПутьКФиче = ПараметрыВидео.МассивСценариевДляВыполнения[0].ПолныйПутьКФиче;
	
	КолШагов = ПолучитьКоличествоШагов(ПараметрыВидео.МассивСценариевДляВыполнения);
	
	СтрокаMP3 = "";
	Если ПараметрыВидео.ФайлМузыки = Неопределено Тогда
		АудиоТрек = "";
	Иначе
		ФайлMp3 = Новый Файл(ПараметрыВидео.ФайлМузыки);
		АудиоТрек = "\nАудио трек: " + ФайлMp3.ИмяБезРасширения;
	КонецЕсли;	 
	
	Голос = "";
	Если СокрЛП(ПараметрыВидео.ЗаписьВидеоИмяTTS) <> "" Тогда
		Голос = "\nГолос: Microsoft Speech Platform - " + ПараметрыВидео.ЗаписьВидеоИмяTTS;
	КонецЕсли;	 
	СтрокаВыводаФичи = "Видео создано автоматически\n" + ТекущаяДата() + "\n\nКоличество сценариев: " + ПараметрыВидео.МассивСценариевДляВыполнения.Количество() + "\nКоличество шагов: "+КолШагов + "\nВерсия Vanessa-Behavior: " + СтрЗаменить(ПараметрыВидео.Ванесса.Заголовок,"ver","") + "\nВерсия скрипта сборки видео: " + ВерсияСкриптаСборкиВидео + АудиоТрек + Голос;
	
	
	КартинкаТекст = СоздатьКартинкуИзТекста(ПараметрыВидео,СтрокаВыводаФичи,"caption",70);
	ФайлВидео     = СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,5,,Ложь);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаТекст);
	
	ФайлТишины = СоздатьИмяФайлаТишины(ПараметрыВидео,10,"outro_silence");
	ДобавитьMp3КВидео(ПараметрыВидео,ФайлВидео,ФайлТишины);
	
	ФайлФайлВидео = Новый Файл(ФайлВидео);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + "outro" + ФайлФайлВидео.Расширение);
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура УскоритьЗамедлитьФайлВидеоИДобавитьMp3(ПараметрыВидео,ИмяФайлВидео,ИмяФайлMP3,ДлительностьВидео,НоваяДлительностьВидео,ДлительностьMP3)
	ИмяФайлаВидеоВременный     = ПолучитьИмявременногоФайла("mp4");
	ФайлИмяФайлаВидеоВременный = Новый Файл(ИмяФайлаВидеоВременный);
	ИмяФайлаВидеоВременный     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлИмяФайлаВидеоВременный.Имя;
	
	Доля    = НоваяДлительностьВидео/ДлительностьВидео;
	Если Доля > 1 Тогда 
		//значит замедляем
		Доля    = Окр(НоваяДлительностьВидео/ДлительностьВидео,1);
	Иначе	
		//значит ускоряем и доля будет что-то вроде 0.425
		Доля    = Окр(НоваяДлительностьВидео/ДлительностьВидео,3);
		Если Доля < 0.4 Тогда
			Доля = 0.4;//чтобы не ускорять слишком быстро
		КонецЕсли;	 
	КонецЕсли;	 
	
	
	Если НоваяДлительностьВидео = -1 Тогда //значит ничего делать не надо
		ПараметрыВидео.Ванесса.Отладка("Не стал изменять длительность видео " + ИмяФайлВидео);
		ИмяФайлаВидеоВременный = ИмяФайлВидео;
	Иначе	
		ДоляСтр = СтрЗаменить(СтрЗаменить(Строка(Доля),",","."),Символы.НПП,"");
		Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + ИмяФайлВидео + "  -filter:v ""setpts=" + ДоляСтр + "*PTS"" " + ИмяФайлаВидеоВременный;
		
		ПараметрыВидео.Ванесса.Отладка(Команда);
		КомандаСистемы(Команда);
	КонецЕсли;	 
	
	
	
	
	//проконтролируем что длительность видео не меньше чем длительность mp3
	ТекущаяДлительностьВидео = Окр(ОпределитьДлительностьФайла(ПараметрыВидео,ИмяФайлаВидеоВременный));
	Если ТекущаяДлительностьВидео < ДлительностьMP3 Тогда
		НоваяДлительностьВидео = ДлительностьMP3 + 1;
		Доля    = НоваяДлительностьВидео/ТекущаяДлительностьВидео;
		Если Доля > 1 Тогда 
			//значит замедляем
			Доля    = Окр(НоваяДлительностьВидео/ТекущаяДлительностьВидео,1);
		Иначе	
			//значит ускоряем и доля будет что-то вроде 0.425
			Доля    = Окр(НоваяДлительностьВидео/ТекущаяДлительностьВидео,3);
		КонецЕсли;	 
		
		ИмяФайлаВидеоВременный2     = ПолучитьИмявременногоФайла("mp4");
		ФайлИмяФайлаВидеоВременный2 = Новый Файл(ИмяФайлаВидеоВременный2);
		ИмяФайлаВидеоВременный2     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлИмяФайлаВидеоВременный2.Имя;
		
		
		ДоляСтр = СтрЗаменить(СтрЗаменить(Строка(Доля),",","."),Символы.НПП,"");
		Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + ИмяФайлаВидеоВременный + "  -filter:v ""setpts=" + ДоляСтр + "*PTS"" " + ИмяФайлаВидеоВременный2;
		
		ПараметрыВидео.Ванесса.Отладка(Команда);
		КомандаСистемы(Команда);
		
		ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаВидеоВременный);
		
		ИмяФайлаВидеоВременный  = ИмяФайлаВидеоВременный2;
	КонецЕсли;	 
	
	
	
	//ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлВидео);
	//ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ИмяФайлаВидеоВременный,ИмяФайлВидео);

	//добавим звук
	Если ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
		ФайлИмяФайлMP3     = Новый Файл(ИмяФайлMP3);
		ИмяБезРасширения = ФайлИмяФайлMP3.ИмяБезРасширения;
		
		ФайлТишины1 = СоздатьИмяФайлаТишины(ПараметрыВидео,0.5,ИмяБезРасширения + "_silence_1");
		ФайлТишины2 = СоздатьИмяФайлаТишины(ПараметрыВидео,10 + Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ИмяФайлаВидеоВременный)),ИмяБезРасширения +"_silence_2");
		ФайлMp3     = ОбъединитьФайлыMP3(ПараметрыВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS",ФайлТишины1,ФайлИмяФайлMP3.Имя,ФайлТишины2);
		ДобавитьMp3КВидео(ПараметрыВидео,ИмяФайлаВидеоВременный,ФайлMp3)
	Иначе
		//значит нет озвучки голосом
		ФайлИмяФайлВидео = Новый Файл(ИмяФайлВидео);
		ФайлMp3 = СоздатьИмяФайлаТишины(ПараметрыВидео,ДлительностьВидео+5,ФайлИмяФайлВидео.ИмяБезРасширения + "_silence_0");
		ДобавитьMp3КВидео(ПараметрыВидео,ИмяФайлаВидеоВременный,ФайлMp3)
	КонецЕсли;	 
	
	
	Если Прав(НРег(ИмяФайлаВидеоВременный),3) = "mp4" Тогда
		//надо перекодировать в mpg
		НовоеИмяФайлВидео = СтрЗаменить(ИмяФайлаВидеоВременный,"mp4","mpg");
		Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + ИмяФайлаВидеоВременный + " -r 29.97 -qscale 1 -ab 192k -acodec libmp3lame " + НовоеИмяФайлВидео;
		ПараметрыВидео.Ванесса.Отладка(Команда);
		КомандаСистемы(Команда);
		
		ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаВидеоВременный);
		ИмяФайлаВидеоВременный = НовоеИмяФайлВидео;
		
		//надо заменить расширение
		ИмяФайлВидео = СтрЗаменить(ИмяФайлВидео,"mp4","mpg");
	КонецЕсли;	 
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлВидео);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ИмяФайлаВидеоВременный,ИмяФайлВидео);
	
КонецПроцедуры

&НаКлиенте
Функция ПодготовитьСтрокуДляСубтитров(Знач Стр)
	Пока Найти(Стр,"  ") > 0 Цикл
		Стр = СтрЗаменить(Стр,"  "," ");
	КонецЦикла;	
	Стр = СтрЗаменить(Стр,Символы.Таб," ");
	Стр = СтрЗаменить(Стр,Символы.ВТаб," ");
	
	Возврат Стр;
КонецФункции	

&НаКлиенте
Процедура ДобавитьСубтитрыКВидео(ПараметрыВидео,ИмяФайлаШага,Шаг,ДлительностьMP3)
	ИмяФайлаСубтитров = СтрЗаменить(ИмяФайлаШага,".mp4",".srt");
	ИмяФайлаСубтитров = СтрЗаменить(ИмяФайлаСубтитров,".mpg",".srt");
	
	//ДлительностьВидео = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ИмяФайлаШага));
	ДлительностьТитра = ДлительностьMP3 + 1;
	Если ДлительностьТитра > 59 Тогда
		ДлительностьТитра = 59;
	КонецЕсли;	 
	
	ДлительностьТитраСтр = Строка(ДлительностьТитра);
	Если СтрДлина(ДлительностьТитраСтр) = 1 Тогда
		ДлительностьТитраСтр = "0" + ДлительностьТитраСтр;
	КонецЕсли;	 
	
	ЗТ = Новый ЗаписьТекста(ИмяФайлаСубтитров,"UTF-8",,Ложь); 
	ЗТ.ЗаписатьСтроку("1"); 
	ЗТ.ЗаписатьСтроку("00:00:00,300 --> 00:00:" + ДлительностьТитраСтр + ",300"); 
	
	Если ЗначениеЗаполнено(Шаг.ТекстИнструкция) Тогда
		ТекстТитра = Шаг.ТекстИнструкция;
	Иначе	
		ТекстТитра = Шаг.Имя;
	КонецЕсли;	 	
	
	ЗТ.ЗаписатьСтроку(ПодготовитьСтрокуДляСубтитров(ТекстТитра)); 
	ЗТ.Закрыть();
	
	
	ВременныйФайлВидео     = ПолучитьИмявременногоФайла("mpg");
	ФайлВременныйФайлВидео = Новый Файл(ВременныйФайлВидео);
	ВременныйФайлВидео     = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + ФайлВременныйФайлВидео.Имя;
	
	
	ФайлСабов = Новый Файл(ИмяФайлаСубтитров);
	
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + ИмяФайлаШага + " -r 29.97 -qscale 1 -vf subtitles=" + ФайлСабов.Имя + ":force_style=""FontName=Consolas"" -acodec copy " + ВременныйФайлВидео;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	//КомандаСистемы(Команда);
	
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	ЗТ.ЗаписатьСтроку(""+ Лев(ФайлСабов.Путь,2)); 
	ЗТ.ЗаписатьСтроку("cd " + ФайлСабов.Путь); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();
	
	КомандаСистемы(ИмяВременногоФайла);
	Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ВременныйФайлВидео) Тогда
		Стр = "Не получилось добавить субтитры к файлу <" + ИмяФайлаШага + ">";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;	 
	
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаШага);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяВременногоФайла);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаСубтитров);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ВременныйФайлВидео,ИмяФайлаШага);	
	
КонецПроцедуры

&НаКлиенте
Процедура СобратьВидеоВставку(ПараметрыВидео,ИмяФайлаШага,ТекстВставки,НомерСценария,НомерШага)
	
	
	//ТекстВставки = Шаг.ЗначенияПараметров[0].Значение.Значение;
	//СтрИмяФайла = "break_scen" + ДобавитьНулей(НомерСценария,3) + "_step" + ДобавитьНулей(НомерШага,3);
	//СтрИмяФайла = "break_scen" + ИмяФайлаШага;
	ПромФайл    = Новый Файл(ИмяФайлаШага);
	СтрИмяФайла = "breakscen_" + ПромФайл.ИмяБезРасширения;
	
	ДлительностьВидео = 5;
	
	Если ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
		ФайлMp3         = ПолучитьФайлMP3ИзТекста(ПараметрыВидео,ТекстВставки,СтрИмяФайла);
		ДлительностьMp3 = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ФайлMp3)) + 1;
	Иначе
		ФайлMp3         = СоздатьИмяФайлаТишины(ПараметрыВидео,ДлительностьВидео,СтрИмяФайла + "_silence_0");
		ДлительностьMp3 = ДлительностьВидео;
	КонецЕсли;	 
	
	
	Если ДлительностьВидео < (ДлительностьMp3+1) Тогда
		ДлительностьВидео = ДлительностьMp3 + 1;
	КонецЕсли;	 
	
	
	КартинкаТекст = СоздатьКартинкуИзТекста(ПараметрыВидео,ТекстВставки,"caption",80);
	ФайлВидео     = СоздатьВидеоНужнойДлительностиИзКартинки(ПараметрыВидео,КартинкаТекст,ДлительностьВидео);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(КартинкаТекст);
	
	
	
	ФайлТишины1 = СоздатьИмяФайлаТишины(ПараметрыВидео,1,СтрИмяФайла + "_silence_1");
	ФайлТишины2 = СоздатьИмяФайлаТишины(ПараметрыВидео,20,СтрИмяФайла + "_silence_2");
	ФайлMp3     = ОбъединитьФайлыMP3(ПараметрыВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS",ФайлТишины1,ФайлMp3,ФайлТишины2);
	
	ДобавитьMp3КВидео(ПараметрыВидео,ФайлВидео,ФайлMp3);
	ФайлФайлВидео = Новый Файл(ФайлВидео);
	
	
	ИмяФайлаШага = СтрЗаменить(ИмяФайлаШага,"mp4","mpg");//так реально создан mpg и дальше нам надо mpg
	
	
	
	//ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + СтрИмяФайла + ФайлФайлВидео.Расширение);
	
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяФайлаШага);
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(ФайлВидео,ИмяФайлаШага);	
КонецПроцедуры

&НаКлиенте
Функция СоздатьДанныеФайла(Имя,ПолныйПуть,Приоритет)
	Зн = Новый Структура;
	Зн.Вставить("Имя",Имя);
	Зн.Вставить("ПолныйПуть",ПолныйПуть);
	Зн.Вставить("Приоритет",Приоритет);
	
	Возврат Зн;
КонецФункции	


&НаКлиенте
Функция ПолучитьСписокФайловКаталога(ПараметрыВидео,Каталог)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	ИмяЛога            = ПолучитьИмяВременногоФайла("txt");
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	
	Команда = "dir /a:-d /b > """ + ИмяЛога + """";
	ПараметрыВидео.Ванесса.Отладка(Команда);
	
	
	ЗТ.ЗаписатьСтроку(""+ Лев(Каталог,2)); 
	ЗТ.ЗаписатьСтроку("CD "+ Каталог); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();
	
	КомандаСистемы(ИмяВременногоФайла);
	Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяЛога) Тогда
		ВызватьИсключение("Команда по получению списка файлов каталога не выполнена! Каталог = " + Каталог);
	КонецЕсли;	 
	
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяВременногоФайла);
	
	
	МассивФайлов = Новый Массив;
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяЛога,"UTF-8");
	
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Если Прав(НРег(Стр),3) = "mp3" Тогда
			Файл = Новый Файл(Каталог + "\" + Стр);
			МассивФайлов.Добавить(Файл);
		КонецЕсли;	 
	КонецЦикла;	
	
	Текст.Закрыть();
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяЛога);
	
	
	Возврат МассивФайлов;
КонецФункции	


&НаКлиенте
Функция ПолучитьMP3МузыкиНужнойДлительности(ПараметрыВидео,ДлительностьВидео)
	ДлительностьMP3   = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ПараметрыВидео.ФайлМузыки));
	Если ДлительностьMP3 = 0 Тогда
		ВызватьИсключение "ДлительностьMP3=0. Файл=" + ПараметрыВидео.ФайлМузыки;
	КонецЕсли;	
	
	Ном = 0;
	
	ФайлMp3 = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS\Music.mp3";
	ПараметрыВидео.Ванесса.КопироватьФайлКомандаСистемы(ПараметрыВидео.ФайлМузыки,ФайлMp3);
	
	Пока ДлительностьMP3 < ДлительностьВидео Цикл
		Ном = Ном + 1;
		ФайлТишины  = СоздатьИмяФайлаТишины(ПараметрыВидео,1,"music_" + XMLСтрока(Ном));
		ФайлMp3     = ОбъединитьФайлыMP3(ПараметрыВидео,ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS",ФайлMp3,ФайлТишины,ФайлMp3);
		
		ДлительностьMP3   = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ФайлMp3));
	КонецЦикла;	
	
	Возврат ФайлMp3;
	
КонецФункции	

&НаКлиенте
Процедура СобратьФинальноеВидеоИзЧастей(ПараметрыВидео,МассивЧастей)
	
	ГдеИскать = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов;
	ТаблицаФайлов = Новый Массив;
	
	ДанныеФайла = СоздатьДанныеФайла("intro.mpg",ГдеИскать + "\intro.mpg",0);
	ТаблицаФайлов.Добавить(ДанныеФайла);
	
	
	
	СписокФайлов = Новый СписокЗначений;
	Для каждого Файл Из МассивЧастей Цикл
		
		Если НРег(Файл.Имя) = НРег("intro.mpg") Тогда
			Продолжить;
		КонецЕсли;	 
		Если НРег(Файл.Имя) = НРег("outro.mpg") Тогда
			Продолжить;
		КонецЕсли;	 
		
		
		СписокФайлов.Добавить(Файл,Файл.Имя);
		
	КонецЦикла;
	
	СписокФайлов.СортироватьПоПредставлению();
	
	Для каждого ЗначениеСписка Из СписокФайлов Цикл
		Файл        = ЗначениеСписка.Значение;
		ДанныеФайла = СоздатьДанныеФайла(Файл.Имя,Файл.ПолноеИмя,1);
		ТаблицаФайлов.Добавить(ДанныеФайла);
	КонецЦикла;
	
	
	ДанныеФайла = СоздатьДанныеФайла("outro.mpg",ГдеИскать + "\outro.mpg",2);
	ТаблицаФайлов.Добавить(ДанныеФайла);
	
	
	ИмяФайлаСписка = ПолучитьИмяВременногоФайла("txt");
	ФайлИмяФайлаСписка = Новый Файл(ИмяФайлаСписка);
	ИмяФайлаСписка = ГдеИскать + "\" + ФайлИмяФайлаСписка.Имя;
	
	
	ЗТ = Новый ЗаписьТекста(ИмяФайлаСписка,"windows-1251",,Истина); 
	Для каждого СтрТаблицаФайлов Из ТаблицаФайлов Цикл
		ЗТ.ЗаписатьСтроку("file " + СтрТаблицаФайлов.Имя); 
	КонецЦикла;
	ЗТ.Закрыть();
	
	
	СобранноеВидео1 = ГдеИскать + "\mix1.mpg";

	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	//ЗТ.ЗаписатьСтроку("CHCP 65001"); 
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -f concat -i " + ИмяФайлаСписка + " -c copy " + СобранноеВидео1;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	
	ЗТ.ЗаписатьСтроку(""+ Лев(ГдеИскать,2)); 
	ЗТ.ЗаписатьСтроку("CD "+ ГдеИскать); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();
	
	КомандаСистемы(ИмяВременногоФайла);
	Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(СобранноеВидео1) Тогда
		ВызватьИсключение("Команда ffmpeg по слиянию всех видео не выполнена!");
	КонецЕсли;	 
	
	
	
	
	
	
	
	//добавим водяной знак
	ИмяФайлаВодянойЗнак = ПараметрыВидео.ЗаписьВидеоФайлВодянойЗнак;
	Если СокрЛП(ИмяФайлаВодянойЗнак) = "" Тогда
		КомандаВодянойЗнак = "";
	Иначе
		Файл_watermark = Новый Файл(ИмяФайлаВодянойЗнак);
		Копияwatermark = ГдеИскать + "\" + Файл_watermark.Имя;
		ПараметрыВидео.Ванесса.КопироватьФайлКомандаСистемы(ИмяФайлаВодянойЗнак,Копияwatermark);
		
		КомандаВодянойЗнак = " -i " +  Файл_watermark.Имя + " -filter_complex overlay=main_w-overlay_w-10:main_h-overlay_h-10 ";
	КонецЕсли;	 
	
	СобранноеВидео3 = ГдеИскать + "\mix3.mpg";
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i mix1.mpg " + КомандаВодянойЗнак + " -qscale 1 -af ""aresample=async=1:first_pts=0"" -codec:a libmp3lame -q:a 0 " + СобранноеВидео3;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	ЗТ.ЗаписатьСтроку(""+ Лев(ГдеИскать,2)); 
	ЗТ.ЗаписатьСтроку("CD "+ ГдеИскать); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();
	
	КомандаСистемы(ИмяВременногоФайла);
	Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(СобранноеВидео3) Тогда
		Если КомандаВодянойЗнак = "" Тогда
			ВызватьИсключение("Команда ffmpeg по перекодированю звука не выполнена!");
		Иначе	
			ВызватьИсключение("Команда ffmpeg по добавлению водяной отметки и перекодированию звука не выполнена!");
		КонецЕсли;	 
	КонецЕсли;	 
	
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяВременногоФайла);
	
	
	
	
	
	
	
	
	
	
	
	СобранноеВидео4 = ГдеИскать + "\mix4.mp4";
	
	
	//конверт в mp4
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
	ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
	//ЗТ.ЗаписатьСтроку("CHCP 65001"); 
	//Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + СобранноеВидео3 + " -qscale 1 -codec:a libmp3lame -qscale:a 2 " + СобранноеВидео4;
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + СобранноеВидео3 + " -qscale 1 " + СобранноеВидео4;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	
	ЗТ.ЗаписатьСтроку(""+ Лев(ГдеИскать,2)); 
	ЗТ.ЗаписатьСтроку("CD "+ ГдеИскать); 
	ЗТ.ЗаписатьСтроку(""+ Команда); 
	ЗТ.Закрыть();
	
	КомандаСистемы(ИмяВременногоФайла);
	Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(СобранноеВидео4) Тогда
		ВызватьИсключение("Команда ffmpeg по кодированию в mp4 не выполнена!");
	КонецЕсли;	 
	
	
	
	
	
	
	//добавляем музыку
	Если ПараметрыВидео.ФайлМузыки <> Неопределено Тогда
		ДлительностьВидео = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,СобранноеВидео4)) + 1;
		mp3файл = ПолучитьMP3МузыкиНужнойДлительности(ПараметрыВидео,ДлительностьВидео);
		
		
		
		
		
		СобранноеВидео5 = ГдеИскать + "\mix5.mp4";
		
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("cmd");
		ЗТ = Новый ЗаписьТекста(ИмяВременногоФайла,"UTF-8",,Истина); 
		//ЗТ.ЗаписатьСтроку("CHCP 65001"); 
		Команда = ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -i " + mp3файл + " -i " + СобранноеВидео4 + " -filter_complex ""[0:a]aformat=sample_fmts=fltp:sample_rates=44100:channel_layouts=stereo,volume=0.15[a1]; [1:a]aformat=sample_fmts=fltp:sample_rates=44100:channel_layouts=stereo,volume=1[a2]; [a1][a2]amerge,pan=stereo:c0<c0+c2:c1<c1+c3[out]"" -map 1:v -map ""[out]"" -c:v copy  -acodec libmp3lame -b:a 320k -shortest " + СобранноеВидео5;
		ПараметрыВидео.Ванесса.Отладка(Команда);
		
		
		ЗТ.ЗаписатьСтроку(""+ Лев(ГдеИскать,2)); 
		ЗТ.ЗаписатьСтроку("CD "+ ГдеИскать); 
		ЗТ.ЗаписатьСтроку(""+ Команда); 
		ЗТ.Закрыть();
		
		
		КомандаСистемы(ИмяВременногоФайла);
		Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(СобранноеВидео5) Тогда
			ВызватьИсключение("Команда ffmpeg по добавлению mp3 в ролик не выполнена!");
		КонецЕсли;	 
		
	Иначе	
		СобранноеВидео5 = СобранноеВидео4;
	КонецЕсли;	 
	
	
	
	ФинальноеИмя = ПараметрыВидео.КаталогOutputИнструкцияВидео + "\result.mp4";
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ФинальноеИмя);	
	
	ФайлФинальноеИмя = Новый Файл(ФинальноеИмя);
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ФайлФинальноеИмя.Путь) Тогда
		ПараметрыВидео.Ванесса.СоздатьКаталогКомандаСистемы(ФайлФинальноеИмя.Путь);
	КонецЕсли;	 
	
	ПараметрыВидео.Ванесса.ПереместитьФайлКомандаСистемы(СобранноеВидео5,ФинальноеИмя);	
	
	Сообщить("Создан файл: " + ФинальноеИмя);
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоШагПервыйВВеткеДерева(ПараметрыВидео,Шаг,МассивВставок)
	МассивСтрокДереваДанныеФормы = ПараметрыВидео.МассивСтрокДереваДанныеФормы;
	ИДСтрокиШага                 = Шаг.СтрокаШага;
	СтрокаШага                   = МассивСтрокДереваДанныеФормы[ИДСтрокиШага];
	
	РодительШага                 = СтрокаШага.ПолучитьРодителя();
	Если РодительШага.Сценарий = Истина Тогда
		Возврат Ложь;
	КонецЕсли;	 
	
	Если (РодительШага.Область <> Истина) и (РодительШага.ШагСценарий <> Истина) Тогда //только эти шаги идут как видеовставки
		Возврат Ложь;
	КонецЕсли;	 
	
	
	
	
	ЭлементыРодителя             = РодительШага.ПолучитьЭлементы();
	Индекс                       = ЭлементыРодителя.Индекс(СтрокаШага);
	Если Индекс = 0 Тогда
		//добавим верхние шаги тоже, если они являются ветками
		ШагРодитель = Новый Структура;
		ШагРодитель.Вставить("СтрокаШага",РодительШага.ИдСтроки);
		ШагРодитель.Вставить("Имя",РодительШага.Имя);
		ЭтоШагПервыйВВеткеДерева(ПараметрыВидео,ШагРодитель,МассивВставок);
		
		
		//ИмяШагаВеткиДерева = РодительШага.Имя;
		МассивВставок.Добавить(РодительШага.Имя);
		Возврат Истина;
	КонецЕсли;	 
	
	Возврат Ложь;
КонецФункции	

&НаКлиенте
Процедура ЗавершитьЗаписьВидео(ПараметрыВидео) Экспорт
	
	СоздатьФайлыTTS(ПараметрыВидео);
	
	СоздатьIntro(ПараметрыВидео);
	СоздатьOutro(ПараметрыВидео);
	
	
	МассивЧастей = Новый Массив;
	
	КолШагов  = ПолучитьКоличествоШагов(ПараметрыВидео.МассивСценариевДляВыполнения);
	ТекИДШага = 0;
	
	
	НомерСценария = -1;
	МассивСценариевДляВыполнения = ПараметрыВидео.МассивСценариевДляВыполнения;
	Для каждого Сценарий Из МассивСценариевДляВыполнения Цикл
		НомерСценария = НомерСценария + 1;
		Шаги = Сценарий.Шаги;
		
		ИмяСценария = Сценарий.Имя;
		
		
		ИмяФайлаЗаголовкаСценария = СоздатьЗаголовокСценария(ПараметрыВидео,ИмяСценария,НомерСценария);
		
		ФайлЧасти = Новый Файл(ИмяФайлаЗаголовкаСценария);
		МассивЧастей.Добавить(ФайлЧасти);
		
		
		НомерШага = -1;
		Для каждого Шаг Из Шаги Цикл
			НомерШага = НомерШага + 1;
			ТекИДШага = ТекИДШага + 1;
			
			Если Шаг.ЭтоШагКонтекста Тогда
				Продолжить;
			КонецЕсли;	 
			
			ПараметрыВидео.Вставить("ТекИД_СценарияВМассиве",НомерСценария);
			ПараметрыВидео.Вставить("ТекИД_ШагаВМассиве",НомерШага);
			
			ИмяФайлаШага          = ПолучитьИмяФайлаВидеоШага(ПараметрыВидео);
			
			//ТекстВставкиВеткиДерева = "";
			МассивВставок = Новый Массив;
			Если ЭтоШагПервыйВВеткеДерева(ПараметрыВидео,Шаг,МассивВставок) Тогда
				СчетчикВставок = 0;
				Для Каждого ТекстВставкиВеткиДерева Из МассивВставок Цикл
					СчетчикВставок = СчетчикВставок + 1;
					ИмяФайлаШагаВетки = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\" + СтрЗаменить(ИмяФайлаШага,"_main.","_ins" + СчетчикВставок + ".");
					СобратьВидеоВставку(ПараметрыВидео,ИмяФайлаШагаВетки,ТекстВставкиВеткиДерева,НомерСценария,НомерШага);
					ФайлЧасти = Новый Файл(ИмяФайлаШагаВетки);
					МассивЧастей.Добавить(ФайлЧасти);
				КонецЦикла;	
			КонецЕсли;	 
			
			Прогресс = Цел((ТекИДШага / КолШагов)*100);
			Состояние("Обработка видеофайлов шагов",Прогресс,"Сценарий №" + (НомерСценария+1) + ", шаг №" + (НомерШага+1) + ". " + Шаг.Имя);
			
			
			
			
			
			ФайлИмяФайлаВидеоШага = Новый Файл(ИмяФайлаШага);
			ИмяMp3Шага            = ФайлИмяФайлаВидеоШага.ИмяБезРасширения + ".mp3";//файл имеет такое же имя
			
			ИмяФайлаШага          = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\"    + ИмяФайлаШага;
			ИмяMp3Шага            = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS\" + ИмяMp3Шага;
			
			Если ПараметрыВидео.Ванесса.ЭтоПрерывающйВидеоШаг(Шаг) Тогда
				СобратьВидеоВставку(ПараметрыВидео,ИмяФайлаШага,Шаг.ЗначенияПараметров[0].Значение.Значение,НомерСценария,НомерШага);
			Иначе
				//надо масштабировать файл видео, чтобы он и звук правильно соотносились
				ДлительностьВидео = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ИмяФайлаШага));
				Если ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
					ДлительностьMP3   = Цел(ОпределитьДлительностьФайла(ПараметрыВидео,ИмяMp3Шага));
				Иначе	
					ДлительностьMP3 = 5;//непонятно подо что подгонять длительность шага если не было озвучки
				КонецЕсли;	 
				
				Если ДлительностьВидео < (ДлительностьMP3 + 1) Тогда
					//значит надо замедлить видео
					НоваяДлительностьВидео = ДлительностьMP3 + 1;
					УскоритьЗамедлитьФайлВидеоИДобавитьMp3(ПараметрыВидео,ИмяФайлаШага,ИмяMp3Шага,ДлительностьВидео,НоваяДлительностьВидео,ДлительностьMP3);
				ИначеЕсли ДлительностьВидео > (ДлительностьMP3 + 2) И Шаг.МасштабироватьВВидео Тогда
					//значит надо ускорить видео
					НоваяДлительностьВидео = ДлительностьMP3 + 2;
					УскоритьЗамедлитьФайлВидеоИДобавитьMp3(ПараметрыВидео,ИмяФайлаШага,ИмяMp3Шага,ДлительностьВидео,НоваяДлительностьВидео,ДлительностьMP3);
				Иначе
					//значит не меняем длительность видео, передаём -1
					УскоритьЗамедлитьФайлВидеоИДобавитьMp3(ПараметрыВидео,ИмяФайлаШага,ИмяMp3Шага,ДлительностьВидео,-1,ДлительностьMP3);
				КонецЕсли;	 
				
				ДобавитьСубтитрыКВидео(ПараметрыВидео,ИмяФайлаШага,Шаг,ДлительностьMP3);
			КонецЕсли;	 
			
			
			
			
			ФайлЧасти = Новый Файл(ИмяФайлаШага);
			МассивЧастей.Добавить(ФайлЧасти);
			
		КонецЦикла;
		
		
		
	КонецЦикла;	
	
	
	СобратьФинальноеВидеоИзЧастей(ПараметрыВидео,МассивЧастей);
	
КонецПроцедуры

&НаКлиенте
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено)
	
	Результат = Новый Массив;
	
	// для обеспечения обратной совместимости
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
		
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Результат.Добавить(Подстрока);
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Результат.Добавить(Строка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

&НаКлиенте
Процедура ОбработатьСтрокуПоСловарю(ПараметрыВидео,ИсходнаяСтрока)
	Если СокрЛП(ПараметрыВидео.ЗаписьВидеоСловарьЗамен) = "" Тогда
		Возврат;
	КонецЕсли;	
	
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ПараметрыВидео.ЗаписьВидеоСловарьЗамен) Тогда
		Сообщить("Не найден файл словаря замен! <" + ПараметрыВидео.ЗаписьВидеоСловарьЗамен + ">");
		ВызватьИсключение "Не найден файл словаря замен! <" + ПараметрыВидео.ЗаписьВидеоСловарьЗамен + ">";
	КонецЕсли;	 
	
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ПараметрыВидео.ЗаписьВидеоСловарьЗамен,"UTF-8");
	
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Если СокрЛП(Стр) = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		Массив = РазложитьСтрокуВМассивПодстрок(Стр,"|");
		
		ИсходнаяСтрока = СтрЗаменить(ИсходнаяСтрока,Массив[0],Массив[1]);
	КонецЦикла;	
	
	Текст.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура УбратьЛишниеСимволыИзТекста(Текст)
	Текст = СтрЗаменить(Текст,Символы.Таб," ");
	Текст = СтрЗаменить(Текст,Символы.ВТаб," ");
	
	Пока Найти(Текст,"  ") > 0 Цикл
		Текст = СтрЗаменить(Текст,"  "," ");
	КонецЦикла;	 
КонецПроцедуры

&НаКлиенте
Процедура УбратьНачалоСтроки(Текст,НачалоСтроки)
	Стр = НРег(НачалоСтроки + " """);//далее идёт параметр
	Если (Лев(НРег(Текст),СтрДлина(Стр)) = Стр) Тогда
		Текст = Сред(Текст,СтрДлина(Стр)+1);
	КонецЕсли;	 
	
	Стр = НРег(НачалоСтроки + " '");//далее идёт параметр
	Если (Лев(НРег(Текст),СтрДлина(Стр)) = Стр) Тогда
		Текст = Сред(Текст,СтрДлина(Стр)+1);
	КонецЕсли;	 
КонецПроцедуры

&НаКлиенте
Процедура УбратьЧастьСлужебныхШаговКоторыеОтвечаютЗаНаборТекста(Текст)
	УбратьНачалоСтроки(Текст,"и я пишу");
	УбратьНачалоСтроки(Текст,"и я добавляю строку");
КонецПроцедуры

&НаКлиенте
Функция ПолучитьВременныйФайлШаг(ПараметрыВидео,Знач Текст)
	ОбработатьСтрокуПоСловарю(ПараметрыВидео,Текст);
	
	УбратьЛишниеСимволыИзТекста(Текст);
	
	УбратьЧастьСлужебныхШаговКоторыеОтвечаютЗаНаборТекста(Текст);
	
	
	Текст = СтрЗаменить(Текст,"\n","");
	Текст = СтрЗаменить(Текст,"\"," ");
	Текст = СтрЗаменить(Текст,"|"," ");
	Текст = Текст + ".";
	
	
	ИмяФайла = ПолучитьИмяВременногоФайла("txt");
	ЗТ = Новый ЗаписьТекста(ИмяФайла,"windows-1251",,Ложь); 
	ЗТ.ЗаписатьСтроку(Текст); 
	ЗТ.Закрыть();
	
	Возврат ИмяФайла;
КонецФункции	

&НаКлиенте
Функция ЗагрузитьФайлВстроку(ИмяФайла)
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяФайла,"UTF-8");
	
	Стр = Текст.Прочитать();
	
	Текст.Закрыть();
	
	Возврат Стр;
КонецФункции	

&НаКлиенте
Функция ПолучитьФайлMP3ИзТекста(ПараметрыВидео,Текст,ИмяФайла)
	Если НЕ ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
		Возврат Неопределено;
	КонецЕсли;	 
	
	//получение wav
	ВременныйФайлШаг = ПолучитьВременныйФайлШаг(ПараметрыВидео,Текст);
	
	AudioEngine         = ПараметрыВидео.ЗаписьВидеоПутьКДвижкуTTS;
	КаталогДляФайловTTS = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS";
	ffmpeg              = ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg;
	
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(КаталогДляФайловTTS) Тогда
		ПараметрыВидео.Ванесса.СоздатьКаталогКомандаСистемы(КаталогДляФайловTTS);
	КонецЕсли;	 
	
	ЛогФайл = ПолучитьИмяВременногоФайла("txt");
	
	ИмяWAV = КаталогДляФайловTTS + "\" + ИмяФайла + ".wav";
	//Команда = """" + AudioEngine + """ -f " + ВременныйФайлШаг + " -w " + ИмяWav +  " -n Elena > 111.txt 2>&1";
	Команда = """" + AudioEngine + """ -f " + ВременныйФайлШаг + " -w " + ИмяWAV +  " -fr 44 -bt 16 -ch 2 -s 2  -n " + ПараметрыВидео.ЗаписьВидеоИмяTTS + " > """ + ЛогФайл + """ 2>&1";
	ПараметрыВидео.Ванесса.Отладка(Команда);
	
	КомандаСистемыЧерезBat(ПараметрыВидео,Команда);
	
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяWAV) Тогда
		Стр = "Не получилось создать файл TTS <" + ИмяWAV + ">!";
		Стр = Стр + Символы.ПС + ЗагрузитьФайлВстроку(ЛогФайл);
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;	 
	
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ВременныйФайлШаг);
	
	
	
	//получение mp3
	ИмяMP3 = СтрЗаменить(ИмяWav,"wav","mp3");
	Если ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяMP3) Тогда
		ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяMP3);
	КонецЕсли;	 
	Команда = "" + ffmpeg + " -i " + ИмяWav + " -vn -ar 44100 -ac 2 -ab 192k -f mp3 -af ""volume=1.15"" " + ИмяMP3;
	ПараметрыВидео.Ванесса.Отладка(Команда);
	
	КомандаСистемы(Команда);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяWav);
	
	Возврат ИмяMP3;
КонецФункции	

&НаКлиенте
Процедура СоздатьФайлыTTS(ПараметрыВидео) Экспорт
	КаталогДляФайловTTS = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов + "\TTS";
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(КаталогДляФайловTTS) Тогда
		ПараметрыВидео.Ванесса.СоздатьКаталогКомандаСистемы(КаталогДляФайловTTS);
	КонецЕсли;	 
	
	Если НЕ ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
		Возврат;
	КонецЕсли;	 
	
	
	AudioEngine = ПараметрыВидео.ЗаписьВидеоПутьКДвижкуTTS;
	//Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(AudioEngine) Тогда
	//	Стр = "Программа для создания TTS не найдена! <" + AudioEngine +">";
	//	Сообщить(Стр);
	//	ВызватьИсключение Стр;
	//КонецЕсли;
	
	Если СокрЛП(ПараметрыВидео.ЗаписьВидеоИмяTTS) = "" Тогда
		Стр = "Не указано имя TTS!";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;
	
	
	ffmpeg = ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg;
	Если СокрЛП(ffmpeg) = "" Тогда
		Стр = "Не указана команда ffmpeg!";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;	 
	
	КолШагов  = ПолучитьКоличествоШагов(ПараметрыВидео.МассивСценариевДляВыполнения);
	ТекИДШага = 0;
	
	
	НомерСценария = -1;
	МассивСценариевДляВыполнения = ПараметрыВидео.МассивСценариевДляВыполнения;
	Для каждого Сценарий Из МассивСценариевДляВыполнения Цикл
		НомерСценария = НомерСценария + 1;
		Шаги = Сценарий.Шаги;
		
		
		НомерШага = -1;
		Для каждого Шаг Из Шаги Цикл
			НомерШага = НомерШага + 1;
			ТекИДШага = ТекИДШага + 1;
			Если Шаг.ЭтоШагКонтекста Тогда
				Продолжить;
			КонецЕсли;	 
			
			Если ЗначениеЗаполнено(Шаг.ТекстИнструкция) Тогда
				Текст = Шаг.ТекстИнструкция;
			Иначе	
				Текст = Шаг.Имя;
			КонецЕсли;	 	
			
			
			Если ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
				Прогресс = Цел((ТекИДШага / КолШагов)*100);
				Состояние("Создание файлов TTS",Прогресс,"Сценарий №" + (НомерСценария+1) + ", шаг №" + (НомерШага+1) + ". " + Шаг.Имя);
			КонецЕсли;	 
			
			
			ИмяMP3 = ПолучитьФайлMP3ИзТекста(ПараметрыВидео,Текст,"scen_" + ДобавитьНулей(НомерСценария,3) + "_step_" + ДобавитьНулей(НомерШага,3) + "_main");
			
			
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ДобавитьНулей(ЗначениеЧисло,КолНулей)
	Стр = СтрЗаменить(Строка(ЗначениеЧисло),Символы.НПП,"");
	
	Пока СтрДлина(Стр) < КолНулей Цикл
		Стр = "0" + Стр;
	КонецЦикла;
	
	Возврат Стр;
КонецФункции	

&НаКлиенте
Функция ПолучитьИмяФайлаВидеоШага(ПараметрыВидео)
	Если ПараметрыВидео.ЭтоИнициализация Тогда
		Стр = "init.mp4";
	Иначе	
		Стр = "scen_" + ДобавитьНулей(ПараметрыВидео.ТекИД_СценарияВМассиве,3) + "_step_" + ДобавитьНулей(ПараметрыВидео.ТекИД_ШагаВМассиве,3) + "_main.mp4";
	КонецЕсли;	 
	
	
	Возврат Стр;
КонецФункции	

&НаКлиенте
Процедура Инициализация(ПараметрыВидео) Экспорт
	ПараметрыВидео.Ванесса.Объект.ИдетЗаписьВидео = Ложь;	
	
	Каталог = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов;
	
	Если СокрЛП(Каталог) = "" Тогда
		ВызватьИсключение "Каталог ЗаписьВидеоКаталогДляВременныхФайлов <" + Каталог + "> не указан!";
	КонецЕсли;	 
	
	Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(Каталог) Тогда
		
		ПараметрыВидео.Ванесса.СоздатьКаталогКомандаСистемы(Каталог);
		
		Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(Каталог) Тогда
			Сообщить("Каталог ЗаписьВидеоКаталогДляВременныхФайлов <" + Каталог + "> не существует!");
			ВызватьИсключение "Каталог ЗаписьВидеоКаталогДляВременныхФайлов <" + Каталог + "> не существует!";
		КонецЕсли;	 
	КонецЕсли;	 
	
	Если СокрЛП(ПараметрыВидео.ЗаписьВидеоКомандаConvert) = "" Тогда
		Стр = "Не указана команда Convert!";
		Сообщить(Стр);
		ВызватьИсключение Стр;
	КонецЕсли;	 
	
	
	Если НЕ Установлен_ImageMagic(ПараметрыВидео) Тогда
		ВызватьИсключение "Не установлен ImageMagic (convert.exe)";
	КонецЕсли;	 
	
	//запишем специальный шаг видео, чтобы VLC проинициализировался (init.mp4)
	ПараметрыВидео.Ванесса.Отладка("Инициализация записи видео...");
	
	НачатьЗаписьВидеоОдногоШага(ПараметрыВидео);
	
	//проверим что появился файл
	СчетчикПроверок     = 0;
	МаксСчетчикПроверок = 30;
	ТекДат = ТекущаяДата();
	Пока Истина Цикл
		СчетчикПроверок = СчетчикПроверок + 1;
		
		Если (СчетчикПроверок > МаксСчетчикПроверок) и (ТекущаяДата() >= (ТекДат+МаксСчетчикПроверок)) Тогда
			ВызватьИсключение "Не найден файл <" + ПараметрыВидео.ТекущийФайлВидео + ">";
		КонецЕсли;	 
		
		
		Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ПараметрыВидео.ТекущийФайлВидео) Тогда
			ПараметрыВидео.Ванесса.Sleep(1);
			Продолжить;
		Иначе
			Прервать;
		КонецЕсли;	 
		
		
	КонецЦикла;
	
	//остановим запись
	ОстановитьЗаписьВидеоОдногоШага(ПараметрыВидео);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаписьВидеоОдногоШага(ПараметрыВидео) Экспорт
	Если Не ПараметрыВидео.ЭтоИнициализация Тогда
		Если ПараметрыВидео.Ванесса.ЭтоПрерывающйВидеоШаг(ПараметрыВидео.ТекШаг) Тогда
			Возврат;
		КонецЕсли;	 
	КонецЕсли;	 
	
	ПараметрыВидео.Ванесса.Объект.ИдетЗаписьВидео = Истина;
	
	
	КомандаНачатьЗаписьВидео = ПараметрыВидео.ЗаписьВидеоКомандаНачатьЗаписьВидео;
	
	//fix для 32 битной винды
	Поз_screen = Найти(КомандаНачатьЗаписьВидео,"screen");
	Если Поз_screen > 0 Тогда
		СтрокаVLC = Лев(КомандаНачатьЗаписьВидео,Поз_screen-1);
		СтрокаVLC = СтрЗаменить(СтрокаVLC,"""","");
		Если Найти(СтрокаVLC," (x86)") > 0 Тогда
			ИмяФайлаVLC = СтрокаVLC;
			Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайлаVLC) Тогда
				ИмяФайлаVLC = СтрЗаменить(ИмяФайлаVLC," (x86)","");
				Если ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайлаVLC) Тогда //значит VLC лежит в другом каталоге
					КомандаНачатьЗаписьВидео = """" + ИмяФайлаVLC + """ " + Сред(КомандаНачатьЗаписьВидео,Поз_screen);
				КонецЕсли;	 
			КонецЕсли;	 
		КонецЕсли;	 
	КонецЕсли;	 
	
	
	
	Если СокрЛП(ПараметрыВидео.ЗаписьВидеоФайлКурсораМышки) <> "" Тогда
		КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,"<screenmouseimage>",ПараметрыВидео.ЗаписьВидеоФайлКурсораМышки);
	Иначе
		КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,":screen-mouse-image=""<screenmouseimage>""","");
	КонецЕсли;	 
	
	
	КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,"<screenwidth>",Формат(ПараметрыВидео.ЗаписьВидеоЭкранШирина,"ND=10; NG=0"));
	КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,"<screenheight>",Формат(ПараметрыВидео.ЗаписьВидеоЭкранВысота,"ND=10; NG=0"));
	
	
	
	ИмяФайлаВидео             = ПараметрыВидео.ЗаписьВидеоКаталогДляВременныхФайлов  + "\" + ПолучитьИмяФайлаВидеоШага(ПараметрыВидео);
	ПараметрыВидео.Вставить("ТекущийФайлВидео",ИмяФайлаВидео);
	КомандаНачатьЗаписьВидео  = СтрЗаменить(КомандаНачатьЗаписьВидео,"<dst>",ИмяФайлаВидео);
	ИмяФайлаВидеоТекущегоШага = ИмяФайлаВидео;
	
	
	КолКадров = ПараметрыВидео.ЗаписьВидеоКоличествоКадров;
	Если КолКадров = 0 Тогда
		ВызватьИсключение "Не указано количество кадров!";
	КонецЕсли;	 
	КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,"<fps>",Строка(КолКадров));
	
	КомандаНачатьЗаписьВидео = СтрЗаменить(КомандаНачатьЗаписьВидео,Символы.НПП,"");
	
	ПараметрыВидео.Ванесса.Отладка(КомандаНачатьЗаписьВидео);
	
	ПараметрыВидео.Ванесса.Объект.ИдетЗаписьВидео = Истина;
	
	ПараметрыВидео.Ванесса.TASKKILL("vlc.exe");
	ПараметрыВидео.Ванесса.ВыполнитьКомандуОСБезПоказаЧерногоОкна(КомандаНачатьЗаписьВидео,0);
	
	
	//ждём чтобы файл появился на диске
	СчетчикПроверок     = 0;
	МаксСчетчикПроверок = 20;
	ТекДат = ТекущаяДата();
	Пока Истина Цикл
		СчетчикПроверок = СчетчикПроверок + 1;
		
		Если (СчетчикПроверок > МаксСчетчикПроверок) и (ТекущаяДата() >= (ТекДат+МаксСчетчикПроверок)) Тогда
			ВызватьИсключение "Не найден файл <" + ИмяФайлаВидео + ">";
		КонецЕсли;	 
		
		
		Если НЕ ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайлаВидео) Тогда
			ПараметрыВидео.Ванесса.Sleep(1);
			Продолжить;
		Иначе
			Прервать;
		КонецЕсли;	 
		
		
	КонецЦикла;
	
	
КонецПроцедуры


&НаКлиенте
Функция ПолучитьАдресСокета(Знач Стр)
	Поз = Найти(Стр,"--rc-host=");
	Стр = Сред(Стр,Поз);
	Стр = СтрЗаменить(Стр,"--rc-host=","");
	
	Поз = Найти(Стр," ");
	Стр = Лев(Стр,Поз-1);
	Стр = СокрЛП(Стр);
	
	Возврат Стр;
КонецФункции	

&НаКлиенте
Процедура ПроверитьЧтоФайлВидеоШагаНеНулевогоРазмера(ПараметрыВидео,ИмяФайла,КолСек)
	//ToDo сделать для ассинхронных вызовов
	Если ПараметрыВидео.ЕстьПоддержкаАсинхронныхВызовов Тогда
		Возврат;
	КонецЕсли;	 
	
	ЦелеваяДата = ТекущаяДата() + КолСек;
	Пока Истина Цикл
		
		Файл = Новый Файл(ИмяФайла);
		Если Файл.Размер() > 1024 Тогда
			//Предупреждение("!!!!",3);
			Прервать;
		КонецЕсли;	 
		
		Если ЦелеваяДата <= ТекущаяДата() Тогда
			Прервать;
		КонецЕсли;	 
		
		ПараметрыВидео.Ванесса.Sleep(1);
		
		
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьЗаписьВидеоОдногоШага(ПараметрыВидео) Экспорт
	
	Если Не ПараметрыВидео.Ванесса.Объект.ИдетЗаписьВидео Тогда
		Возврат;
	КонецЕсли;	 
	
	//ПроверитьЧтоФайлВидеоШагаНеНулевогоРазмера(ПараметрыВидео,ИмяФайлаВидеоТекущегоШага,10);
	
	АдресСокета = ПолучитьАдресСокета(ПараметрыВидео.ЗаписьВидеоКомандаНачатьЗаписьВидео);
	
	КомандаОстановитьЗапись = "oscript " + ПараметрыВидео.КаталогИнструментов + "\tools\onescript\StopVideoRec.os """ + АдресСокета + """";
	
	ПараметрыВидео.Ванесса.Отладка("КомандаОстановитьЗапись=" + КомандаОстановитьЗапись);
	
	ПараметрыВидео.Ванесса.Объект.ИдетЗаписьВидео = Ложь;	
	
	Рез = ПараметрыВидео.Ванесса.ВыполнитьКомандуОСБезПоказаЧерногоОкна(КомандаОстановитьЗапись);
	Если Рез <> 0 Тогда
		Сообщить("Команда ОстановитьЗаписьВидео вернула код возврата = " + Рез);
		ВызватьИсключение "Команда ОстановитьЗаписьВидео вернула код возврата = " + Рез;
	КонецЕсли;	 
	
	//надо дать vlc закрыться
	МаксКолПопыток = 10;
	КолПопыток     = 0;
	ТекДат = ТекущаяДата();
	Пока Истина Цикл
		КолПопыток = КолПопыток + 1;
		
		МассивPID = ПараметрыВидео.Ванесса.ПолучитьМассивPIDПроцессов("vlc.exe");
		Если МассивPID.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;	 
		
		ПараметрыВидео.Ванесса.Sleep(1);
		
		Если (КолПопыток >= МаксКолПопыток) и (ТекущаяДата() >= (ТекДат + МаксКолПопыток)) Тогда
			Прервать;
		КонецЕсли;	 
		
	КонецЦикла;	
		
	
КонецПроцедуры

&НаКлиенте
Функция ВТекстеЕстьСтрока(Лог,ЧтоИщем)
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(Лог,"UTF-8");
	
	Нашли = Ложь;
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Если Найти(Стр,ЧтоИщем)  > 0 Тогда
			Нашли = Истина;
			Прервать;
		КонецЕсли;	 
		
	КонецЦикла;	
	
	Текст.Закрыть();
	
	Возврат Нашли;
КонецФункции	

&НаКлиенте
Функция Установлен_ffmpeg(ПараметрыВидео)
	Лог = ПолучитьИмяВременногоФайла("txt");
	Команда = "" + ПараметрыВидео.ЗаписьВидеоКомандаFfmpeg + " -version > """ + Лог + """";
	ПараметрыВидео.Ванесса.Отладка(Команда);
	КомандаСистемы(Команда);
	
	
	Возврат ВТекстеЕстьСтрока(Лог,"ffmpeg version");
КонецФункции	

&НаКлиенте
Процедура КомандаСистемыЧерезBat(ПараметрыВидео,Команда)
	ИмяВременногоBat = ПолучитьИмяВременногоФайла("bat");
	//ЗТ = Новый ЗаписьТекста(ИмяВременногоBat,"windows-1251",,Ложь); 
	//ЗТ.Закрыть();
	ЗТ = Новый ЗаписьТекста(ИмяВременногоBat,"UTF-8",,Истина); 
	ЗТ.Записать(Команда);
	ЗТ.Закрыть();
	
	КомандаСистемы(ИмяВременногоBat);
	ПараметрыВидео.Ванесса.УдалитьФайлыКомандаСистемы(ИмяВременногоBat);
КонецПроцедуры

&НаКлиенте
Функция Установлен_ImageMagic(ПараметрыВидео)
	Лог = ПолучитьИмяВременногоФайла("txt");
	Команда = """" + ПараметрыВидео.ЗаписьВидеоКомандаConvert + """ -version > """ + Лог + """ 2>&1";
	ПараметрыВидео.Ванесса.Отладка(Команда);
	//КомандаСистемы(Команда);
	КомандаСистемыЧерезBat(ПараметрыВидео,Команда);
	
	
	Возврат ВТекстеЕстьСтрока(Лог,"Version:");
КонецФункции	

&НаКлиенте
Функция Установлен_ГолосовойДвижок(ПараметрыВидео)
	Лог = ПолучитьИмяВременногоФайла("txt");
	Команда = """" + ПараметрыВидео.ЗаписьВидеоПутьКДвижкуTTS + """ -g > """ + Лог + """";
	ПараметрыВидео.Ванесса.Отладка(Команда);
	//КомандаСистемы(Команда);
	КомандаСистемыЧерезBat(ПараметрыВидео,Команда);
	
	
	Возврат ВТекстеЕстьСтрока(Лог,"1:");
КонецФункции	

&НаКлиенте
Функция Установлен_VLC(ПараметрыВидео)
	Нашли = Ложь;
	
	ИмяФайла1 = "C:\Program Files (x86)\VideoLAN\VLC\NEWS.txt";
	ИмяФайла2 = "C:\Program Files\VideoLAN\VLC\NEWS.txt";
	
	ИмяФайла = ИмяФайла1;
	Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайла) Тогда
		ИмяФайла = ИмяФайла2; 
	КонецЕсли;	 
	
	Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайла) Тогда
		Сообщить("Не найдена установка VLC!");
		Возврат Ложь;
	КонецЕсли;	 
	
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяФайла,"UTF-8");
	
	Пока Истина Цикл
		Стр = Текст.ПрочитатьСтроку();
		Если Стр = Неопределено Тогда
			Прервать;
		КонецЕсли;	 
		
		Поз = Найти(Стр,"2.1.5");
		Если Поз > 0 Тогда
			Нашли = Истина;
			Прервать;
		КонецЕсли;	 
	КонецЦикла;	
	
	Текст.Закрыть();
	
	Возврат Нашли;
КонецФункции	

&НаКлиенте
Процедура ПроверитьЧтоЗаписьВидеоБудетРаботать(ПараметрыВидео) Экспорт
	БылиОшибки = Ложь;
	
	Если Не ПараметрыВидео.Ванесса.УстановленOneScript() Тогда
		Сообщить("Не установлен oscript!");
		БылиОшибки = Истина;
	Иначе
		Сообщить("oscript - OK");
	КонецЕсли;	
	
	Если Не Установлен_VLC(ПараметрыВидео) Тогда
		Сообщить("Не установлен VLC 2.1.5!");
		БылиОшибки = Истина;
	Иначе
		Сообщить("VLC 2.1.5 - OK");
	КонецЕсли;	
	
	Если Не Установлен_ffmpeg(ПараметрыВидео) Тогда
		Сообщить("Не установлен ffmpeg!");
		БылиОшибки = Истина;
	Иначе
		Сообщить("ffmpeg - OK");
	КонецЕсли;	
	
	Если Не Установлен_ImageMagic(ПараметрыВидео) Тогда
		Сообщить("Не установлен ImageMagic (convert.exe)!");
		БылиОшибки = Истина;
	Иначе
		Сообщить("ImageMagic - OK");
	КонецЕсли;	
	
	Если ПараметрыВидео.ЗаписьВидеоДелатьНаложениеTTS Тогда
		Если Не Установлен_ГолосовойДвижок(ПараметрыВидео) Тогда
			Сообщить("Не установлен ГолосовойДвижок!");
			БылиОшибки = Истина;
		Иначе
			Сообщить("ГолосовойДвижок - OK");
			
			ФайлMp3 = ПолучитьФайлMP3ИзТекста(ПараметрыВидео,"тестовый текст","test");
			Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ФайлMp3) Тогда
				Сообщить("Не получилось создать тестовый MP3");
				БылиОшибки = Истина;
			Иначе	
				Сообщить("Создать тестовый mp3 - OK");
			КонецЕсли;	 
			
			
		КонецЕсли;	
	КонецЕсли;	 
	
	Если ПараметрыВидео.ЗаписьВидеоПодсвечиватьАктивныеЭлементыФорм Тогда
		ИмяФайла = ПараметрыВидео.КаталогИнструментов + "\tools\VideoTools\FrameShow.exe";
		
		Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайла) Тогда
			Сообщить("Файл <" + ИмяФайла + "> не найден!");
			БылиОшибки = Истина;
		Иначе	
			Сообщить("Подсвет рамки - OK");
		КонецЕсли;	 
	КонецЕсли;	 
	
	Если ПараметрыВидео.ЗаписьВидеоПеремещатьКурсорМышкиКАктивномуЭлементуФормы Тогда
		ИмяФайла = ПараметрыВидео.КаталогИнструментов + "\tools\VideoTools\MouseMove.exe";
		
		Если Не ПараметрыВидео.Ванесса.ФайлСуществуетКомандаСистемы(ИмяФайла) Тогда
			Сообщить("Файл <" + ИмяФайла + "> не найден!");
			БылиОшибки = Истина;
		Иначе	
			Сообщить("Передвижение курсора мышки - OK");
		КонецЕсли;	 
	КонецЕсли;	 
	                               
	Если БылиОшибки Тогда
		Сообщить("Необходимо установить программное обеспечение. Подробности тут: https://github.com/silverbulleters/vanessa-behavior/blob/develop/README.md");
		Сообщить("Включите флаг <Включить отладочные сообщения> чтобы посмотреть выполняемые команды.");
	Иначе
		Сообщить("Все проверки пройдены.");
	КонецЕсли;	 
	
КонецПроцедуры


ВерсияСкриптаСборкиВидео = "1.04";