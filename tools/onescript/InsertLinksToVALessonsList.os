#Использовать v8runner
#Использовать logos

Перем Лог;

Процедура ВыполнитьОбработку(ПутьКСпискуУроковVA, ПутьКСпискуУроковИзРелиза)
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ПутьКСпискуУроковИзРелиза, "UTF-8");
	ТекстФайла = Текст.Прочитать();
	Текст.Закрыть();        
	
	ДанныеСсылок = Новый Соответствие;
	
	МассивСтрок = СтрРазделить(ТекстФайла, Символы.ПС);
	Для Сч = 0 По МассивСтрок.Количество()-1 Цикл
		Стр = МассивСтрок[Сч];
		
		Если Найти(Стр, "[(Видео)]") = 0 Тогда
			Продолжить;
		КонецЕсли;	   
		
		Значения = Новый Структура;
		Значения.Вставить("PDF", "");
		Значения.Вставить("MD", "");
		Значения.Вставить("Видео", "");
		
		ПолучитьЗначенияИзСтроки(Стр, Значения);
		
		Если НЕ ЗначениеЗаполнено(Значения.PDF) Тогда
			ВызватьИсключение "Не найдена ссылка PDF в строке: " + МассивСтрок[Сч];
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(Значения.MD) Тогда
			ВызватьИсключение "Не найдена ссылка MD в строке: " + МассивСтрок[Сч];
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(Значения.Видео) Тогда
			ВызватьИсключение "Не найдена ссылка Видео в строке: " + МассивСтрок[Сч];
		КонецЕсли;
		
		Файл = Новый Файл(Значения.PDF);
		ДанныеСсылок.Вставить(НРег(Файл.ИмяБезРасширения), Значения);
		
	КонецЦикла;	
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ПутьКСпискуУроковVA, "UTF-8");
	ТекстФайла = Текст.Прочитать();
	Текст.Закрыть();
	
	МассивСтрок = СтрРазделить(ТекстФайла, Символы.ПС);
	Для Сч = 0 По МассивСтрок.Количество()-1 Цикл
		Стр = МассивСтрок[Сч];
		
		Поз = Найти(Стр, "[(Видео)]");
		Если Поз > 0 Тогда
			Стр = СокрП(Лев(Стр, Поз - 1));
		КонецЕсли;
		НачалоСтроки = Стр;
		
		Поз = Найти(Стр, "[Запустить урок]");
		Если Поз = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Имя = СокрЛП(Сред(Стр, Поз + СтрДлина("[Запустить урок]")));
		Имя = СтрЗаменить(Имя, "(", "");
		Имя = СтрЗаменить(Имя, ")", ""); 
		
		Если Найти(Стр, "(MD)") > 0 ИЛИ Найти(Стр, "(PDF)") > 0 ИЛИ Найти(Стр, "(Видео)") > 0 ИЛИ Найти(Стр, "(Video)") > 0 Тогда
			ВызватьИсключение "В файл уже выполнялась вставка ссылок. Строка: " + МассивСтрок[Сч];
		КонецЕсли;	
		
		Стр = СокрП(Стр);
		
		Если Прав(Стр, 1) <> ")" Тогда
			ВызватьИсключение "Ожидалась скобка "")"" в конце строки: " + МассивСтрок[Сч];
		КонецЕсли;
		
		ЗначенияСсылок = ДанныеСсылок[НРег(Имя)];
		Если ЗначенияСсылок = Неопределено Тогда
			Сообщить("Не найдены ссылки для строки: " + МассивСтрок[Сч]);
		КонецЕсли;	
		
		МассивСтрок[Сч] = СокрП(НачалоСтроки) + " " + СтрШаблон("[(Видео)](%1) [(PDF)](%2) [(MD)](%3)", ЗначенияСсылок.Видео, ЗначенияСсылок.PDF, ЗначенияСсылок.MD);
		
	КонецЦикла;	
	
	ТекстФайла = СтрСоединить(МассивСтрок, Символы.ПС);
	
	ЗТ = Новый ЗаписьТекста(ПутьКСпискуУроковVA,"UTF-8",, Ложь); 
	ЗТ.Записать(ТекстФайла); 
	ЗТ.Закрыть();
	
КонецПроцедуры

Процедура ПолучитьЗначенияИзСтроки(Знач Стр, Значения)
	Поз = Найти(Стр, "[");
	Стр = Сред(Стр, Поз);
	
	Пока Найти(Стр, "]") > 0 Цикл
		Поз = Найти(Стр, "]");
		ЛевЧасть = Лев(Стр, Поз);
		ПраваяЧасть = Сред(Стр, Поз + 1);
		
		ЛевЧасть = СтрЗаменить(ЛевЧасть, "[", "");
		ЛевЧасть = СтрЗаменить(ЛевЧасть, "]", "");
		ЛевЧасть = СтрЗаменить(ЛевЧасть, "(", "");
		ЛевЧасть = СтрЗаменить(ЛевЧасть, ")", "");
		ИмяЗначения = СокрЛП(ЛевЧасть);
		
		
		Поз = Найти(ПраваяЧасть, ")");
		Ссылка = Лев(ПраваяЧасть, Поз);
		Ссылка = СтрЗаменить(Ссылка, "(", "");
		Ссылка = СтрЗаменить(Ссылка, ")", "");
		
		Стр = Сред(ПраваяЧасть, Поз + 1);
		
		Значения.Вставить(ИмяЗначения, Ссылка);
	КонецЦикла;	
	
КонецПроцедуры

Лог = Логирование.ПолучитьЛог("vb.compile.log");
Лог.УстановитьУровень(УровниЛога.Отладка);


Если АргументыКоманднойСтроки.Количество() = 0 Тогда
	Лог.Ошибка("Не переданы параметры!");
ИначеЕсли АргументыКоманднойСтроки.Количество() <> 2 Тогда
	Лог.Ошибка("Скрипт принимает два параметра!");
Иначе
	ВыполнитьОбработку(АргументыКоманднойСтроки[0], АргументыКоманднойСтроки[1]);
КонецЕсли;

