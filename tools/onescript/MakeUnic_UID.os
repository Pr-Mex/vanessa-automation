Процедура ОбработатьДанные(Путь, ЧастьСтроиПоиска)
	Результат = Новый Соответствие;
	
	МассивДублей = Новый Массив;
	
	Файлы = НайтиФайлы(Путь,"*.xml",Истина);
	Для Каждого Файл Из Файлы Цикл
		Текст = Новый ЧтениеТекста;
		Текст.Открыть(Файл.ПолноеИмя,"UTF-8");
		
		Пока Истина Цикл
			Стр = Текст.ПрочитатьСтроку();
			Если Стр = Неопределено Тогда
				Прервать;
			КонецЕсли;	 
			
			СтрокаПоиска = ЧастьСтроиПоиска;
			
			Поз = Найти(Стр,СтрокаПоиска);
			Если Поз > 0 Тогда
				Стр = Сред(Стр,Поз + СтрДлина(СтрокаПоиска)+1);
				Стр = Лев(Стр,СтрДлина(Стр)-2);
				Если Результат[Стр] <> Неопределено Тогда
					МассивДублей.Добавить(Новый Структура("ИмяФайла,UID",Файл.ПолноеИмя,Стр));
					Продолжить;
				КонецЕсли;	 
				
				Результат.Вставить(Стр,Файл.ПолноеИмя);
			КонецЕсли;	 
		КонецЦикла;	
		
		Текст.Закрыть();
		
	КонецЦикла;	
	
	Для Каждого ДанныеФайла Из МассивДублей Цикл
		ИмяФайла = ДанныеФайла.ИмяФайла;
		Сообщить(ДанныеФайла.ИмяФайла);
		
		Текст = Новый ЧтениеТекста;
		Текст.Открыть(ИмяФайла,"UTF-8");
		
		ТекстФайла = Текст.Прочитать();
		СтрокаПоиска = ЧастьСтроиПоиска + """%1"">";
		СтрокаПоиска = СтрЗаменить(СтрокаПоиска,"%1",ДанныеФайла.UID);
		
		СтрокаЗамены = ЧастьСтроиПоиска + """%1"">";
		СтрокаЗамены = СтрЗаменить(СтрокаЗамены,"%1",Строка(Новый УникальныйИдентификатор));
		
		Если Найти(ТекстФайла,СтрокаПоиска) = 0 Тогда
			ВызватьИсключение "В файле <" + ИмяФайла + "> не найдена строка <" + СтрокаПоиска + ">";
		КонецЕсли;	 
		
		ТекстФайла = СтрЗаменить(ТекстФайла,СтрокаПоиска,СтрокаЗамены);
		
		Текст.Закрыть();
		
		УдалитьФайлы(ИмяФайла);
		
		ЗТ = Новый ЗаписьТекста(ИмяФайла,"UTF-8",,Истина); 
		ЗТ.Записать(ТекстФайла); 
		ЗТ.Закрыть();
	КонецЦикла;	 
КонецПроцедуры 


Если АргументыКоманднойСтроки.Количество() = 0 Тогда
	ВызватьИсключение "Не переданы параметры!";
ИначеЕсли АргументыКоманднойСтроки.Количество() > 1 Тогда
	ВызватьИсключение "Скрипт принимает только один параметр!";
КонецЕсли;

ОбработатьДанные(АргументыКоманднойСтроки[0], "<ExternalDataProcessor uuid=");
ОбработатьДанные(АргументыКоманднойСтроки[0], "<Form uuid=");
	
Сообщить("Обработка закончена.");
