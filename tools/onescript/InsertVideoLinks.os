#Использовать v8runner
#Использовать logos

Перем Лог;

Процедура ВставитьВHTMLСсылкуВидео(ИмяФайла, СсылкаВидео)
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяФайла, "UTF-8");
	ТекстФайла = Текст.Прочитать();
	Текст.Закрыть();
	
	ТекстФайла = СтрЗаменить(ТекстФайла, "></", ">" + Символы.ПС  + "</");
	ТекстФайла = СтрЗаменить(ТекстФайла, "><", ">" + Символы.ПС  + "<");
	
	МассивСтрок = СтрРазделить(ТекстФайла, Символы.ПС);
	Для Сч = 0 По МассивСтрок.Количество()-1 Цикл
		Если МассивСтрок[Сч] = "<main>" Тогда
			МассивСтрок.Вставить(Сч+1, "</div>");
			МассивСтрок.Вставить(Сч+1, СтрШаблон("<a href=""%1"">Ссылка на видео</a>", СсылкаВидео));
			МассивСтрок.Вставить(Сч+1, "<div style=""text-align:center;font-size: 30px"">");
			Прервать;
		КонецЕсли;	
		//МассивСтрок[Сч] = "  " + МассивСтрок[Сч];
	КонецЦикла;	
	
	ТекстФайла = СтрСоединить(МассивСтрок, Символы.ПС);
	
	ЗТ = Новый ЗаписьТекста(ИмяФайла,"UTF-8",, Ложь); 
	ЗТ.Записать(ТекстФайла); 
	ЗТ.Закрыть();
	
КонецПроцедуры

Процедура ВставитьВMDСсылкуВидео(ИмяФайла, СсылкаВидео)
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ИмяФайла, "UTF-8");
	ТекстФайла = Текст.Прочитать();
	Текст.Закрыть();
	
	МассивСтрок = СтрРазделить(ТекстФайла, Символы.ПС);
	МассивСтрок.Вставить(0, СтрШаблон("##### [Ссылка на видео](%1)", СсылкаВидео));
	
	ТекстФайла = СтрСоединить(МассивСтрок, Символы.ПС);
	
	ЗТ = Новый ЗаписьТекста(ИмяФайла,"UTF-8",, Ложь); 
	ЗТ.Записать(ТекстФайла); 
	ЗТ.Закрыть();
	
КонецПроцедуры

Процедура ВыполнитьОбработкуHTML(Путь, ПутьКДаннымВидео)
	
	ДанныеВидео = ДанныеВидео(ПутьКДаннымВидео);
	
	Файлы = НайтиФайлы(Путь, "*.html", Истина);
	Для Каждого Файл Из Файлы Цикл
		ИмяБезРасширения = Файл.ИмяБезРасширения;
		
		Нашли = Ложь;
		Для Каждого ТекВидео Из ДанныеВидео Цикл
			Если НРег(ТекВидео.ИмяФайлаБезРасширения) = НРег(ИмяБезРасширения) Тогда
				Нашли = Истина;
				Прервать;
			КонецЕсли;	
		КонецЦикла;      
		
		Если НЕ Нашли Тогда
			ВызватьИсключение "Не найдены данные видео по файлу: " + Файл.ПолноеИмя;
		КонецЕсли;
		
		ВставитьВHTMLСсылкуВидео(Файл.ПолноеИмя, ТекВидео.СсылкаИнтернет);
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВыполнитьОбработкуMD(Путь, ПутьКДаннымВидео)
	ДанныеВидео = ДанныеВидео(ПутьКДаннымВидео);
	
	Файлы = НайтиФайлы(Путь, "*.MD", Истина);
	Для Каждого Файл Из Файлы Цикл
		ИмяБезРасширения = Файл.ИмяБезРасширения;
		Если НРег(ИмяБезРасширения) = "index" Тогда
			Продолжить;
		КонецЕсли;	
		
		Нашли = Ложь;
		Для Каждого ТекВидео Из ДанныеВидео Цикл
			Если НРег(ТекВидео.ИмяФайлаБезРасширения) = НРег(ИмяБезРасширения) Тогда
				Нашли = Истина;
				Прервать;
			КонецЕсли;	
		КонецЦикла;      
		
		Если НЕ Нашли Тогда
			ВызватьИсключение "Не найдены данные видео по файлу: " + Файл.ПолноеИмя;
		КонецЕсли;
		
		ВставитьВMDСсылкуВидео(Файл.ПолноеИмя, ТекВидео.СсылкаИнтернет);
		
	КонецЦикла;	
КонецПроцедуры

Функция ДанныеВидео(ИмяФайла)
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(ИмяФайла, "UTF-8");
	Данные = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	Возврат Данные;
	
КонецФункции	

Процедура ВыполнитьОбработку(Тип, ПутьККаталогу, ПутьКДаннымВидео)
	
	
	Если НРег(Тип) = "html" Тогда
		ВыполнитьОбработкуHTML(ПутьККаталогу, ПутьКДаннымВидео);
	ИначеЕсли НРег(Тип) = "md" Тогда
		ВыполнитьОбработкуMD(ПутьККаталогу, ПутьКДаннымВидео);
	Иначе
		ВызватьИсключение СтрШаблон("Передан неверный тип <%1>", Тип);
	КонецЕсли;	
	
КонецПроцедуры

Лог = Логирование.ПолучитьЛог("vb.compile.log");
Лог.УстановитьУровень(УровниЛога.Отладка);


Если АргументыКоманднойСтроки.Количество() = 0 Тогда
	Лог.Ошибка("Не переданы параметры!");
ИначеЕсли АргументыКоманднойСтроки.Количество() <> 3 Тогда
	Лог.Ошибка("Скрипт принимает три параметра!");
Иначе
	ВыполнитьОбработку(АргументыКоманднойСтроки[0], АргументыКоманднойСтроки[1], АргументыКоманднойСтроки[2]);
КонецЕсли;

